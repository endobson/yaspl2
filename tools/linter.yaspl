#:module (tools linter)
#:import {
  (bytes)
  (compiler)
  (data indexed-set)
  (either)
  (io)
  (list)
  (module-header-lint)
  (module-name)
  (module-signature)
  (prim)
  (set)
  (source-language)
  (unused-bindings)
  (unused-local-bindings)
}
(export
  (#:types LintResults)
  (#:values lint-module write-lint-results)
  (#:patterns lint-results))
(types
  (define-type LintResults
    (lint-results
      [filename : Bytes]
      [unused-bindings : UnusedBindings]
      [unused-local-bindings : UnusedLocalBindings]
      [unused-dependencies : (List ModName)]
      [header-violations : ModuleHeaderLintViolations])))


(define (indent [bytes : Bytes]) : Bytes
  (bytes-append (varargs list #"  " bytes)))
(define (indent2 [bytes : Bytes]) : Bytes
  (bytes-append (varargs list #"    " bytes)))
(define (indent3 [bytes : Bytes]) : Bytes
  (bytes-append (varargs list #"      " bytes)))
(define (indent4 [bytes : Bytes]) : Bytes
  (bytes-append (varargs list #"        " bytes)))


(define (write-results-section [title : Bytes] [unused : (List Bytes)] [output : OutputPort]) : Void
  (let ([write-out-line (lambda ([x : Bytes]) (write-line x output))])
    (case unused
      [(empty) (void)]
      [unused
       (begin
         (write-out-line (indent title))
         (for-each write-out-line (map indent2 unused)))])))

(define (write-unused-imports-section [imports : (List ImportsUnusedBindings)] [output : OutputPort]) : Void
  (let ([write-out-line (lambda ([x : Bytes]) (write-line x output))])
    (let ([write-section (lambda ([name : Bytes] [entries : (List Bytes)])
                           (case entries
                             [(empty) (void)]
                             [_
                              (begin
                                (write-out-line (indent3 name))
                                (for-each
                                  write-out-line
                                  (map indent4 entries)))]))])
      (case imports
        [(empty) (void)]
        [_
          (begin
            (write-out-line #"  Unused imports:")
            (for-each
              (lambda ([unused : ImportsUnusedBindings])
                (case unused
                  [(partial-imports-unused mod-name types values patterns)
                   (begin
                     (write-out-line (indent2 (mod-name->bytes mod-name)))
                     (write-section #"Types:" types)
                     (write-section #"Values:" values)
                     (write-section #"Patterns:" patterns))]
                  [(partial-imports-completely-unused mod-name)
                   (write-out-line
                     (indent2 (bytes-append (varargs list #"Everything in: "
                                                          (mod-name->bytes mod-name)))))]
                  [(full-imports-unused mod-name)
                   (write-out-line
                     (indent2 (bytes-append (varargs list #"Everything in: "
                                                          (mod-name->bytes mod-name)))))]))
              imports))]))))

(define (write-lint-results [results : LintResults] [output : OutputPort]) : Void
  (case results
    [(lint-results
       _
       (unused-bindings (empty) (empty))
       (unused-local-bindings (empty))
       (empty)
       (module-header-lint-violations
         (empty)
         (empty)))
     (void)]
    [(lint-results
       src-file-path
       (unused-bindings unused-imports unused-var-defs)
       (unused-local-bindings unused-local-vars)
       unused-dependencies
       (module-header-lint-violations
         unsorted-modules
         unsorted-bindings))
     (begin
      (write-line src-file-path output)
      (write-unused-imports-section unused-imports output)
      (write-results-section #"Unused defined-functions" unused-var-defs output)
      (for-each
        (lambda ([fun : UnusedFunctionLocalBindings])
          (case fun
            [(unused-function-local-bindings name vars)
             (write-results-section
               (bytes-append
                 (varargs list
                   #"Unused function local bindings: "
                   name))
               vars
               output)]))
        unused-local-vars)
      (write-results-section #"Unused dependencies:" (map mod-name->bytes unused-dependencies) output)
      (write-results-section #"Unsorted modules:" (map mod-name->bytes unsorted-modules) output)

      (write-results-section #"Unsorted bindings:" unsorted-bindings output))]))

(define (imports-module-name [i : Imports]) : ModName
  (case i
    [(partial-imports mod-name _ _ _) mod-name]
    [(full-imports mod-name) mod-name]))

(define (lint-module [module-file : Bytes] [signatures : (List Bytes)]) : (Either Bytes LintResults)
  (case (parse-module-file module-file)
    [(left v) (left (parser-error->bytes v))]
    [(right module)
     (case (parse-signature-files signatures)
       [(left v) (left v)]
       [(right signatures)
        (let ([signatures
                (foldl
                  (lambda ([sig : ModuleSignature] [acc : (IndexedSet ModuleSignature ModName)])
                    (indexed-set-add acc sig))
                  signatures
                  (make-indexed-set module-signature-name mod-name-cmp))])
          (right (lint-results
                   module-file
                   (module-unused-bindings module signatures)
                   (module-unused-local-bindings module)
                   (set->list
                     (set-remove-all
                       (set-add-all
                         (make-set mod-name-cmp)
                         (map module-signature-name (indexed-set->list signatures)))
                       (map imports-module-name (module-imports module))))
                   (find-header-violations module))))])]))
