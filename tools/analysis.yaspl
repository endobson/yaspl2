#:module (tools analysis)
#:import {
  (either)
  (io)
  (list)
  (maybe)
  (prim)
  (sexp-parser)
  (source-language)
}
(export
  (#:types AnalysisResults)
  (#:values write-analysis-results find-problematic-functions)
  (#:patterns))
(types
  (define-type AnalysisResults
    (analysis-results
      [file-name : Bytes]
      [bad-functions : (List Bytes)])))

(define (write-analysis-results [results : AnalysisResults] [output : OutputPort]) : Void
  (case results
    [(analysis-results _ (empty))
     (void)]
    [(analysis-results name functions)
     (begin
        (write-all-bytes name output)
        (write-line #":" output)
        (for-each
          (lambda ([function : Bytes])
            (begin
              (write-all-bytes #"  " output)
              (write-line function output)))
          functions))]))

(define (find-problematic-functions [module-file : Bytes]) : (Either Bytes AnalysisResults)
  (case (parse-sexps (call-with-input-file module-file read-all-bytes))
    [(left v) (left v)]
    [(right module-forms)
     (case (parse-module module-forms)
      [(left v) (left v)]
      [(right mod)
       (match-define (module _ _ _ _ defs) mod)
       (right
         (analysis-results
           module-file
           (filter-map
             (lambda ([def : FunctionDefinition]) : (Maybe Bytes)
               (case def
                 [(function-definition name _ _ _ body)
                  (if (search/block body)
                      (just name)
                      (nothing))]))
             defs)))])]))

(define (search/block [block : Block]) : Boolean
  (match-define (block defs expr) block)
  (or
    (ormap search/block-def defs)
    (search/expr expr)))

(define (search/expr [expr : Expression]) : Boolean
  (case expr
    ;; Problematic expressions
    [(app-expr _ (lambda-expr _ _ _) _) #t]

    ;; Recur
    [(int-literal _) #f]
    [(bytes-literal _) #f]
    [(boolean-literal _) #f]
    [(var-expr _) #f]
    [(annotated-expr _ expr)
     (search/expr expr)]
    [(if-expr c t f)
     (or (search/expr c)
         (or (search/expr t)
             (search/expr f)))]
    [(begin-expr exprs last-expr)
     (or (ormap search/expr exprs)
         (search/expr last-expr))]
    [(app-expr _ op args)
     (or (ormap search/expr args)
         (search/expr op))]
    [(varargs-app-expr _ op args)
     (or (ormap search/expr args)
         (search/expr op))]
    [(let-expr _ expr body)
     (or (search/expr expr)
         (search/expr body))]
    [(lambda-expr _ _ body)
     (search/expr body)]
    [(case-expr expr clauses)
     (or (ormap search/clause clauses)
         (search/expr expr))]))

(define (search/block-def [block-def : BlockDefinition]) : Boolean
  (match-define (match-def pattern expr) block-def)
  (search/expr expr))

(define (search/clause [clause : CaseClause]) : Boolean
  (match-define (case-clause pattern block) clause)
  (search/block block))
