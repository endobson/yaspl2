#:module (tools shell shell-main)
#:import {
  (prim)
  (io)
  (list)
  (maybe)
  (bytes)
  (numbers)
}
(export)
(types
  (define-type Termios
    (termios
      [input-flags : Bytes]
      [output-flags : Bytes]
      [control-flags : Bytes]
      [local-flags : Bytes]
      [control-chars : Bytes]
      [input-speed : Bytes]
      [output-speed : Bytes]))

  (define-type EditLineState
    (edit-line-state [input-state : InputState] [before-bytes : (List U8)] [after-chars : (List U8)]))
  (define-type InputState
    (input-state-empty)
    (input-state-esc)
    (input-state-esc-bracket)))

(define (ioctl-number
          [modality : Int]
          [group : Int]
          [num : Int]
          [len : Int]) : Int
  (bitwise-ior
    modality
    (bitwise-ior
      (logical-shift-left (bitwise-and #x1FFF len) 16)
      (bitwise-ior
        (logical-shift-left group 8)
        num))))


(define (parse-termios-bytes [bytes : Bytes]) : Termios
  (termios
    (subbytes bytes 0 8)
    (subbytes bytes 8 16)
    (subbytes bytes 16 24)
    (subbytes bytes 24 32)
    (subbytes bytes 32 52)
    (subbytes bytes 56 64)
    (subbytes bytes 64 72)))

(define (serialize-termios [termios : Termios]) : Bytes
  (match-define (termios in out control local chars input-speed output-speed) termios)
  (let ([bytes (make-bytes 72)])
    (begin
      (bytes-copy! in 0 8 bytes 0)
      (bytes-copy! out 0 8 bytes 8)
      (bytes-copy! control 0 8 bytes 16)
      (bytes-copy! local 0 8 bytes 24)
      (bytes-copy! chars 0 20 bytes 32)
      (bytes-copy! input-speed 0 8 bytes 56)
      (bytes-copy! output-speed 0 8 bytes 64)
      bytes)))


(define (read-termios [terminal : InputPort]) : Termios
  (let ([bytes (make-bytes 72)])
    (begin
      (ioctl terminal (ioctl-number #x40000000 116 19 72) bytes)
      (parse-termios-bytes bytes))))

(define (write-termios [termios : Termios] [terminal : InputPort]) : Void
  (ioctl terminal (ioctl-number #x80000000 116 20 72) (serialize-termios termios)))



(define (main-loop [stdin : InputPort] [stdout : OutputPort]) : (Maybe Bytes)
  (begin
    (write-all-bytes #"> " stdout)
    (edit-loop (edit-line-state (input-state-empty) (empty) (empty)) stdin stdout)))

(define (edit-loop [line-state : EditLineState] [stdin : InputPort] [stdout : OutputPort]) : (Maybe Bytes)
  ;; TODO handle more than 1 byte at a time
  (let ([buffer (make-bytes 1)])
    (let ([amount-read (read-bytes buffer stdin 0 (bytes-length buffer))])
      (if (= amount-read 1)
          (handle-byte line-state (bytes-ref buffer 0) stdin stdout)
          (panic #"Unexpected read amount")))))

(define (handle-byte [line-state : EditLineState] [byte : Int] [stdin : InputPort] [stdout : OutputPort]) : (Maybe Bytes)
  (case line-state
    [(edit-line-state (input-state-empty) before after)
     (case byte
       [10 (begin (newline stdout) (main-loop stdin stdout))]
       [4 (begin (newline stdout) (nothing))]
       [127
        (case before
          [(empty)
           (let ([bytes (make-bytes 1)])
             (begin
               (bytes-set! bytes 0 (u8 7))
               (write-all-bytes bytes stdout)
               (edit-loop (edit-line-state (input-state-empty) (empty) after) stdin stdout)))]
          [(cons _ before)
           (let ([bytes (make-bytes 3)])
             (begin
               (bytes-set! bytes 0 (u8 8))
               (bytes-set! bytes 1 (u8 32))
               (bytes-set! bytes 2 (u8 8))
               (write-all-bytes bytes stdout)
               (edit-loop (edit-line-state (input-state-empty) before after) stdin stdout)))])]
       [27
        (edit-loop (edit-line-state (input-state-esc) before after) stdin stdout)]
       [_
        (let ([buffer (make-bytes 1)])
          (begin
            (bytes-set! buffer 0 (u8 byte))
            (write-all-bytes buffer stdout)
            (edit-loop (edit-line-state (input-state-empty) (cons (u8 byte) before) after) stdin stdout)))])]
    [(edit-line-state (input-state-esc) before after)
     (case byte
       [91
        (edit-loop (edit-line-state (input-state-esc-bracket) before after) stdin stdout)]
       [_
         (just #"Invalid start of escape sequence")])]
    [(edit-line-state (input-state-esc-bracket) before after)
     (case byte
       [65
        (let ([bytes (make-bytes 1)])
          (begin
            (bytes-set! bytes 0 (u8 7))
            (write-all-bytes bytes stdout)
            (edit-loop (edit-line-state (input-state-empty) before after) stdin stdout)))]
       [66
        (let ([bytes (make-bytes 1)])
          (begin
            (bytes-set! bytes 0 (u8 7))
            (write-all-bytes bytes stdout)
            (edit-loop (edit-line-state (input-state-empty) before after) stdin stdout)))]
       [67
        (case after
          [(empty)
           (let ([bytes (make-bytes 1)])
             (begin
               (bytes-set! bytes 0 (u8 7))
               (write-all-bytes bytes stdout)
               (edit-loop (edit-line-state (input-state-empty) before after) stdin stdout)))]
          [(cons char after)
           (let ([bytes (make-bytes 3)])
             (begin
               (bytes-set! bytes 0 (u8 27))
               (bytes-set! bytes 1 (u8 91))
               (bytes-set! bytes 2 (u8 67))
               (write-all-bytes bytes stdout)
               (edit-loop (edit-line-state (input-state-empty) (cons char before) after) stdin
                          stdout)))])]
       [68
        (case before
          [(empty)
           (let ([bytes (make-bytes 1)])
             (begin
               (bytes-set! bytes 0 (u8 7))
               (write-all-bytes bytes stdout)
               (edit-loop (edit-line-state (input-state-empty) before after) stdin stdout)))]
          [(cons char before)
           (let ([bytes (make-bytes 3)])
             (begin
               (bytes-set! bytes 0 (u8 27))
               (bytes-set! bytes 1 (u8 91))
               (bytes-set! bytes 2 (u8 68))
               (write-all-bytes bytes stdout)
               (edit-loop (edit-line-state (input-state-empty) before (cons char after)) stdin
                          stdout)))])]
       [_
         (just #"Unknown escape sequence")])]))





(define (main [args : (Array Bytes)] [stdin : InputPort] [stdout : OutputPort] [stderr : OutputPort]) : Int
  (begin
    (let ([termios (read-termios stdin)])
      (case termios
        [(termios in out control local chars in-speed out-speed)
         (begin
           (bytes-set! local 0 (u8 (bitwise-and #xF7 (bytes-ref local 0))))
           (bytes-set! local 1 (u8 (bitwise-and #xFE (bytes-ref local 1))))
           (write-termios termios stdin))]))
    (case (main-loop stdin stdout)
      [(nothing) 0]
      [(just msg)
       (begin
         (write-line msg stderr)
         1)])))
