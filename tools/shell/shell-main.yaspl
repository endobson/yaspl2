#:module (tools shell shell-main)
#:import {
  (bytes)
  (data lifted-primitives)
  (either)
  (io)
  (list)
  (maybe)
  (prim)
  (tuples)
}
(export)
(types
  (define-type Termios
    (termios
      [input-flags : Bytes]
      [output-flags : Bytes]
      [control-flags : Bytes]
      [local-flags : Bytes]
      [control-chars : Bytes]
      [input-speed : Bytes]
      [output-speed : Bytes]))

  (define-type EditLineState
    (edit-line-state
      [before-bytes : (List LiftedU8)]
      [after-chars : (List LiftedU8)]))
  (define-type InputState
    (input-state-empty)
    (input-state-esc)
    (input-state-esc-bracket))

  (define-type HistoryState
    (history-state
      [executed-commands : (List Bytes)]
      [current-prev-commands : (List Bytes)]
      [current-next-commands : (List Bytes)]))

  (define-type InputAction
    (print-action [val : Byte])
    (previous-command-action)
    (no-action)
    (next-command-action)
    (move-cursor-right-action)
    (move-cursor-left-action)
    (finish-command-action)
    (exit-action)
    (backspace-action))

  (define-type ActionResult
    (continue-editing-result [edit-line-state : EditLineState]
                             [history-state : HistoryState])
    (command-result [command : Bytes])
    (exit-result)))

(define (ioctl-number
          [modality : Int]
          [group : Int]
          [num : Int]
          [len : Int]) : Int
  (bitwise-ior
    modality
    (bitwise-ior
      (logical-shift-left (bitwise-and #x1FFF len) 16)
      (bitwise-ior
        (logical-shift-left group 8)
        num))))


(define (parse-termios-bytes [bytes : Bytes]) : Termios
  (termios
    (subbytes bytes 0 8)
    (subbytes bytes 8 16)
    (subbytes bytes 16 24)
    (subbytes bytes 24 32)
    (subbytes bytes 32 52)
    (subbytes bytes 56 64)
    (subbytes bytes 64 72)))

(define (serialize-termios [termios : Termios]) : Bytes
  (match-define (termios in out control local chars input-speed output-speed) termios)
  (let ([bytes (make-bytes 72)])
    (begin
      (bytes-copy! in 0 8 bytes 0)
      (bytes-copy! out 0 8 bytes 8)
      (bytes-copy! control 0 8 bytes 16)
      (bytes-copy! local 0 8 bytes 24)
      (bytes-copy! chars 0 20 bytes 32)
      (bytes-copy! input-speed 0 8 bytes 56)
      (bytes-copy! output-speed 0 8 bytes 64)
      bytes)))


(define (read-termios [terminal : InputPort]) : Termios
  (let ([bytes (make-bytes 72)])
    (begin
      (ioctl terminal (ioctl-number #x40000000 116 19 72) bytes)
      (parse-termios-bytes bytes))))

(define (write-termios [termios : Termios] [terminal : InputPort]) : Void
  (ioctl terminal (ioctl-number #x80000000 116 20 72) (serialize-termios termios)))



(define (main-loop
          [executed-commands : (List Bytes)]
          [stdin : InputPort]
          [stdout : OutputPort])
  : (Maybe Bytes)
  (begin
    (write-all-bytes #"> " stdout)
    (edit-loop (history-state executed-commands executed-commands (empty))
               (edit-line-state (empty) (empty))
               (input-state-empty)
               stdin stdout)))

(define (edit-loop [hist-state : HistoryState] [line-state : EditLineState]
                   [input-state : InputState]
                   [stdin : InputPort] [stdout : OutputPort]) : (Maybe Bytes)
  ;; TODO handle more than 1 byte at a time
  (let ([buffer (make-bytes 1)])
    (let ([amount-read (read-bytes buffer stdin 0 (bytes-length buffer))])
      (if (not (= amount-read 1))
          (panic #"Unexpected read amount")
          (case (handle-input-byte (u8->s64 (bytes-ref buffer 0)) input-state)
            [(left v) (just v)]
            [(right (tuple2 input-state action))
             (match-define (tuple2 output action-result) (handle-input-action action hist-state line-state))
             (begin
               (write-all-bytes output stdout)
               (case action-result
                 [(continue-editing-result line-state hist-state)
                  (edit-loop hist-state line-state input-state stdin stdout)]
                 [(command-result command)
                  (finish-iteration command (history-state-executed-commands hist-state) stdin stdout)]
                 [(exit-result)
                  (nothing)]))])))))

(define (list->bytes [l : (List LiftedU8)]) : Bytes
  (list->bytes-helper (make-bytes (length l)) 0 l))

(define (list->bytes-helper [bytes : Bytes] [index : Int] [l : (List LiftedU8)]) : Bytes
  (case l
    [(empty) bytes]
    [(cons (lifted-u8 e) l)
     (begin
       (bytes-set! bytes index e)
       (list->bytes-helper bytes (+ 1 index) l))]))

(define (bytes->list [b : Bytes]) : (List LiftedU8)
  (bytes-map b lifted-u8))



(define (finish-iteration [bytes : Bytes] [executed-commands : (List Bytes)]
                          [stdin : InputPort] [stdout : OutputPort]) : (Maybe Bytes)
  (case bytes
    [#""
     (main-loop executed-commands stdin stdout)]
    [_
     (begin
       (write-line bytes stdout)
       (main-loop (cons bytes executed-commands) stdin stdout))]))

(define (handle-input-byte [byte : Int] [state : InputState])
  : (Either Bytes (Tuple2 InputState InputAction))
  (case state
    [(input-state-empty)
     (case byte
       ;; Newline
       [10 (right (tuple2 (input-state-empty) (finish-command-action)))]
       ;; Control-D
       [4 (right (tuple2 (input-state-empty) (exit-action)))]
       ;; Control-P
       [16 (right (tuple2 (input-state-empty) (previous-command-action)))]
       ;; Control-N
       [14 (right (tuple2 (input-state-empty) (next-command-action)))]
       ;; Backspace
       [127 (right (tuple2 (input-state-empty) (backspace-action)))]
       ;; ESC
       [27 (right (tuple2 (input-state-esc) (no-action)))]
       ;; Anything else
       [byte (right (tuple2 (input-state-empty) (print-action byte)))])]
    [(input-state-esc)
     (case byte
       ;; Bracket
       [91 (right (tuple2 (input-state-esc-bracket) (no-action)))]
       ;; Anything else
       [_ (left #"Invalid start of escape sequence")])]
    [(input-state-esc-bracket)
     (case byte
       ;; Up arrow
       [65 (right (tuple2 (input-state-empty) (previous-command-action)))]
       ;; Down arrow
       [66 (right (tuple2 (input-state-empty) (next-command-action)))]
       ;; Right arrow
       [67 (right (tuple2 (input-state-empty) (move-cursor-right-action)))]
       ;; Left arrow
       [68 (right (tuple2 (input-state-empty) (move-cursor-left-action)))]
       ;; Anything else
       [_ (left #"Unknown escape sequence")])]))

(define (handle-input-action
          [action : InputAction]
          [hist-state : HistoryState]
          [line-state : EditLineState])
  : (Tuple2 Bytes ActionResult)
  (match-define (edit-line-state before after) line-state)
  (case action
    [(no-action) (tuple2 #"" (continue-editing-result line-state hist-state))]
    [(finish-command-action)
     (tuple2 #"\n" (command-result (list->bytes (append (reverse before) after))))]
    [(exit-action) (tuple2 #"\n" (exit-result))]
    [(previous-command-action)
     (case hist-state
       [(history-state _ (empty) _)
        (tuple2 #"" (continue-editing-result line-state hist-state))]
       [(history-state exec-commands (cons prev-command prev-commands) next-commands)
        (match-define current-command
          (bytes-append
            (varargs list (list->bytes (reverse before)) (list->bytes after))))
        (match-define output
          (bytes-append
            (varargs list
              (list->bytes (map (lambda ([_ : LiftedU8]) (lifted-u8 (u8 8))) before))
              (list->bytes (map (lambda ([_ : LiftedU8]) (lifted-u8 (u8 32))) before))
              (list->bytes (map (lambda ([_ : LiftedU8]) (lifted-u8 (u8 32))) after))
              (list->bytes (map (lambda ([_ : LiftedU8]) (lifted-u8 (u8 8))) before))
              (list->bytes (map (lambda ([_ : LiftedU8]) (lifted-u8 (u8 8))) after))
              prev-command)))
        (tuple2 output
                (continue-editing-result
                  (edit-line-state (reverse (bytes->list prev-command)) (empty))
                  (history-state exec-commands prev-commands
                                 (cons current-command next-commands))))])]
    [(next-command-action)
     (case hist-state
       [(history-state _ _ (empty))
        (tuple2 #"" (continue-editing-result line-state hist-state))]
       [(history-state exec-commands prev-commands (cons next-command next-commands))
        (match-define current-command
          (bytes-append
            (varargs list (list->bytes (reverse before)) (list->bytes after))))
        (match-define output
          (bytes-append
            (varargs list
              (list->bytes (map (lambda ([_ : LiftedU8]) (lifted-u8 (u8 8))) before))
              (list->bytes (map (lambda ([_ : LiftedU8]) (lifted-u8 (u8 32))) before))
              (list->bytes (map (lambda ([_ : LiftedU8]) (lifted-u8 (u8 32))) after))
              (list->bytes (map (lambda ([_ : LiftedU8]) (lifted-u8 (u8 8))) before))
              (list->bytes (map (lambda ([_ : LiftedU8]) (lifted-u8 (u8 8))) after))
              next-command)))
        (tuple2 output
                (continue-editing-result
                  (edit-line-state (reverse (bytes->list next-command)) (empty))
                  (history-state exec-commands (cons current-command prev-commands)
                                               next-commands)))])]
    [(backspace-action)
     (case before
       [(empty)
        (tuple2 #"\x07" (continue-editing-result line-state hist-state))]
       [(cons _ before)
        (tuple2
          (bytes-append
            (varargs list
              #"\x08"
              (list->bytes after)
              #" "
              (list->bytes (map (lambda ([_ : LiftedU8]) (lifted-u8 (u8 8))) after))
              #"\x08"))
          (continue-editing-result (edit-line-state before after) hist-state))])]
    [(print-action val)
     (tuple2
       (bytes-append
         (varargs list
           (bytes/1 (u8 val))
           (list->bytes after)
           (list->bytes (map (lambda ([_ : LiftedU8]) (lifted-u8 (u8 8))) after))))
       (continue-editing-result (edit-line-state (cons (lifted-u8 (u8 val)) before) after) hist-state))]
    [(move-cursor-right-action)
     (case after
       [(empty)
        (tuple2 #"\x07" (continue-editing-result line-state hist-state))]
       [(cons char after)
        (tuple2 #"\x1b[C" (continue-editing-result (edit-line-state (cons char before) after)
                                                hist-state))])]
    [(move-cursor-left-action)
     (case before
       [(empty)
        (tuple2 #"\x07" (continue-editing-result line-state hist-state))]
       [(cons char before)
        (tuple2 #"\x1b[D" (continue-editing-result (edit-line-state before (cons char after))
                                                   hist-state))])]))


(define (main [args : (Array Bytes)] [stdin : InputPort] [stdout : OutputPort] [stderr : OutputPort]) : Int
  (begin
    (let ([termios (read-termios stdin)])
      (case termios
        [(termios _in _out _control local _chars _in-speed _out-speed)
         (begin
           (bytes-set! local 0 (u8 (bitwise-and #xF7 (u8->s64 (bytes-ref local 0)))))
           (bytes-set! local 1 (u8 (bitwise-and #xFE (u8->s64 (bytes-ref local 1)))))
           (write-termios termios stdin))]))
    (case (main-loop (empty) stdin stdout)
      [(nothing) 0]
      [(just msg)
       (begin
         (write-line msg stderr)
         1)])))
