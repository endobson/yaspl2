#:module (tools objdump objdump)
#:import {
  (bytes)
  (either)
  (formats pe)
  (io)
  (list)
  (machine-code-writer)
  (maybe)
  (numbers)
  (prim)
  (system-abi)
}
(export
  (#:values main))
(types
  (define-type Args
    (args
      [abi : SystemAbi]
      [file-name : Bytes])))

(define (parse-args [input : (List Bytes)]) : (Either Bytes Args)
  (case input
    [(empty)
     (left #"No executable???")]
    [(cons _ (empty))
     (left #"No abi provided.")]
    [(cons _ (cons _ (empty)))
     (left #"No file name provided.")]
    [(cons _ (cons _ (cons _ (cons _ _))))
     (left #"Too many arguments.")]
    [(cons _ (cons abi (cons file-name (empty))))
     (case (parse-system-abi abi)
       [(nothing) (left #"Unknown abi")]
       [(just abi)
        (right (args abi file-name))])]))

(define (run [args : Bytes] [output : OutputPort]) : (Maybe Bytes)
  (case (parse-args (args->list args))
    [(left v) (just v)]
    [(right (args abi file-name))
     (case abi
       [(osx-abi)
        (just #"Objdump doesn't work for osx-abi yet.")]
       [(linux-abi)
        (just #"Objdump doesn't work for linux-abi yet.")]
       [(yasplos-abi)
        (case (call-with-input-file file-name read-pe)
          [(left v) (just v)]
          [(right pe-file)
           (begin
             (print-pe pe-file output)
             (nothing))])])]))

(define (print-pe [pe : PeFile] [output : OutputPort]) : Void
  (match-define (pe-file (coff-header num-sections symbol-table-offset num-symbols)
                         section-headers symbols _) pe)
  (begin
    (write-line #"-- Coff Header --" output)
    (write-all-bytes #"Num Sections: " output)
    (write-line (integer->decimal-bytes num-sections) output)
    (write-all-bytes #"Symbol Table Offset: " output)
    (write-line (integer->decimal-bytes symbol-table-offset) output)
    (write-all-bytes #"Number of Symbols:  " output)
    (write-line (integer->decimal-bytes num-symbols) output)
    (write-line #"" output)
    (write-line #"-- Section Headers --" output)
    (for-each
      (lambda ([sect : SectionHeader])
        (match-define (section-header name virt-addr virt-size raw-data-ptr raw-data-size) sect)
        (match-define name-padding (make-bytes/fill (- 8 (bytes-length name)) (u8 20)))
        (begin
          (write-all-bytes name output)
          (write-all-bytes name-padding output)
          (write-all-bytes #": " output)
          (write-all-bytes (integer->hex-bytes virt-addr) output)
          (write-all-bytes #" " output)
          (write-all-bytes (integer->hex-bytes virt-size) output)
          (write-all-bytes #" " output)
          (write-all-bytes (integer->hex-bytes raw-data-ptr) output)
          (write-all-bytes #" " output)
          (write-all-bytes (integer->hex-bytes raw-data-size) output)
          (newline output)))
      section-headers)

    (write-line #"" output)
    (write-line #"-- Symbol Table --" output)
    (for-each
      (lambda ([sym : SymbolTableEntry])
        (match-define (symbol-table-entry name value section-number storage-class) sym)
        (match-define hex-bytes (integer->hex-bytes value))
        (match-define hex-padding (make-bytes/fill (- 8 (bytes-length hex-bytes)) (u8 48)))
        (begin
          (write-all-bytes hex-padding output)
          (write-all-bytes hex-bytes output)
          (write-all-bytes #" " output)
          (write-all-bytes (integer->decimal-bytes section-number) output)
          (write-all-bytes #" " output)
          (write-all-bytes (integer->decimal-bytes storage-class) output)
          (write-all-bytes #" | " output)
          (write-all-bytes name output)
          (newline output)))
      symbols)
    (void)))

(define (main [args : Bytes] [stdin : InputPort] [stdout : OutputPort] [stderr : OutputPort]): Int
  (case (run args stdout)
    [(just v)
     (begin
       (write-all-bytes v stderr)
       (newline stderr)
       1)]
    [(nothing)
     0]))
