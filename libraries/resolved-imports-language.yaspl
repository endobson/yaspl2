#:module (resolved-imports-language)
#:import {
  (bytes)
  (data indexed-set)
  (dict)
  (either)
  (join-list)
  (list)
  (maybe)
  (module-name)
  (module-signature)
  (prim)
  (set)
  {(source-language)
    {#:types
      Block
      BlockDefinition
      CaseClause
      Export
      Exports
      Expression
      FunctionArg
      FunctionDefinition
      Import
      [Imports s:Imports]
      [Module s:Module]
      Pattern
      PreType
      StaticDefinition
      TypeDefinition
      VariantDefinition
      VariantField
    }
    {#:values
      bytes-pattern
      export-exported-name
      export-local-name
      function-definition-name
      ignore-pattern
      pre-type-source-span
      type-definition-name
    }
    {#:patterns
      abstraction-pattern
      annotated-expr
      app-expr
      begin-expr
      block
      boolean-literal
      bytes-literal
      bytes-pattern
      case-clause
      case-expr
      export
      exports
      [full-imports s:full-imports]
      fun-pre-type
      function-arg
      function-definition
      if-expr
      ignore-pattern
      import
      int-literal
      int-pattern
      lambda-expr
      let-expr
      match-def
      [module s:module]
      [partial-imports s:partial-imports]
      type-app-pre-type
      type-definition
      var-expr
      var-pre-type
      varargs-app-expr
      varargs-definition
      varargs2-app-expr
      variable-pattern
      variant-definition
      variant-field
    }
  }
  (top-level-name)
  (tuples)
  (types)
}
(export
  (#:types
      Imports Module Block Export Pattern PreType BlockDefinition CaseClause Exports Expression
      FunctionDefinition FunctionArg TypeDefinition VariantDefinition VariantField StaticDefinition)
  (#:values resolve-imports import-resolution-errors->bytes
            module-definitions module-name module-types module-imports
            imports-patterns resolve-imports/importss type-definition-name
            bytes-pattern ignore-pattern
            pre-type-source-span
            export-exported-name export-local-name function-definition-name)
  (#:patterns imports module app-expr annotated-expr begin-expr block boolean-literal bytes-literal
              case-clause case-expr export exports fun-pre-type function-definition function-arg
              if-expr int-literal lambda-expr let-expr match-def type-app-pre-type type-definition
              var-expr var-pre-type varargs-app-expr varargs2-app-expr variant-definition variant-field
              abstraction-pattern bytes-pattern ignore-pattern int-pattern variable-pattern
              varargs-definition))
(types
  (define-type Module
    (module [name : ModName]
            [imports : Imports]
            [exports : Exports]
            [types : (List TypeDefinition)]
            [definitions : (List FunctionDefinition)]
            [statics : (List StaticDefinition)]))
  (define-type Imports
    (imports
      [types : (Dict Bytes Type)]
      [values : (Dict Bytes ValueSignature)]
      [patterns : (Dict Bytes PatternSignature)]
      [statics : (Dict Bytes StaticSignature)]
      [static-info : (Dict TopLevelName TopLevelSignature)]))

  (define-type ImportResolutionErrors
    (import-resolution-errors
      [module-name : ModName]
      [errors : (List ImportResolutionError)]))

  (define-type ImportResolutionError
    (non-existent-imports/module [module-name : ModName])
    (non-existent-imports/bindings
      [module-name : ModName]
      [types : (List Bytes)]
      [values : (List Bytes)]
      [patterns : (List Bytes)]
      [statics : (List Bytes)])
    (duplicate-imports
      [modules : (List ModName)]
      [types : (List Bytes)]
      [values : (List Bytes)]
      [patterns : (List Bytes)]
      [statics : (List Bytes)])))

(define (resolve-imports [mod : s:Module] [sigs : (IndexedSet ModuleSignature ModName)])
  : (Either ImportResolutionErrors Module)
  (match-define (s:module name importss exports types definitions statics) mod)
  (case (resolve-imports/importss
          importss sigs (make-set mod-name-cmp)
          (make-dict bytes-cmp) (make-dict bytes-cmp) (make-dict bytes-cmp) (make-dict bytes-cmp)
          (empty) (empty) (empty) (empty) (empty) (empty))
    [(left errors)
     (left (import-resolution-errors name errors))]
    [(right imports)
     (right (module name imports exports types definitions statics))]))

(define (resolve-imports/importss
          [importss : (List s:Imports)]
          [sigs : (IndexedSet ModuleSignature ModName)]
          [seen-modules : (Set ModName)]
          [types : (Dict Bytes Type)]
          [values : (Dict Bytes ValueSignature)]
          [patterns : (Dict Bytes PatternSignature)]
          [statics : (Dict Bytes StaticSignature)]
          [errors : (List ImportResolutionError)]
          [duplicate-modules : (List ModName)]
          [duplicate-types : (List Bytes)]
          [duplicate-values : (List Bytes)]
          [duplicate-patterns : (List Bytes)]
          [duplicate-statics : (List Bytes)])
  : (Either (List ImportResolutionError) Imports)
  (case importss
    [(empty)
     (match-define final-errors
       (case (tuple2 (tuple2 duplicate-modules duplicate-types)
                     (tuple3 duplicate-values duplicate-patterns duplicate-statics))
         [(tuple2 (tuple2 (empty) (empty)) (tuple3 (empty) (empty) (empty)))
          errors]
         [_
          (cons (duplicate-imports duplicate-modules duplicate-types duplicate-values
                                   duplicate-patterns duplicate-statics)
                errors)]))
     (case final-errors
       [(empty)
        (right (imports types values patterns statics (make-static-info sigs)))]
       [e
        (left e)])]
    [(cons (s:full-imports mod-name) importss)
     (if (set-member? seen-modules mod-name)
         (resolve-imports/importss
           importss sigs
           seen-modules types values patterns statics errors
           (cons mod-name duplicate-modules)
           duplicate-types duplicate-values duplicate-patterns duplicate-statics)
         (case (indexed-set-ref sigs mod-name)
           [(nothing)
            (resolve-imports/importss
              importss sigs
              (set-add seen-modules mod-name) types values patterns statics
              (cons (non-existent-imports/module mod-name) errors)
              duplicate-modules duplicate-types duplicate-values duplicate-patterns duplicate-statics)]
           [(just (module-signature _ e-values e-patterns e-types e-statics _))
            (match-define (tuple2 duplicate-types types)
              (add-all-imports e-types types duplicate-types))
            (match-define (tuple2 duplicate-values values)
              (add-all-imports e-values values duplicate-values))
            (match-define (tuple2 duplicate-patterns patterns)
              (add-all-imports e-patterns patterns duplicate-patterns))
            (match-define (tuple2 duplicate-statics statics)
              (add-all-imports e-statics statics duplicate-statics))
            (resolve-imports/importss
              importss sigs
              (set-add seen-modules mod-name) types values patterns statics errors
              duplicate-modules duplicate-types duplicate-values duplicate-patterns duplicate-statics)]))]
    [(cons (s:partial-imports mod-name i-types i-values i-patterns i-statics) importss)
     (if (set-member? seen-modules mod-name)
         (resolve-imports/importss
           importss sigs
           seen-modules types values patterns statics errors
           (cons mod-name duplicate-modules)
           duplicate-types duplicate-values duplicate-patterns duplicate-statics)
         (case (indexed-set-ref sigs mod-name)
           [(nothing)
            (resolve-imports/importss
              importss sigs
              (set-add seen-modules mod-name) types values patterns statics
              (cons (non-existent-imports/module mod-name) errors)
              duplicate-modules duplicate-types duplicate-values duplicate-patterns duplicate-statics)]
           [(just (module-signature _ e-values e-patterns e-types e-statics _))
            (match-define (tuple3 unknown-types duplicate-types types)
              (add-imports i-types e-types types duplicate-types))
            (match-define (tuple3 unknown-values duplicate-values values)
              (add-imports i-values e-values values duplicate-values))
            (match-define (tuple3 unknown-patterns duplicate-patterns patterns)
              (add-imports i-patterns e-patterns patterns duplicate-patterns))
            (match-define (tuple3 unknown-statics duplicate-statics statics)
              (add-imports i-statics e-statics statics duplicate-statics))
            (match-define new-errors
              (case (tuple2 (tuple2 unknown-types unknown-values)
                            (tuple2 unknown-patterns unknown-statics))
                [(tuple2 (tuple2 (empty) (empty))
                         (tuple2 (empty) (empty)))
                 errors]
                [_
                 (cons (non-existent-imports/bindings mod-name unknown-types unknown-values unknown-patterns
                                                      unknown-statics)
                       errors)]))
            (resolve-imports/importss
              importss sigs (set-add seen-modules mod-name) types values patterns statics new-errors
              duplicate-modules duplicate-types duplicate-values duplicate-patterns duplicate-statics)]))]))


(define [Val] (add-imports
                [imports : (List Import)]
                [exports : (Dict Bytes Val)]
                [locals : (Dict Bytes Val)]
                [duplicate-bindings : (List Bytes)])
  : (Tuple3 (List Bytes) (List Bytes) (Dict Bytes Val))
  (add-imports* imports exports locals (empty) duplicate-bindings))

(define [Val] (add-imports*
                [imports : (List Import)]
                [exports : (Dict Bytes Val)]
                [locals : (Dict Bytes Val)]
                [unknown-imports : (List Bytes)]
                [duplicate-bindings : (List Bytes)])
  : (Tuple3 (List Bytes) (List Bytes) (Dict Bytes Val))
  (case imports
    [(empty)
     (tuple3 unknown-imports duplicate-bindings locals)]
    [(cons (import ex-name im-name) imports)
     (case (dict-ref exports ex-name)
       [(nothing)
        (add-imports* imports exports locals (cons ex-name unknown-imports) duplicate-bindings)]
       [(just v)
        (case (dict-maybe-add locals im-name v)
          [(nothing)
           (add-imports* imports exports locals unknown-imports (cons im-name duplicate-bindings))]
          [(just locals)
           (add-imports* imports exports locals unknown-imports duplicate-bindings)])])]))

(define [Val] (add-all-imports
                [exports : (Dict Bytes Val)]
                [locals : (Dict Bytes Val)]
                [duplicate-bindings : (List Bytes)])
  : (Tuple2 (List Bytes) (Dict Bytes Val))
  (dict-fold
    exports
    (lambda ([name : Bytes] [v : Val] [acc : (Tuple2 (List Bytes) (Dict Bytes Val))])
      (case acc
        [(tuple2 duplicate-bindings locals)
         (case (dict-maybe-add locals name v)
           [(nothing)
            (tuple2 (cons name duplicate-bindings) locals)]
           [(just locals)
            (tuple2 duplicate-bindings locals)])]))
    (tuple2 duplicate-bindings locals)))

(define (import-resolution-errors->bytes [me : ImportResolutionErrors]) : Bytes
  (case me
    [(import-resolution-errors mod-name errors)
     (bytes-append
       (map (lambda ([error : ImportResolutionError]) (import-resolution-error->bytes mod-name error))
            errors))]))

(define (import-resolution-error->bytes [mod-name : ModName] [e : ImportResolutionError]) : Bytes
  (case e
    [(non-existent-imports/module import-name)
     (bytes-append
       (varargs list #"Missing import in " (mod-name->bytes mod-name)
                     #". Could not find module: " (mod-name->bytes import-name) #"\n"))]
    [(non-existent-imports/bindings import-name types values patterns statics)
     (let ([warn-no-exports
             (lambda ([kind : Bytes] [names : (List Bytes)]) : (JoinList Bytes)
               (case names
                 [(empty) (empty-jl)]
                 [_
                   (append-jl
                     (varargs join-list #"  " kind #" imports:\n")
                     (concat-map-jl/list
                       (lambda ([name : Bytes]) (varargs join-list #"    " name #"\n"))
                       names))]))])
        (bytes-append
          (jl->list
            (append-jl
              (varargs join-list
                #"Undefined imports in module " (mod-name->bytes mod-name) #" from module "
                (mod-name->bytes import-name) #":\n")
              (varargs append-jl*
                (warn-no-exports #"Type" types)
                (warn-no-exports #"Value" values)
                (warn-no-exports #"Pattern" patterns)
                (warn-no-exports #"Statics" statics))))))]
    [(duplicate-imports mod-names types values patterns statics)
     (let ([warn-duplicate-imports
             (lambda ([kind : Bytes] [names : (List Bytes)]) : (JoinList Bytes)
               (case names
                 [(empty) (empty-jl)]
                 [_
                   (append-jl
                     (varargs join-list #"  " kind #" imports:\n")
                     (concat-map-jl/list
                       (lambda ([name : Bytes]) (varargs join-list #"    " name #"\n"))
                       names))]))])
        (bytes-append
          (jl->list
            (append-jl
              (varargs join-list
                #"Duplicate imports in module " (mod-name->bytes mod-name) #":\n")
              (varargs append-jl*
                (warn-duplicate-imports #"Type" types)
                (warn-duplicate-imports #"Module" (map mod-name->bytes mod-names))
                (warn-duplicate-imports #"Value" values)
                (warn-duplicate-imports #"Pattern" patterns)
                (warn-duplicate-imports #"Static" statics))))))]))

(define (make-static-info [sigs : (IndexedSet ModuleSignature ModName)])
  : (Dict TopLevelName TopLevelSignature)
  (merge-dicts*
    (make-dict top-level-name-cmp)
    (map module-signature-static-info (indexed-set->list sigs))))
