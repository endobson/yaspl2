#:module (expanded-datatypes-language)
#:import {
  (bytes)
  (data source-location)
  (dict)
  (list)
  (module-name)
  (prim)
  (tuples)
  {(type-checked-language)
    {#:types
      Block
      BlockDefinition
      CaseClause
      Export
      Exports
      Expression
      FunctionArg
      [FunctionDefinition tc:FunctionDefinition]
      Imports
      [Module tc:Module]
      Pattern
      PatternExport
      StaticDefinition
      TypeDefinition
      TypeExport
      ValueExport
      VariantDefinition
      VariantField
    }
    {#:values
      exports
      function-arg-pattern
      value-export
      variant-definition-name
      variant-field-name
    }
    {#:patterns
      abstraction-pattern
      annotated-expr
      app-expr
      begin-expr
      block
      boolean-literal
      bytes-literal
      bytes-pattern
      case-clause
      case-expr
      export
      exports
      function-arg
      [function-definition tc:function-definition]
      if-expr
      ignore-pattern
      imports
      int-literal
      int-pattern
      lambda-expr
      let-expr
      match-def
      [module tc:module]
      pattern-export
      type-definition
      type-export
      value-export
      var-expr
      varargs-app-expr
      varargs-definition
      variable-pattern
      variant-definition
      variant-field
    }
  }
  (types)
  (variant-info)
}
(export
  (#:types
    Block BlockDefinition CaseClause DatatypeDefinition Exports Expression FieldDescriptor
    FunctionDefinition FunctionArg Imports Module Pattern StaticDefinition Export TypeExport
    PatternExport ValueExport)
  (#:values
   expand-datatypes
   module exports value-export module-definitions module-imports module-name
   module-static-definitions module-patterns
   function-arg-pattern)
  (#:patterns
    module annotated-expr app-expr type-export export exports begin-expr block boolean-literal
    bytes-literal case-clause case-expr function-definition function-arg
    if-expr int-literal lambda-expr let-expr
    match-def var-expr varargs-app-expr varargs-definition
    imports abstraction-pattern bytes-pattern ignore-pattern
    int-pattern variable-pattern value-export pattern-export
    variant-constructor variant-accessor))
(types
  (define-type Module
    (module [name : ModName]
            [imports : Imports]
            [exports : Exports]
            [definitions : (List FunctionDefinition)]
            [datatype-definitions : (List DatatypeDefinition)]
            [patterns : (Dict Bytes VariantDestructionInfo)]
            [static-definitions : (List StaticDefinition)]))

  (define-type FunctionDefinition
    (function-definition [name : Bytes]
                         [type-vars : (List Bytes)]
                         [args : (List FunctionArg)]
                         [return-type : Type]
                         [body : Block]))

  (define-type DatatypeDefinition
    (variant-constructor
      [variant-name : Bytes]
      [fields : (List FieldDescriptor)]
      [tag : U8])
    (variant-accessor
      [variant-name : Bytes]
      [field-name : Bytes]
      [field-offset : Int]))

  (define-type FieldDescriptor
    (field-descriptor [name : Bytes] [runtime-index : Int])))

(define (expand-datatypes [mod : tc:Module]) : Module
  (case mod
    [(tc:module name imports exports types definitions statics)
     (module name imports exports (expand-datatypes/function-definitions definitions)
                                  (expand-datatypes/type-definitions types)
                                  (expand-datatypes/patterns types)
                                  statics)]))

(define (expand-datatypes/function-definitions [defs : (List tc:FunctionDefinition)])
  : (List FunctionDefinition)
  (map
    (lambda ([def : tc:FunctionDefinition])
      (case def
        [(tc:function-definition
           name type-vars args (source-span-annotated return-type _) body)
         (function-definition name type-vars args return-type body)]))
    defs))

(define (expand-datatypes/type-definitions [defs : (List TypeDefinition)])
  : (List DatatypeDefinition)
  (foldl
    (lambda ([def : TypeDefinition] [acc : (List DatatypeDefinition)])
      (case def
        [(type-definition name _ (cons variant (empty)))
         (case variant
           [(variant-definition variant-name fields)
            (variant-definition->accessors
              (variant-definition->constructor acc (u8 0) name variant)
              0 name variant-name fields)])]
        [(type-definition name _ variants)
         (variant-definitions->constructors acc 0 name variants)]))
    defs
    (ann (List DatatypeDefinition) (empty))))

(define (expand-datatypes/patterns [defs : (List TypeDefinition)])
  : (Dict Bytes VariantDestructionInfo)
  (foldl
    (lambda ([def : TypeDefinition] [acc : (Dict Bytes VariantDestructionInfo)])
      (match-define (type-definition name _ variants) def)
      (match-define all-variant-names (map variant-definition-name variants))
      (dict-add-all
        acc
        (map/indexed
          (lambda ([var : VariantDefinition] [tag : Int])
            (match-define (variant-definition name fields) var)
            (tuple2
              name
              (variant-destruction-info name (u8 tag) (length fields) all-variant-names)))
          variants)))
    defs
    (ann (Dict Bytes VariantDestructionInfo) (make-dict bytes-cmp))))


(define (variant-definitions->constructors
          [acc : (List DatatypeDefinition)]
          [tag : Int]
          [type-name : Bytes]
          [defs : (List VariantDefinition)]) : (List DatatypeDefinition)
  (case defs
    [(empty) acc]
    [(cons def defs)
     (variant-definitions->constructors
       (variant-definition->constructor acc (u8 tag) type-name def)
       (+ 1 tag)
       type-name
       defs)]))

(define (variant-definition->constructor
          [acc : (List DatatypeDefinition)]
          [tag : U8]
          [type-name : Bytes]
          [def : VariantDefinition]) : (List DatatypeDefinition)
  (case def
    [(variant-definition name fields)
     (match-define field-descriptors
       (map/indexed
         (lambda ([v : VariantField] [i : Int])
           (field-descriptor (variant-field-name v) i))
         fields))
     (cons
       (variant-constructor name field-descriptors tag)
       acc)]))

(define (variant-definition->accessors
          [acc : (List DatatypeDefinition)]
          [offset : Int]
          [type-name : Bytes]
          [variant-name : Bytes]
          [fields : (List VariantField)]) : (List DatatypeDefinition)
  (case fields
    [(empty) acc]
    [(cons (variant-field name _type) fields)
     (variant-definition->accessors
       (cons
         (variant-accessor variant-name name offset)
         acc)
       (+ offset 1)
       type-name
       variant-name
       fields)]))
