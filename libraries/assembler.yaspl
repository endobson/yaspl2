#:module (assembler)
#:import {
  (aligned-bytes)
  (bytes)
  (data lifted-primitives)
  (join-list)
  (list)
  (maybe)
  (numbers)
  (panic)
  (prim)
  (set)
  (structured-native-code)
  (tuples)
  (yaspl x86-64-assembly)
  (yaspl x86-64-instruction-assembler)
}
(export
  (#:types)
  (#:values assemble-text-section assemble-const-section)
  (#:patterns))
(types
  (define-type EMCCommand
    (emc-mc [command : MCCommand])
    (emc-global-symbol [name : Bytes])
    (emc-align [alignment : Int])
    (emc-local-symbol [name : Bytes])))

(define (fragments->mc-commands [fragments : (List TextFragment)]) : (List EMCCommand)
  (jl->list
    (concat-map-jl
      (lambda ([fragment : TextFragment]) : (JoinList EMCCommand)
        (case fragment
          [(function-fragment fun-name sections)
           (match-define referenced-labels (sections->referenced-labels sections))
           (cons-jl
             (emc-align 4) ;; Align to 16 bytes
             (cons-jl
               (emc-global-symbol fun-name)
               (concat-map-jl
                 (lambda ([s : Section])
                   (case s
                     [(section blocks)
                      (concat-map-jl
                        (lambda ([bb : BasicBlock])
                          (case bb
                           [(basic-block block-name instructions)
                            (match-define commands
                               (map-jl emc-mc (concat-map-jl instruction->mc-commands instructions)))
                            (if (or (bytes=? block-name fun-name)
                                    (not (set-member? referenced-labels block-name)))
                                commands
                                (cons-jl (emc-local-symbol block-name) commands))]))
                        (list->jl blocks))]))
                 (list->jl sections))))]))
      (list->jl fragments))))

(define (RelocationKind)
  (partition-text-section-relocations
    [relocations : (List (Relocation RelocationKind))]
    [local-symbol-names : (Set Bytes)])
  : (Tuple2 (List (Relocation RelocationKind))
            (List (Relocation RelocationKind)))
  (partition-text-section-relocations* relocations (empty) (empty) local-symbol-names))

(define (RelocationKind)
  (partition-text-section-relocations*
    [relocations : (List (Relocation RelocationKind))]
    [local-acc : (List (Relocation RelocationKind))]
    [global-acc : (List (Relocation RelocationKind))]
    [local-symbol-names : (Set Bytes)])
  : (Tuple2 (List (Relocation RelocationKind))
            (List (Relocation RelocationKind)))
  (case relocations
    [(empty) (tuple2 local-acc global-acc)]
    [(cons reloc relocations)
     (match-define local (set-member? local-symbol-names (relocation-symbol reloc)))
     (partition-text-section-relocations*
       relocations
       (if local (cons reloc local-acc) local-acc)
       (if local global-acc (cons reloc global-acc))
       local-symbol-names)]))


(define (RelocationKind)
  (apply-local-text-relocations
    [relocs : (List (Relocation RelocationKind))]
    [symbols : (List DefinedSymbol)]
    [text : Bytes]) : Void
  (case relocs
    [(empty) (void)]
    ;; TODO this should look at the relocation kind
    [(cons (relocation _ offset symbol) relocs)
     (begin
        (case (find/maybe (lambda ([x : DefinedSymbol]) : (Maybe LiftedInt)
                            (case x
                              [(defined-symbol name offset)
                               (if (bytes=? symbol name)
                                   (just (lifted-int offset))
                                   (nothing))])) symbols)
          [(nothing)
           (panic/void #"Bad local relocation")]
          [(just (lifted-int symbol-offset))
           (bytes-set!/s32-le
             text
             offset
             (s32 (- (- symbol-offset offset) 4)))])
       (apply-local-text-relocations relocs symbols text))]))

(define (text-section-fold
          [commands : (List EMCCommand)]
          [rev-global-symbols : (List DefinedSymbol)]
          [rev-local-symbols : (List DefinedSymbol)]
          [reversed-contents : (List Bytes)]
          [offset : Int]
          [max-alignment : Int]
          [relocations : (List (Relocation AbstractRelocationKind))])
  : (ProgramSection AbstractRelocationKind)
  (case commands
    [(cons (emc-mc (mc-bytes bytes)) commands)
     (text-section-fold
       commands
       rev-global-symbols
       rev-local-symbols
       (cons bytes reversed-contents)
       (+ offset (bytes-length bytes))
       max-alignment
       relocations)]
    [(cons (emc-mc (mc-branch-relocation symbol)) commands)
     (text-section-fold
       commands
       rev-global-symbols
       rev-local-symbols
       (cons #"\x00\x00\x00\x00" reversed-contents)
       (+ offset 4)
       max-alignment
       (cons (relocation (branch-relocation) offset symbol) relocations))]
    [(cons (emc-mc (mc-call-relocation symbol)) commands)
     (text-section-fold
       commands
       rev-global-symbols
       rev-local-symbols
       (cons #"\x00\x00\x00\x00" reversed-contents)
       (+ offset 4)
       max-alignment
       (cons (relocation (call-relocation) offset symbol) relocations))]
    [(cons (emc-mc (mc-load-relocation symbol)) commands)
     (text-section-fold
       commands
       rev-global-symbols
       rev-local-symbols
       (cons #"\x00\x00\x00\x00" reversed-contents)
       (+ offset 4)
       max-alignment
       (cons (relocation (load-relocation) offset symbol) relocations))]
    [(cons (emc-global-symbol symbol) commands)
     (text-section-fold
       commands
       (cons (defined-symbol symbol offset) rev-global-symbols)
       rev-local-symbols
       reversed-contents
       offset
       max-alignment
       relocations)]
    [(cons (emc-local-symbol symbol) commands)
     (text-section-fold
       commands
       rev-global-symbols
       (cons (defined-symbol symbol offset) rev-local-symbols)
       reversed-contents
       offset
       max-alignment
       relocations)]
    [(cons (emc-align alignment) commands)
     (match-define aligned-offset (round-up offset (logical-shift-left 1 alignment)))
     (match-define reversed-contents
       (if (= aligned-offset offset)
           reversed-contents
           (cons (make-bytes (- aligned-offset offset)) reversed-contents)))
     (text-section-fold
       commands
       rev-global-symbols
       rev-local-symbols
       reversed-contents
       aligned-offset
       (max max-alignment alignment)
       relocations)]
    [(empty)
     (match-define global-symbols (reverse rev-global-symbols))
     (match-define local-symbols (reverse rev-local-symbols))
     (match-define local-symbol-names
       (set-add-all
         (make-set bytes-cmp)
         (map defined-symbol-name local-symbols)))

     (match-define (tuple2 local-text-section-relocations global-text-section-relocations)
       (partition-text-section-relocations relocations local-symbol-names))
     (match-define text-bytes (bytes-append (reverse reversed-contents)))
     (begin
       (apply-local-text-relocations local-text-section-relocations local-symbols text-bytes)
       (program-section
         (aligned-bytes text-bytes max-alignment)
         global-text-section-relocations
         global-symbols
         local-symbols))]))

(define (assemble-text-section [text-fragments : (List TextFragment)])
  : (ProgramSection AbstractRelocationKind)
  (text-section-fold (fragments->mc-commands text-fragments) (empty) (empty) (empty) 0 0 (empty)))

(define (const-section-fold
          [fragments : (List ConstFragment)]
          [reversed-contents : (List Bytes)]
          [offset : Int]
          [max-alignment : Int]
          [symbols : (List DefinedSymbol)]
          [relocations : (List (Relocation AbstractRelocationKind))])
  : (ProgramSection AbstractRelocationKind)
  (case fragments
    [(empty)
     (program-section
       (aligned-bytes (bytes-append (reverse reversed-contents)) max-alignment)
       (reverse relocations)
       (reverse symbols)
       (empty))]
    [(cons (address-fragment name value) fragments)
     (match-define alignment 3)
     (match-define aligned-offset (round-up offset (logical-shift-left 1 alignment)))
     (match-define reversed-contents
       (if (= aligned-offset offset)
           reversed-contents
           (cons (make-bytes (- aligned-offset offset)) reversed-contents)))
     (const-section-fold
       fragments
       (cons (bytes/8 (u8 0) (u8 0) (u8 0) (u8 0) (u8 0) (u8 0) (u8 0) (u8 0)) reversed-contents)
       (+ aligned-offset 8)
       (max alignment max-alignment)
       (cons (defined-symbol name aligned-offset) symbols)
       (cons (relocation (address-relocation) aligned-offset value) relocations))]
    [(cons (bytes-fragment name value) fragments)
     (const-section-fold
       fragments
       (cons value reversed-contents)
       (+ offset (bytes-length value))
       max-alignment
       (cons (defined-symbol name offset) symbols)
       relocations)]))

(define (assemble-const-section [fragments : (List ConstFragment)])
  : (ProgramSection AbstractRelocationKind)
  (const-section-fold fragments (empty) 0 0 (empty) (empty)))
