(module set
  (import
    (prim
      #:types (Boolean Int)
      #:values ()
      #:patterns ())
    (list
      #:types (List)
      #:values (cons empty length filter)
      #:patterns (cons empty)))
  (export
    #:types (Set)
    #:values (make-set set-add set->list set-member? set-remove set-add-all
              set-remove-all set-count set-intersect)
    #:patterns ())
  (types
    (define-type (Set a)
      (set [eq? : (a a -> Boolean)] [vals : (List a)])))

  (define (a) (make-set [eq : (a a -> Boolean)]) : (Set a)
    (set eq (empty)))

  (define (a) (set->list [s : (Set a)]) : (List a)
    (set-vals s))

  (define (a) (set-count [s : (Set a)]) : Int
    (length (set-vals s)))

  (define (a) (set-add [s : (Set a)] [v : a]) : (Set a)
    (if (set-member? s v)
        s
        (case s
          [(set eq vs)
           (set eq (cons v vs))])))

  (define (a) (set-add-all [s : (Set a)] [vs : (List a)] ) : (Set a)
    (case vs
      [(empty) s]
      [(cons v vs) (set-add-all (set-add s v) vs)]))

  (define (a) (set-remove [s : (Set a)] [v : a]) : (Set a)
    (case s
      [(set eq vs)
       (set eq (set-remove-helper eq vs v))]))

  (define (a) (set-remove-all [s : (Set a)] [vs : (List a)]) : (Set a)
    (case vs
      [(empty) s]
      [(cons v vs) (set-remove-all (set-remove s v) vs)]))


  (define (a) (set-remove-helper [eq : (a a -> Boolean)] [vs : (List a)] [v : a]) : (List a)
    (case vs
      [(empty) (empty)]
      [(cons v2 vs)
       (if (eq v v2)
           vs
           (cons v2 (set-remove-helper eq vs v)))]))

  (define (a) (set-member? [s : (Set a)] [v : a]) : Boolean
    (case s
      [(set eq vs)
       (set-member?-helper eq vs v)]))

  (define (a) (set-member?-helper [eq : (a a -> Boolean)] [vs : (List a)] [v : a]) : Boolean
    (case vs
      [(empty) #f]
      [(cons v2 vs)
       (if (eq v v2)
           #t
           (set-member?-helper eq vs v))]))

  (define (a) (set-intersect [s1 : (Set a)] [s2 : (Set a)]) : (Set a)
    (case s1
      [(set eq _)
       (set eq (filter (lambda ([x : a]) (set-member? s1 x)) (set->list s2)))])))

