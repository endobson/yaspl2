#:module (either)
#:import {
  (data ordering)
  (prim)
  (tuples)
}
(export
  (#:types Either)
  (#:values left right either=? either-cmp)
  (#:patterns left right))
(types
  (define-type (Either a b)
    (left [v : a])
    (right [v : b])))

(define [A B] (either=? [a=? : (A A -> Boolean)] [b=? : (B B -> Boolean)])
  : ((Either A B) (Either A B) -> Boolean)
  (lambda ([v1 : (Either A B)] [v2 : (Either A B)])
    (case (tuple2 v1 v2)
      [(tuple2 (left a1) (left a2))
       (a=? a1 a2)]
      [(tuple2 (right b1) (right b2))
       (b=? b1 b2)]
      [_ #f])))

(define [A B] (either-cmp [a-cmp : (A A -> Ordering)] [b-cmp : (B B -> Ordering)])
  : ((Either A B) (Either A B) -> Ordering)
  (lambda ([v1 : (Either A B)] [v2 : (Either A B)])
    (case (tuple2 v1 v2)
      [(tuple2 (left _) (right _))
       (less-than)]
      [(tuple2 (right _) (left _))
       (greater-than)]
      [(tuple2 (left a1) (left a2))
       (a-cmp a1 a2)]
      [(tuple2 (right b1) (right b2))
       (b-cmp b1 b2)])))
