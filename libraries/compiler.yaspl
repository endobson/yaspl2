(module compiler
  (import
    (prim
      #:types (Bytes Byte InputPort OutputPort )
      #:values (panic make-bytes read-bytes)
      #:patterns ())
    (io read-all-bytes write-all-bytes)
    (list
      #:types (List)
      #:values (empty cons append map list)
      #:patterns (empty cons))
    (dict make-dict dict-set dict-add-all)
    (tuples tuple2)
    (bytes bytes=?)
    (source-language
      #:types (Module)
      #:values (parse-module)
      #:patterns ())
    (sexp-parser
      #:types (Sexp)
      #:values (parse-sexps)
      #:patterns ())
    (source-to-stack
      #:types (ModuleSignature)
      #:values (compile-module module-signature)
      #:patterns (compiled-module))
    (stack-machine
      #:types (StackFunction)
      #:values ()
      #:patterns ())
    (x86-64-stack-machine compile-stack-machine)
    (prim-implementation prim-implementation)
    (either
      #:types (Either)
      #:values ()
      #:patterns (left right)))
  (export main cmain)
  (types)

  (define (prim-signature) : ModuleSignature
    (module-signature #"prim"
      (dict-add-all
        (make-dict bytes=?)
        (varargs list
          (tuple2 #"+" #"prim_add")
          (tuple2 #"-" #"prim_sub")
          (tuple2 #"*" #"prim_mult")
          (tuple2 #"=" #"prim_equal")
          (tuple2 #"<" #"prim_less_than")
          (tuple2 #"<=" #"prim_less_than_or_equal")
          (tuple2 #">" #"prim_greater_than")
          (tuple2 #">=" #"prim_greater_than_or_equal")
          (tuple2 #"bytes-ref" #"prim_bytes_ref")
          (tuple2 #"make-bytes" #"prim_make_bytes")
          (tuple2 #"bytes-set!" #"prim_bytes_set")
          (tuple2 #"bytes-length" #"prim_bytes_length")
          (tuple2 #"write-bytes" #"prim_write_bytes")
          (tuple2 #"read-bytes" #"prim_read_bytes")
          (tuple2 #"void" #"prim_void")
          (tuple2 #"and" #"prim_and")
          (tuple2 #"or" #"prim_or")
          (tuple2 #"quotient" #"prim_quotient")
          (tuple2 #"remainder" #"prim_remainder")
          (tuple2 #"panic" #"prim_panic")
          (tuple2 #"make-array" #"prim_make_array")
          (tuple2 #"array-length" #"prim_array_length")
          (tuple2 #"array-ref" #"prim_array_ref")
          (tuple2 #"array-set!" #"prim_array_set")))
      (make-dict bytes=?)))


  (define (compile-modules [mods : (List Module)] [sigs : (List ModuleSignature)])
    : (List StackFunction)
    (case mods
      [(empty) (prim-implementation)]
      [(cons mod mods)
       (case (compile-module mod sigs)
         [(compiled-module code sig)
          (append code (compile-modules mods (cons sig sigs)))])]))

  (define (extract-modules [vals : (List (Either Bytes Module))] [stderr : OutputPort]) : (List Module)
    (case vals
      [(empty) (empty)]
      [(cons (right v) vals) (cons v (extract-modules vals stderr))]
      [(cons (left v) _)
       (begin
         (write-all-bytes v stderr)
         (panic #""))]))

  (define (extract-sexps [either : (Either Bytes (List Sexp))] [stderr : OutputPort]) : (List Sexp)
    (case either
      [(left v) (panic v)]
      [(right (empty))
       (begin
         (write-all-bytes #"No provided modules" stderr)
         (panic #""))]
      [(right sexps) sexps]))

  (define (main [stdin : InputPort] [stdout : OutputPort] [stderr : OutputPort]) : Byte
    (begin
      (compile-stack-machine
        (compile-modules
          (extract-modules
            (map parse-module
              (extract-sexps
                (parse-sexps (read-all-bytes stdin))
                stderr))
            stderr)
          (cons (prim-signature) (empty)))
        stdout)
      0)))
