#:module (stack-trace)
#:import {
  (bytes)
  (data ordering)
  (data red-black-tree)
  (either)
  (list)
  (mach-o)
  (maybe)
  (numbers)
  (prim)
  (tuples)
}
(export
  (#:types StackSnapshot)
  (#:values make-stack-snapshot stack-snapshot->stack-trace)
  (#:patterns))
(types
  (define-type Address
    (address [v : Int]))
  (define-type StackSnapshot
    (stack-snapshot
      [start-address : Int]
      [size : Int]
      [buffer : Bytes])))

(define (make-stack-snapshot) : StackSnapshot
  (match-define buffer (make-bytes 800))
  (match-define metadata (make-bytes 8))
  (match-define stack-size (fill-stack buffer metadata))
  (stack-snapshot
    (u64->s64 (bytes-ref/u64-le metadata 0))
    stack-size
    buffer))

(define (read-stack-address [ss : StackSnapshot] [address : Int]) : Int
  (match-define (stack-snapshot start-address size buffer) ss)
  (u64->s64 (bytes-ref/u64-le buffer (- address start-address))))

(define (read-return-addresses [ss : StackSnapshot]) : (List Address)
  (read-return-addresses* ss (stack-snapshot-start-address ss)))

(define (read-return-addresses* [ss : StackSnapshot] [base-pointer : Int]) : (List Address)
  (match-define next-base-pointer (read-stack-address ss base-pointer))
  (if (= 0 next-base-pointer)
      (empty)
      (cons
        (address (read-stack-address ss (+ 8 base-pointer)))
        (read-return-addresses* ss next-base-pointer))))

(define (make-symbol-map [symbols : (List SymbolTableEntry)])
  : (RedBlackTree Address Bytes)
  (foldl
    (lambda ([entry : SymbolTableEntry] [acc : (RedBlackTree Address Bytes)])
      (case entry
        [(symbol-table-entry name type section description value)
         (rb-tree-set acc (address (u64->s64 value)) name)]))
    symbols
    (ann (RedBlackTree Address Bytes) (make-rb-tree address-cmp))))

(define (address-cmp [l : Address] [r : Address]) : Ordering
  (match-define (address lv) l)
  (match-define (address rv) r)
  (int-cmp lv rv))

(define (stack-snapshot->stack-trace [ss : StackSnapshot]) : Bytes
  (case (read-mach-o/mem)
    [(left v)
     (bytes-append (varargs list #"Error: " v))]
    [(right mach-o)
     (match-define symbol-map (make-symbol-map (read-symbol-table mach-o)))
     (bytes-append
       (map
         (lambda ([addr : Address])
           (case (rb-tree-infimum symbol-map addr)
             [(nothing)
              #"No function\n"]
             [(just (tuple2 (address function-start) name))
              (match-define (address v) addr)
              (bytes-append
                (varargs list
                  #"["
                  (integer->hex-bytes v)
                  #"] "
                  name #"+" (integer->decimal-bytes (- v function-start))
                  #"\n")) ]))
         (read-return-addresses ss)))]))
