#:module (intermediate-to-stack)
#:import {
  (bytes)
  (intermediate-language)
  (join-list)
  (list)
  (module-name)
  (prim)
  (stack-machine)
  (yaspl module-environment)
  (yaspl top-level-objects)
}
(export
  #:types ()
  #:values (compile-variant-definition/constructor compile-variant-definition/accessors)
  #:patterns ())
(types)


(define (compile-variant-definition/constructor
          [mod-name : ModName]
          [type-name : Bytes]
          [def : VariantDefinition]) : (JoinList TopLevelObject)
  (match-define (variant-definition name variant-tag fields) def)
  (match-define mangled-name
    (mangle-name (bytes-append (varargs list (mangled-mod-name mod-name) #"_" type-name #"_" name))))
  (cons-jl
    (trivial-closure-tlo
      (bytes-append (varargs list mangled-name #"_closure"))
      mangled-name)
    (case fields
      [(empty)
       (match-define const-name (bytes-append (varargs list mangled-name #"_const")))
       (varargs join-list
         (trivial-variant-tlo const-name variant-tag)
         (stack-function-tlo
           (stack-function
             mangled-name
             (length fields)
             (cons
               (stack-basic-block
                 (cons (load-global-cmd const-name) (empty))
                 (return 0))
               (empty)))))]
      [_
       (single-jl
         (stack-function-tlo
           (stack-function
             mangled-name
             (length fields)
             (cons
               (stack-basic-block
                 (add-reversed-args
                   0
                   (length fields)
                   (cons
                     (alloc-variant-cmd variant-tag (length fields))
                     (empty)))
                 (return (length fields)))
               (empty)))))])))


(define (compile-variant-definition/accessors
          [mod-name : ModName]
          [type-name : Bytes]
          [variant : VariantDefinition])
  : (JoinList TopLevelObject)
  (case variant
    [(variant-definition variant-name variant-tag fields)
     (compile-variant-definition/accessors*
       mod-name
       type-name
       variant-name
       variant-tag
       0
       fields)]))

(define (compile-variant-definition/accessors*
          [mod-name : ModName]
          [type-name : Bytes]
          [variant-name : Bytes]
          [variant-tag : Int]
          [field-index : Int]
          [fields : (List VariantField)])
  : (JoinList TopLevelObject)
  (case fields
    [(empty) (empty-jl)]
    [(cons (variant-field field-name _) fields)
     (append-jl
       (compile-variant-definition-accessor
         mod-name
         type-name
         variant-name
         field-name
         variant-tag
         field-index)
       (compile-variant-definition/accessors*
         mod-name
         type-name
         variant-name
         variant-tag
         (+ field-index 1)
         fields))]))

(define (compile-variant-definition-accessor
          [mod-name : ModName]
          [type-name : Bytes]
          [variant-name : Bytes]
          [field-name : Bytes]
          [variant-tag : Int]
          [field-index : Int]) : (JoinList TopLevelObject)
  (let ([mangled-name
         (mangle-name
           (bytes-append (varargs list (mangled-mod-name mod-name) #"_" type-name #"_" variant-name
                                  #"_" field-name)))])
    (varargs join-list
      (stack-function-tlo
        (stack-function
          mangled-name
          1
          (cons
            (stack-basic-block
              (add-reversed-args 0 1 (cons (variant-ref-cmd variant-tag field-index) (empty)))
              (return 1))
            (empty))))
      (trivial-closure-tlo
        (bytes-append (varargs list mangled-name #"_closure"))
        mangled-name))))

(define (add-reversed-args [arg-number : Int] [args : Int] [cmds : (List StackCmd)]) : (List StackCmd)
  (if (= arg-number args)
      cmds
      (add-reversed-args
        (+ arg-number 1)
        args
        (cons (dup-cmd (- (* 2 args) (* 2 (+ arg-number 1)))) cmds))))
