(module intermediate-to-stack
  (import
    (prim
      #:types (Bytes Boolean Byte)
      #:values (panic + * - = > bytes-length bytes-ref bytes-set! make-bytes or)
      #:patterns ())
    (list
      #:types (List)
      #:values (cons empty length append map append* reverse list foldl)
      #:patterns (cons empty))
    (bytes bytes=? bytes-append)
    (either
      #:types (Either)
      #:values (right left)
      #:patterns (right left))
    (dict
      #:types (Dict)
      #:values (make-dict dict-ref dict-add) 
      #:patterns ())
    (maybe
      #:types (Maybe)
      #:values ()
      #:patterns (just nothing))
    (tuples
      #:types (Tuple2)
      #:values (tuple2)
      #:patterns (tuple2))
    (join-list
      #:types ()
      #:values (jl->list empty-jl append-jl snoc-jl)
      #:patterns ())
    (environment
      #:types (Environment VariantInfo VarReference)
      #:values (environment global-value local-var free-var variant-info
                environment-tag-ref environment-value-ref environment-value-set)
      #:patterns (local-var global-value free-var))
    (intermediate-to-module-signature
      #:types (ModuleSignature)
      #:values (module->module-signature 
                module-signature-name module-signature-pattern-exports module-signature-value-exports)
      #:patterns ())
    (source-to-intermediate-language source->intermediate)
    (stack-machine
      #:types (StackFunction StackBasicBlock StackCmd StackTerminal GenState TopLevelObject)
      #:values (stack-function stack-basic-block gen-state-cmds gen-state
                initial-variables return add-byte-literal name-temporary unscope-variable
                variable-ref create-closure load-global call-function add-boolean-literal
                remove-temporaries tmp-slot boolean-jmp uncond-jmp add-bytes-literal ignore-value
                alloc-variant-cmd dup-cmd variant-ref-cmd variant-switch no-catch-all-case
                dup-temporary catch-all-case variant-case pop-cmd boolean-lit-cmd
                variant-ref var-slot swap-cmd assert-stack-equal? add-bytes-cmp add-byte-cmp
                make-varargs-array function-tlo trivial-closure-tlo load-free-var)
      #:patterns (gen-state tmp-slot var-slot))
    (source-language
      #:types (Module FunctionDefinition Imports Import TypeDefinition VariantDefinition
               VariantField Pattern)
      #:values (module-name module-definitions module-types
                function-definition-name
                type-definition-variants)
      #:patterns (module imports import function-definition
                  variant-definition type-definition
                  variant-field ignore-pattern variable-pattern
                  abstraction-pattern bytes-pattern byte-pattern))
    (intermediate-language
      #:types ((Module i:Module)
               (FunctionDefinition i:FunctionDefinition)
               Expression CaseClause ClosureConstant)
      #:values ((module-name i:module-name)
                (module-closures i:module-closures)
                (module-definitions i:module-definitions)
                (module-types i:module-types)
                (function-definition-name i:function-definition-name)
                closure-constant-name)
      #:patterns ((module i:module)
                  (function-definition i:function-definition)
                  bytes-literal byte-literal boolean-literal let-expr local-var-expr
                  global-var-expr if-expr
                  create-closure-expr call-closure-expr
                  begin-expr varargs-call-closure-expr
                  case-expr case-clause
                  closure-constant)))
  (export compile-module)
  (types
    (define-type CompiledModule
      (compiled-module [code (List TopLevelObject)] [sig ModuleSignature])))

  (define (compile-module [imod : i:Module] [sigs : (List ModuleSignature)]) :
    (Either Bytes CompiledModule)
    (case (make-global-env imod sigs)
      [(left v) (left v)]
      [(right env)
       (right
         (compiled-module
           (append
             (compile-type-definitions imod)
             (append
               (compile-function-definitions imod env)
               (compile-closures imod)))
           (module->module-signature imod env)))]))


  (define (make-global-env [imod : i:Module] [sigs : (List ModuleSignature)])
    : (Either Bytes Environment)
    (case (add-all-imported-patterns
            (add-type-definitions-variant-infos/top
              (initial-variant-bindings) imod)
            imod sigs)
      [(left v) (left v)]
      [(right pattern-env)
       (case (setup-value-environment imod sigs)
         [(left v) (left v)]
         [(right value-env)
          (right
            (environment
              value-env
              pattern-env))])]))

  (define (setup-value-environment [imod : i:Module] [sigs : (List ModuleSignature)])
    : (Either Bytes (Dict Bytes VarReference))
    (add-all-imported-functions
      (add-type-definitions-functions/top
        (add-locally-defined-functions
          (add-locally-defined-closures
            (initial-value-bindings)
            imod)
          imod)
        imod)
      imod sigs))


  (define (initial-value-bindings) : (Dict Bytes VarReference)
    (make-dict bytes=?))

  (define (initial-variant-bindings) : (Dict Bytes VariantInfo)
    (make-dict bytes=?))


  (define (add-all-imported-patterns
            [env : (Dict Bytes VariantInfo)]
            [imod : i:Module]
            [sigs : (List ModuleSignature)]) : (Either Bytes (Dict Bytes VariantInfo))
    (case imod
      [(i:module _ imports _ _ _ _)
       (add-all-imports/patterns env imports sigs)]))

  (define (add-all-imports/patterns
            [env : (Dict Bytes VariantInfo)]
            [imports : (List Imports)]
            [sigs : (List ModuleSignature)]) : (Either Bytes (Dict Bytes VariantInfo))
    (case imports
      [(empty) (right env)]
      [(cons (imports mod-name _ _ patterns) imports)
       (case (lookup-module-signature mod-name sigs)
         [(left v) (left v)]
         [(right sig)
          (case (add-imports/patterns env patterns sig)
            [(left v) (left v)]
            [(right env)
             (add-all-imports/patterns env imports sigs)])])]))

  (define (add-imports/patterns
            [env : (Dict Bytes VariantInfo)]
            [imports : (List Import)]
            [sig : ModuleSignature]) : (Either Bytes (Dict Bytes VariantInfo))
    (case imports
      [(empty) (right env)]
      [(cons (import exported-name local-name) imports)
       (case (dict-ref (module-signature-pattern-exports sig) exported-name)
         [(nothing)
          (left
            (bytes-append
              (cons #"No corresponding export for pattern import of "
                (cons exported-name (empty)))))]
         [(just tag)
          (add-imports/patterns
            (dict-add env local-name (variant-info tag))
            imports
            sig)])]))




  (define (add-all-imported-functions
            [env : (Dict Bytes VarReference)]
            [imod : i:Module]
            [sigs : (List ModuleSignature)]) : (Either Bytes (Dict Bytes VarReference))
    (case imod
      [(i:module _ imports _ _ _ _)
       (add-all-imports env imports sigs)]))

  (define (add-all-imports
            [env : (Dict Bytes VarReference)]
            [imps : (List Imports)]
            [sigs : (List ModuleSignature)]) : (Either Bytes (Dict Bytes VarReference))
    (case imps
      [(empty) (right env)]
      [(cons (imports mod-name _ values _) imps)
       (case (lookup-module-signature mod-name sigs)
         [(left v) (panic v)]
         [(right sig)
          (case (add-imports env values sig)
            [(left v) (left v)]
            [(right env) (add-all-imports env imps sigs)])])]))

  (define (add-imports
            [env : (Dict Bytes VarReference)]
            [imps : (List Import)]
            [sig : ModuleSignature]) : (Either Bytes (Dict Bytes VarReference))
    (case imps
      [(empty) (right env)]
      [(cons (import exported-name local-name) imps)
       (case (dict-ref (module-signature-value-exports sig) exported-name)
         [(nothing)
          (left
            (bytes-append
              (cons #"No corresponding export for import of "
                (cons exported-name (empty)))))]
         [(just global-name)
          (add-imports
            (dict-add env local-name (global-value global-name))
            imps
            sig)])]))

  (define (lookup-module-signature [mod-name : Bytes] [sigs : (List ModuleSignature)])
    : (Either Bytes ModuleSignature)
    (case sigs
      [(empty) (left (bytes-append (cons #"No module found for import: " (cons mod-name (empty)))))]
      [(cons sig sigs)
       (if (bytes=? (module-signature-name sig) mod-name)
           (right sig)
           (lookup-module-signature mod-name sigs))]))

  (define (add-locally-defined-functions [env : (Dict Bytes VarReference)] [imod : i:Module])
    : (Dict Bytes VarReference)
    (let ([var-names (map i:function-definition-name (i:module-definitions imod))])
      (let ([mangled-names (get-mangled-names (i:module-name imod) (i:module-definitions imod))])
        (add-all-globals env var-names mangled-names))))

  (define (get-mangled-names [mod-name : Bytes] [funs : (List i:FunctionDefinition)]) : (List Bytes)
    (case funs
      [(empty) (empty)]
      [(cons (i:function-definition name _ _ _ _) funs)
       (cons
         (mangle-name (bytes-append (varargs list mod-name #"_" name)))
         (get-mangled-names mod-name funs))]))

  (define (i-get-mangled-names [mod-name : Bytes] [closures : (List ClosureConstant)]) : (List Bytes)
    (case closures
      [(empty) (empty)]
      [(cons (closure-constant name _) closures)
       (cons
         (mangle-name (bytes-append (varargs list mod-name #"_" name)))
         (i-get-mangled-names mod-name closures))]))

  (define (add-locally-defined-closures [env : (Dict Bytes VarReference)] [imod : i:Module])
    : (Dict Bytes VarReference)
    (let ([var-names (map closure-constant-name (i:module-closures imod))])
      (let ([mangled-names (i-get-mangled-names (i:module-name imod) (i:module-closures imod))])
        (add-all-globals env var-names mangled-names))))


  (define (add-all-globals [env : (Dict Bytes VarReference)]
                           [var-names : (List Bytes)]
                           [mangled-names : (List Bytes)])
    : (Dict Bytes VarReference)
    (case var-names
      [(empty) env]
      [(cons var-name var-names)
       (case mangled-names
         [(empty) (panic #"var-names not the same length as mangled-names")]
         [(cons mangled-name mangled-names)
          (add-all-globals
            (dict-add env var-name (global-value mangled-name)) var-names mangled-names)])]))

  (define (add-type-definitions-functions/top [env : (Dict Bytes VarReference)] [imod : i:Module])
    : (Dict Bytes VarReference)
    (let ([mod-name (i:module-name imod)])
      (foldl
        (lambda ([def : TypeDefinition] [env : (Dict Bytes VarReference)])
          (case def
            [(type-definition name _ variants)
             (add-variant-definitions-functions env mod-name name variants)]))
        (i:module-types imod)
        env)))


  (define (add-variant-definitions-functions
            [env : (Dict Bytes VarReference)]
            [mod-name : Bytes]
            [type-name : Bytes]
            [defs : (List VariantDefinition)]) : (Dict Bytes VarReference)
    (case defs
      [(empty) env]
      [(cons def defs)
       (add-variant-definitions-functions
         (add-variant-definition-functions env mod-name type-name def)
         mod-name
         type-name
         defs)]))

  (define (add-variant-definition-functions
            [env : (Dict Bytes VarReference)]
            [mod-name : Bytes]
            [type-name : Bytes]
            [def : VariantDefinition]) : (Dict Bytes VarReference)
    (case def
      [(variant-definition name fields)
       (add-variant-definition-accessors
         (dict-add
           env
           name
           (global-value
             (mangle-name
               (bytes-append
                 (varargs list mod-name #"_" type-name #"_" name #"_closure")))))
         mod-name
         type-name
         name
         fields)]))

  (define (add-variant-definition-accessors
            [env : (Dict Bytes VarReference)]
            [mod-name : Bytes]
            [type-name : Bytes]
            [variant-name : Bytes]
            [fields : (List VariantField)]) : (Dict Bytes VarReference)
    (case fields
      [(empty) env]
      [(cons (variant-field name _) fields)
       (add-variant-definition-accessors
         (dict-add
           env
           (bytes-append (cons variant-name (cons #"-" (cons name (empty)))))
           (global-value
             (mangle-name
               (bytes-append
                 (varargs list mod-name #"_" type-name #"_" variant-name #"_" name #"_closure")))))
         mod-name
         type-name
         variant-name
         fields)]))

  (define (add-type-definitions-variant-infos/top [env : (Dict Bytes VariantInfo)] [imod : i:Module])
    : (Dict Bytes VariantInfo)
    (add-type-definitions-variant-infos env (i:module-types imod)))

  (define (add-type-definitions-variant-infos
            [env : (Dict Bytes VariantInfo)]
            [defs : (List TypeDefinition)])
    : (Dict Bytes VariantInfo)
    (case defs
      [(empty) env]
      [(cons (type-definition _ _ variants) defs)
       (add-type-definitions-variant-infos
         (add-variant-definitions-variant-infos env 0 variants)
         defs)]))

  (define (add-variant-definitions-variant-infos
            [env : (Dict Bytes VariantInfo)]
            [index : Byte]
            [defs : (List VariantDefinition)])
    : (Dict Bytes VariantInfo)
    (case defs
      [(empty) env]
      [(cons (variant-definition name _) defs)
       (add-variant-definitions-variant-infos
         (dict-add env name (variant-info index))
         (+ index 1)
         defs)]))

  (define (add-all-locals [env : Environment] [names : (List Bytes)])
    : Environment
    (case names
      [(empty) env]
      [(cons name names) (add-all-locals (environment-value-set env name (local-var)) names)]))

  (define (add-all-free-vars [env : Environment] [names : (List Bytes)] [index : Byte])
    : Environment
    (case names
      [(empty) env]
      [(cons name names)
       (add-all-free-vars (environment-value-set env name (free-var index)) names (+ 1 index))]))


  (define (compile-type-definitions [imod : i:Module]) : (List TopLevelObject)
    (case imod
      [(i:module name _ _ types _ _)
       (compile-type-definitions/list name types (empty))]))

  (define (compile-type-definitions/list
            [mod-name : Bytes]
            [defs : (List TypeDefinition)]
            [acc : (List TopLevelObject)]) : (List TopLevelObject)
    (case defs
      [(empty) acc]
      [(cons def defs)
       (compile-type-definitions/list
         mod-name
         defs
         (compile-type-definition mod-name def acc))]))

  (define (compile-type-definition
            [mod-name : Bytes]
            [def : TypeDefinition]
            [acc : (List TopLevelObject)]) : (List TopLevelObject)
    (case def
      [(type-definition name _ variants)
       (compile-type-definition/variants mod-name name 0 variants acc)]))

  (define (compile-type-definition/variants
            [mod-name : Bytes]
            [type-name : Bytes]
            [variant-index : Byte]
            [variants : (List VariantDefinition)]
            [acc : (List TopLevelObject)]) : (List TopLevelObject)
    (case variants
      [(empty) acc]
      [(cons variant variants)
       (compile-type-definition/variants
         mod-name
         type-name
         (+ variant-index 1)
         variants
         (compile-variant-definition mod-name type-name variant-index variant acc))]))

  (define (compile-variant-definition
            [mod-name : Bytes]
            [type-name : Bytes]
            [variant-index : Byte]
            [def : VariantDefinition]
            [acc : (List TopLevelObject)]) : (List TopLevelObject)
    (case def
      [(variant-definition name fields)
       (let ([mangled-name
              (mangle-name (bytes-append (varargs list mod-name #"_" type-name #"_" name)))])
         (cons
           (function-tlo
             (stack-function
               mangled-name
               (cons
                 (stack-basic-block
                   (add-reversed-args
                     (length fields)
                     (cons
                       (alloc-variant-cmd variant-index (length fields))
                       (empty)))
                   (return (length fields)))
                 (empty))))
           (cons
             (trivial-closure-tlo
               (bytes-append (varargs list mangled-name #"_closure"))
               mangled-name)
             (compile-variant-definition/accessors
               mod-name
               type-name
               name
               variant-index
               0
               fields
               acc))))]))

  (define (compile-variant-definition/accessors
            [mod-name : Bytes]
            [type-name : Bytes]
            [variant-name : Bytes]
            [variant-index : Byte]
            [field-index : Byte]
            [fields : (List VariantField)]
            [acc : (List TopLevelObject)]) : (List TopLevelObject)
    (case fields
      [(empty) acc]
      [(cons (variant-field name _) fields)
       (compile-variant-definition/accessors
         mod-name
         type-name
         variant-name
         variant-index
         (+ field-index 1)
         fields
         (let ([mangled-name
                (mangle-name
                  (bytes-append (varargs list mod-name #"_" type-name #"_" variant-name #"_" name)))])
           (cons
             (function-tlo
               (stack-function
                 mangled-name
                 (cons
                   (stack-basic-block
                     (add-reversed-args 1 (cons (variant-ref-cmd variant-index field-index) (empty)))
                     (return 1))
                   (empty))))
             (cons
               (trivial-closure-tlo
                 (bytes-append (varargs list mangled-name #"_closure"))
                 mangled-name)
               acc))))]))

  (define (add-reversed-args [args : Byte] [cmds : (List StackCmd)]) : (List StackCmd)
    (if (= 0 args)
        cmds
        (add-reversed-args
          (- args 1)
          (cons (dup-cmd (* 2 args)) cmds))))

  (define (compile-closures [imod : i:Module]) : (List TopLevelObject)
    (compile-closures/list (i:module-name imod) (i:module-closures imod)))

  (define (compile-closures/list
            [mod-name : Bytes]
            [pairs : (List ClosureConstant)])
    : (List TopLevelObject)
    (case pairs
      [(empty) (empty)]
      [(cons (closure-constant name fun-name) pairs)
       (cons
         (trivial-closure-tlo
           (mangle-name (bytes-append (varargs list mod-name #"_" name)))
           (mangle-name (bytes-append (varargs list mod-name #"_" fun-name))))
         (compile-closures/list mod-name pairs))]))

  (define (compile-function-definitions [imod : i:Module] [env : Environment]) : (List TopLevelObject)
    (compile-function-definitions/list (i:module-name imod) (i:module-definitions imod) env))

  (define (compile-function-definitions/list
            [mod-name : Bytes]
            [funs : (List i:FunctionDefinition)]
            [env : Environment]) : (List TopLevelObject)
    (case funs
      [(empty) (empty)]
      [(cons fun funs)
       (cons (compile-function-definition mod-name fun env)
             (compile-function-definitions/list mod-name funs env))]))

  (define (compile-function-definition
            [mod-name : Bytes]
            [fun : i:FunctionDefinition]
            [env : Environment]) : TopLevelObject
    (case fun
      [(i:function-definition name pre-type arg-names free-vars body)
       (function-tlo
         (stack-function
           (mangle-name (bytes-append (varargs list mod-name #"_" name)))
           ;; TODO validate the genstate has one temporary left in it, the return addr, and then the
           ;; variables.
           (case (compile-expression
                   body
                   (add-all-free-vars (add-all-locals env arg-names) free-vars 0)
                   (gen-state (initial-variables arg-names) (empty-jl) 0 (empty-jl)))
             [(gen-state _ cmds label blocks)
              (jl->list
                (snoc-jl
                  blocks
                  (stack-basic-block (jl->list cmds) (return (length arg-names)))))])))]))

  (define (compile-expressions
            [exprs : (List Expression)]
            [env : Environment]
            [state : GenState]) : GenState
    (case exprs
      [(empty) state]
      [(cons expr exprs)
       (compile-expressions exprs env
         (compile-expression expr env state))]))

  (define (compile-expression
            [expr : Expression]
            [env : Environment]
            [state : GenState]) : GenState
    (case expr
      [(byte-literal v) (add-byte-literal v state)]
      [(boolean-literal v) (add-boolean-literal v state)]
      [(bytes-literal v) (add-bytes-literal v state)]
      [(let-expr name expr body)
       (unscope-variable name
         (compile-expression
           body
           (environment-value-set env name (local-var))
           (name-temporary name
             (compile-expression expr env state))))]
      [(local-var-expr v)
       (case (environment-value-ref env v)
         [(just (local-var)) (variable-ref v state)]
         [(just (free-var offset))
          (load-free-var offset state)]
         [(just (global-value n))
          (load-global n state)]
         [(nothing) (panic (bytes-append (cons #"Unbound variable: " (cons v (empty)))))])]
      [(global-var-expr v)
       (panic #"global-var: NYI")]
      ;; TODO compile the expressions in the right order
      [(create-closure-expr op args)
       (create-closure (length args)
         (compile-expression op env
           (compile-expressions (reverse args) env state)))]
      ;; TODO make effects of op go before arguments
      [(call-closure-expr op args)
       (call-function (length args)
         (compile-expression op env
           (compile-expressions args env state)))]
      ;; TODO make effects of op go before arguments
      [(varargs-call-closure-expr op args)
       (call-function 1
         (compile-expression op env
           (make-varargs-array (length args)
             (compile-expressions args env state))))]
      [(if-expr c t f)
       (case (compile-expression c env state)
         [(gen-state stack cmds-c label-c-end blocks)
          (let ([stack-split (remove-temporaries 1 stack)])
            (let ([label-t-begin (+ label-c-end 1)])
              (case (compile-expression t env
                      (gen-state stack-split (empty-jl) label-t-begin (empty-jl)))
                [(gen-state stack-t cmds-t label-t-end blocks-t)
                 (let ([label-f-begin (+ label-t-end 1)])
                   (case (compile-expression f env
                           (gen-state stack-split (empty-jl) label-f-begin (empty-jl)))
                     [(gen-state stack-f cmds-f label-f-end blocks-f)
                      (let ([new-label (+ label-f-end 1)])
                        (begin
                          ; (assert-stack-equal? stack-f stack-t)
                          ; (assert-stack-equal? stack-f (tmp-slot stack-split))
                          (gen-state
                            (tmp-slot stack-split)
                            (empty-jl)
                            new-label
                            (append-jl
                              (snoc-jl
                                blocks
                                (stack-basic-block (jl->list cmds-c) (boolean-jmp label-t-begin label-f-begin)))
                              (append-jl
                                (snoc-jl
                                  blocks-t
                                  (stack-basic-block (jl->list cmds-t) (uncond-jmp new-label)))
                                (snoc-jl
                                  blocks-f
                                  (stack-basic-block (jl->list cmds-f) (uncond-jmp new-label))))))))]))])))])]
      [(begin-expr expr exprs)
       (compile-begin-exprs exprs env (compile-expression expr env state))]
      [(case-expr expr clauses)
       (let ([state (compile-expression expr env state)])
         (compile-case-clauses clauses env state))]))

  ;; This should take a stack of A :: Rest to B :: Rest.
  ;; Where A is the type being cased on and B is the return type
  (define (compile-case-clauses
            [clauses : (List CaseClause)]
            [env : Environment]
            [state : GenState]) : GenState
    (case clauses
      [(empty)
       (case state
         [(gen-state stack cmds label blocks)
          (gen-state
            (tmp-slot (remove-temporaries 1 stack))
            (empty-jl)
            (+ 1 label)
            (snoc-jl
              blocks
              (stack-basic-block
                (jl->list cmds)
                (variant-switch (no-catch-all-case)))))])]
      [(cons clause clauses)
       (compile-case-clause clause clauses env state)]))

  (define (compile-case-clause
            [clause : CaseClause]
            [clauses : (List CaseClause)]
            [env : Environment]
            [state : GenState]) : GenState
    (case clause
      [(case-clause pattern expr)
       (case (compile-pattern/match pattern env (dup-temporary state))
         [(gen-state match-stack match-cmds match-end-label match-blocks)
          (let ([stack-split (remove-temporaries 1 match-stack)])
            (let ([extract-start-label (+ 1 match-end-label)])
              (case (compile-pattern/extract pattern expr env
                      (gen-state stack-split
                                 (empty-jl)
                                 extract-start-label
                                 (empty-jl)))
                [(gen-state extract-stack extract-cmds extract-end-label extract-blocks)
                 (let ([other-start-label (+ 1 extract-end-label)])
                   (case (compile-case-clauses clauses env
                           (gen-state stack-split
                                      (empty-jl)
                                      other-start-label
                                      (empty-jl)))
                     [(gen-state other-stack other-cmds other-end-label other-blocks)
                      (let ([join-label (+ other-end-label 1)])
                        (begin
                          ; (assert-stack-equal? other-stack extract-stack)
                          ; (assert-stack-equal? extract-stack stack-split)
                          (gen-state
                            stack-split
                            (empty-jl)
                            join-label
                            (append-jl
                              (snoc-jl
                                match-blocks
                                (stack-basic-block
                                  (jl->list match-cmds)
                                  (boolean-jmp extract-start-label other-start-label)))
                              (append-jl
                                (snoc-jl
                                  extract-blocks
                                  (stack-basic-block
                                    (jl->list extract-cmds)
                                    (uncond-jmp join-label)))
                                (snoc-jl
                                  other-blocks
                                  (stack-basic-block
                                    (jl->list other-cmds)
                                    (uncond-jmp join-label))))))))]))])))])]))

  ;; Takes a stack of the form A :: Rest and turns it into the state Boolean :: Rest
  ;; where the top of the stack is true if the value matched the pattern.
  (define (compile-pattern/match
            [pattern : Pattern]
            [env : Environment]
            [state : GenState]) : GenState
    (case pattern
      [(ignore-pattern) (add-boolean-literal #t (ignore-value state))]
      [(variable-pattern _) (add-boolean-literal #t (ignore-value state))]
      [(abstraction-pattern name patterns)
       (let ([tag (environment-tag-ref env name)])
         (case state
           [(gen-state stack cmds old-label blocks)
            (let ([non-matching-label (+ 1 old-label)])
              (let ([matching-label (+ 1 non-matching-label)])
                (case (compile-variant-patterns/match tag 0 patterns env
                        (gen-state stack (empty-jl) matching-label (empty-jl)))
                  [(gen-state matching-stack matching-cmds matching-end-label matching-blocks)
                   (let ([join-label (+ 1 matching-end-label)])
                     (begin
                       ; (assert-stack-equal? matching-stack stack)
                       (gen-state
                         (tmp-slot (remove-temporaries 1 stack))
                         (empty-jl)
                         join-label
                         (append-jl
                           (snoc-jl
                             (snoc-jl
                               blocks
                               (stack-basic-block
                                 (jl->list cmds)
                                 (variant-switch
                                   (variant-case tag matching-label
                                     (catch-all-case non-matching-label)))))
                             (stack-basic-block
                               (cons (pop-cmd) (cons (boolean-lit-cmd #f) (empty)))
                               (uncond-jmp join-label)))
                           (snoc-jl
                             matching-blocks
                             (stack-basic-block
                               (jl->list matching-cmds)
                               (uncond-jmp join-label)))))))])))]))]
      [(bytes-pattern lit)
       (add-bytes-cmp (add-bytes-literal lit state))]
      [(byte-pattern lit)
       (add-byte-cmp (add-byte-literal lit state))]))

  ;; Takes a stack of the form A :: Rest and turns it into the state Boolean :: Rest
  ;; where the top of the stack is true if the value matched all the patterns
  (define (compile-variant-patterns/match
            [tag : Byte]
            [field : Byte]
            [patterns : (List Pattern)]
            [env : Environment]
            [state : GenState]) : GenState
    (case patterns
      [(empty) (add-boolean-literal #t (ignore-value state))]
      [(cons pattern patterns)
       (let ([state (variant-ref tag field (dup-temporary state))])
         (case (compile-pattern/match pattern env state)
           [(gen-state stack cmds label blocks)
            (let ([non-matching-label (+ label 1)])
              (let ([matching-label (+ non-matching-label 1)])
                (case (compile-variant-patterns/match tag (+ field 1) patterns env
                        (gen-state (remove-temporaries 1 stack) (empty-jl) matching-label (empty-jl)))
                  [(gen-state matching-stack matching-cmds matching-end-label matching-blocks)
                   (let ([join-label (+ matching-end-label 1)])
                     (begin
                       ; (assert-stack-equal? matching-stack (remove-temporaries 1 stack))
                       (gen-state
                         matching-stack
                         (empty-jl)
                         join-label
                         (append-jl
                           (snoc-jl
                             (snoc-jl
                               blocks
                               (stack-basic-block
                                 (jl->list cmds)
                                 (boolean-jmp matching-label non-matching-label)))
                             (stack-basic-block
                               (cons (pop-cmd) (cons (boolean-lit-cmd #f) (empty)))
                               (uncond-jmp join-label)))
                           (snoc-jl
                             matching-blocks
                             (stack-basic-block
                               (jl->list matching-cmds)
                               (uncond-jmp join-label)))))))])))]))]))

  ;; Takes a stack of the form A :: Rest and sets it up with some variables
  ;; on top of Rest before evaluating expr, and then cleans up all those variables.
  (define (compile-pattern/extract
            [pattern : Pattern]
            [expr : Expression]
            [env : Environment]
            [state : GenState]) : GenState
    (let ([extractions (compute-extractions (cons (tuple2 pattern (empty)) (empty)) env (empty))])
      (cleanup-extractions/state
        (reverse extractions)
        (compile-expression
          expr
          (run-extractions/env extractions env)
          (run-extractions/state extractions state)))))

  (define (run-extractions/state
            [extractions : (List (Tuple2 Bytes (List (Tuple2 Byte Byte))))]
            [state : GenState]) : GenState
    (case extractions
      [(empty) (ignore-value state)]
      [(cons (tuple2 name path) extractions)
       (run-extractions/state
         extractions
         (swap-var
           (name-temporary name
             (extract-path (reverse path) (dup-temporary state)))))]))

  (define (cleanup-extractions/state
            [extractions : (List (Tuple2 Bytes (List (Tuple2 Byte Byte))))]
            [state : GenState]) : GenState
    (case extractions
      [(empty) state]
      [(cons (tuple2 name _) extractions)
       (cleanup-extractions/state
         extractions
         (unscope-variable name state))]))

  (define (extract-path [path : (List (Tuple2 Byte Byte))] [state : GenState]) : GenState
    (case path
      [(empty) state]
      [(cons (tuple2 tag field) path)
       (extract-path
         path
         (variant-ref tag field state))]))

  (define (swap-var [state : GenState]) : GenState
    (case state
      [(gen-state stack cmds label blocks)
       (case stack
         [(var-slot v (tmp-slot stack))
          (gen-state
            (tmp-slot (var-slot v stack))
            (snoc-jl cmds (swap-cmd))
            label
            blocks)]
         [_ (panic #"swap-var: Not a valid state")])]))

  (define (run-extractions/env
            [extractions : (List (Tuple2 Bytes (List (Tuple2 Byte Byte))))]
            [env : Environment]) : Environment
    (case extractions
      [(empty) env]
      [(cons (tuple2 name _) extractions)
       (run-extractions/env
         extractions
         (environment-value-set env name (local-var)))]))

  (define (compute-extractions
            [patterns : (List (Tuple2 Pattern (List (Tuple2 Byte Byte))))]
            [env : Environment]
            [acc-extractions : (List (Tuple2 Bytes (List (Tuple2 Byte Byte))))])
    : (List (Tuple2 Bytes (List (Tuple2 Byte Byte))))
    (case patterns
      [(empty) acc-extractions]
      [(cons (tuple2 pattern path) patterns)
       (case pattern
         [(ignore-pattern) (compute-extractions patterns env acc-extractions)]
         [(bytes-pattern _) (compute-extractions patterns env acc-extractions)]
         [(byte-pattern _) (compute-extractions patterns env acc-extractions)]
         [(variable-pattern v)
          (compute-extractions patterns env (cons (tuple2 v path) acc-extractions))]
         [(abstraction-pattern name inner-patterns)
          (let ([tag (environment-tag-ref env name)])
            (compute-extractions
              (compute-extraction-paths inner-patterns path tag 0 patterns)
              env
              acc-extractions))])]))

  (define (compute-extraction-paths
            [patterns : (List Pattern)]
            [path : (List (Tuple2 Byte Byte))]
            [tag : Byte]
            [field : Byte]
            [acc : (List (Tuple2 Pattern (List (Tuple2 Byte Byte))))])
    : (List (Tuple2 Pattern (List (Tuple2 Byte Byte))))
    (case patterns
      [(empty) acc]
      [(cons pattern patterns)
       (compute-extraction-paths
         patterns
         path
         tag
         (+ field 1)
         (cons (tuple2 pattern (cons (tuple2 tag field) path)) acc))]))


  (define (compile-begin-exprs
            [exprs : (List Expression)]
            [env : Environment]
            [state : GenState]) : GenState
    (case exprs
      [(empty) state]
      [(cons expr exprs)
       (compile-begin-exprs exprs env (compile-expression expr env (ignore-value state)))]))


  (define (mangle-name [name : Bytes]) : Bytes
    (let ([len (bytes-length name)])
      (mangle-name/helper name (make-bytes len) (- len 1))))

  (define (mangle-name/helper [orig-name : Bytes] [buf : Bytes] [index : Byte]) : Bytes
    (if (> 0 index)
        buf
        (let ([byte (bytes-ref orig-name index)])
          (let ([new-byte
                 (if (or (= byte 45)
                         (or (= byte 42) 
                             (or (= byte 62)
                                 (or (= byte 47)
                                     (or (= byte 61)
                                         (or (= byte 63)
                                             (= byte 33)))))))
                     95
                     byte)])
            (begin
              (bytes-set! buf index new-byte)
              (mangle-name/helper orig-name buf (- index 1)))))))

  )


