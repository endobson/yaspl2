#:module (register-language-compiler)
#:import {
  (chained-register-language)
  (chained-register-language-to-x86-64)
  (color-lowered-register-language)
  (dict)
  (either)
  {(lower-x86-64)
    {#:types
      Reg
      X8664Register
    }
    {#:values
      lower-function
      x86-64-register=?
    }
    {#:patterns
      fixed-reg
      temporary-reg
    }
  }
  {(lowered-register-language)
    {#:types
      [FunctionDefinition l:FunctionDefinition]
    }
  }
  (prim)
  {(register-language)
    {#:types
      [FunctionDefinition r:FunctionDefinition]
    }
  }
  {(simplify-assigned-register-language)
    {#:values
      [simplify-function-definition ar:simplify-function-definition]
    }
  }
  {(simplify-assigned-register-language-x86-64)
    {#:values
      [simplify-function-definition ar-x86-64:simplify-function-definition]
    }
  }
  (tuples)
  (yaspl x86-64-assembly)
}
(export
  (#:types CompileFunctionIntermediates)
  (#:values compile-function compile-function/intermediates)
  (#:patterns compile-function-intermediates))
(types
  (define-type CompileFunctionIntermediates
    (compile-function-intermediates
      [register : r:FunctionDefinition]
      [original-lowered : (l:FunctionDefinition Reg)]
      [spilled-lowered : (l:FunctionDefinition Reg)]
      [assem : TextFragment])))


(define (compile-function [f : r:FunctionDefinition])
  : (Either Bytes TextFragment)
  (case (compile-function/intermediates f)
    [(left v) (left v)]
    [(right intermediates)
     (right (compile-function-intermediates-assem intermediates))]))

(define (compile-function/intermediates [f : r:FunctionDefinition])
  : (Either Bytes CompileFunctionIntermediates)
  (match-define original-lowered (lower-function f))
  (match-define (tuple2 spilled-lowered assignments) (color-function original-lowered))
  (match-define assigned-reg
    (assign-registers
      spilled-lowered
      (lambda ([r : Reg]) : X8664Register
        (case r
          [(fixed-reg r) r]
          [(temporary-reg v)
           (dict-ref/panic assignments v)]))))
  (match-define opt-assigned-reg
    (ar-x86-64:simplify-function-definition
      (ar:simplify-function-definition
        assigned-reg
        x86-64-register=?)))
  (match-define assembly
    (chained-function-definition->assembly-fragment
      (layout-function-definition opt-assigned-reg)))
  (right (compile-function-intermediates f original-lowered spilled-lowered assembly)))
