#:module (register-language)
#:import {
  (bytes)
  (dict)
  (list)
  (numbers)
  (prim)
  (prim-language) {
    #:types
      BinOp
      ComparisonOp
  }
  (syscalls)
}
(export
  #:types (FunctionDefinition BasicBlock Instruction Terminal Var Condition UnaryOp CallingConvention)
  #:values (function-definition return basic-block bin-op-inst var cond-jump jump
            var=? int-literal var->bytes var-condition comparison-condition negated-var-condition
            function-call-inst indirect-function-call syscall
            function-allocate-inst pointer-set!-inst pointer-ref-inst
            eight-byte-mem-op unary-op-inst truncate-unsigned-int64->unsigned-int8 symbol-address-inst
            closure-call heap-allocate-inst memory-compare-condition halt plain-function-convention
            closure-convention)
  #:patterns (function-definition var))
(types
  (define-type FunctionDefinition
    (function-definition
      [name : Bytes]
      [calling-convention : CallingConvention]
      [blocks : (Dict Int BasicBlock)]
      [next-var-number : Int]
      [next-block-number : Int]))
  (define-type CallingConvention
    (plain-function-convention [num-args : Int] [block : Int])
    (closure-convention [num-args : Int] [block : Int] [num-free-args : Int]))
  (define-type Var
    (var [name : Bytes] [counter : Int]))
  (define-type BasicBlock
    (basic-block [args : (List Var)] [instructions : (List Instruction)] [terminal : Terminal]))

  (define-type Instruction
    (int-literal [output : Var] [v : Int])
    (bin-op-inst [output : Var] [op : BinOp] [left : Var] [right : Var])
    (function-call-inst [output : Var] [f : FunctionKind] [args : (List Var)])
    (unary-op-inst [output : Var] [op : UnaryOp] [expr : Var])
    (pointer-set!-inst [size : MemoryOperationSize] [pointer : Var] [offset : Int] [val : Var])
    (pointer-ref-inst [output : Var] [size : MemoryOperationSize] [pointer : Var] [offset : Int])
    (memory-compare-inst [output : Var] [size : Var] [v1 : Var] [v2 : Var])
    (symbol-address-inst [output : Var] [name : Bytes])
    (function-allocate-inst [output : Var] [num-bytes : Var])
    (heap-allocate-inst [output : Var] [num-bytes : Var]))

  (define-type FunctionKind
    (indirect-function-call [f : Var])
    (closure-call [c : Var])
    (syscall [s : Syscall]))

  (define-type UnaryOp
    (truncate-unsigned-int64->unsigned-int8))

  (define-type MemoryOperationSize
    (one-byte-mem-op)
    (eight-byte-mem-op))

  (define-type Condition
    (var-condition [name : Var])
    (negated-var-condition [name : Var])
    (comparison-condition [op : ComparisonOp] [left : Var] [right : Var])
    (memory-compare-condition [negated? : Boolean] [size : Var] [v1 : Var] [v2 : Var]))

  (define-type Terminal
    (return [name : Var])
    (halt)
    (cond-jump [cond : Condition]
               [true : Int] [true-args : (List Var)]
               [false : Int] [false-args : (List Var)])
    (jump [index : Int] [args : (List Var)])))

(define (var=? [v1 : Var] [v2 : Var]) : Boolean
  (match-define (var n1 c1) v1)
  (match-define (var n2 c2) v2)
  (and (bytes=? n1 n2) (= c1 c2)))

(define (var->bytes [v : Var]) : Bytes
  (match-define (var name counter) v)
  (bytes-append (varargs list name #"_" (integer->decimal-bytes counter))))
