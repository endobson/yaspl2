#:module (register-language)
#:import {
  (dict)
  (either)
  (list)
  (maybe)
  (prim)
  (prim-language) {
    #:types
      BinOp
  }
  (syscalls)
  (yaspl math-operations)
  (yaspl var)
}
(export
  #:types (FunctionDefinition BasicBlock Instruction Terminal Condition UnaryOp CallingConvention
           FunctionKind MemoryOperationSize CompareMemArg PointerOffset PointerOffsetScale)
  #:values (function-definition return basic-block bin-op-inst cond-jump jump tail-call
            int-literal var-condition comparison-condition negated-var-condition
            function-call-inst indirect-function-call syscall
            function-allocate-inst pointer-set!-inst pointer-ref-inst
            eight-byte-mem-op one-byte-mem-op
            unary-op-inst truncate-unsigned-int64->unsigned-int8 boolean-not symbol-address-inst
            closure-call heap-allocate-inst memory-compare-condition halt plain-function-convention
            closure-convention direct-call output-var numeric-op-immediate-inst comparison-op-immediate-inst
            fill-memory-inst compare-mem-arg-val compare-mem-arg-ptr fixed-pointer-offset
            scaled-pointer-offset one-byte-scale eight-byte-scale)
  #:patterns (function-definition basic-block
              direct-call indirect-function-call closure-call syscall
              closure-convention plain-function-convention
              eight-byte-mem-op one-byte-mem-op
              int-literal bin-op-inst function-call-inst unary-op-inst pointer-set!-inst
              pointer-ref-inst memory-compare-inst symbol-address-inst function-allocate-inst
              heap-allocate-inst
              return halt jump cond-jump tail-call
              var-condition negated-var-condition comparison-condition memory-compare-condition
              truncate-unsigned-int64->unsigned-int8 boolean-not numeric-op-immediate-inst
              comparison-op-immediate-inst fill-memory-inst compare-mem-arg-val compare-mem-arg-ptr
              fixed-pointer-offset scaled-pointer-offset one-byte-scale eight-byte-scale))
(types
  (define-type FunctionDefinition
    (function-definition
      [name : Bytes]
      [calling-convention : CallingConvention]
      [blocks : (Dict Int BasicBlock)]
      [next-var-number : Int]
      [next-block-number : Int]))
  (define-type CallingConvention
    (plain-function-convention [num-args : Int] [block : Int])
    (closure-convention [num-args : Int] [block : Int] [num-free-args : Int]))
  (define-type BasicBlock
    (basic-block [args : (List Var)] [instructions : (List Instruction)] [terminal : Terminal]))

  (define-type Instruction
    (int-literal [output : Var] [v : Int])
    (bin-op-inst [output : Var] [op : BinOp] [left : Var] [right : Var])
    (function-call-inst [output : Var] [f : FunctionKind] [args : (List Var)])
    (unary-op-inst [output : Var] [op : UnaryOp] [input : Var])
    (pointer-set!-inst [size : MemoryOperationSize] [pointer : Var] [offset : Int] [val : Var])
    (fill-memory-inst [size : MemoryOperationSize] [pointer : Var] [offset : Int] [val : Var]
                      [amount : Var])
    (pointer-ref-inst [output : Var] [size : MemoryOperationSize] [pointer : Var]
                      [offset : PointerOffset])
    (memory-compare-inst [output : Var] [size : Var] [v1 : Var] [v2 : Var])
    (symbol-address-inst [output : Var] [name : Bytes])
    (function-allocate-inst [output : Var] [num-bytes : Var])
    (heap-allocate-inst [output : Var] [num-bytes : Var])
    (numeric-op-immediate-inst [output : Var] [op : NumericBinOp] [input : Var] [amount : S32])
    (comparison-op-immediate-inst [output : Var] [op : ComparisonBinOp] [input : Var] [amount : S32]))

  (define-type PointerOffset
    (fixed-pointer-offset [offset : Int])
    (scaled-pointer-offset [amount : Var] [scale : PointerOffsetScale] [fixed-offset : Int]))
  (define-type PointerOffsetScale
    (one-byte-scale)
    (eight-byte-scale))

  (define-type FunctionKind
    (direct-call [f : Bytes])
    (indirect-function-call [f : Var])
    (closure-call [c : Var])
    (syscall [s : Syscall]))

  (define-type UnaryOp
    (truncate-unsigned-int64->unsigned-int8)
    (boolean-not))

  (define-type MemoryOperationSize
    (one-byte-mem-op)
    (eight-byte-mem-op))

  (define-type Condition
    (var-condition [name : Var])
    (negated-var-condition [name : Var])
    (comparison-condition [op : ComparisonBinOp] [left : CompareMemArg] [right : (Either Var S32)])
    (memory-compare-condition [negated? : Boolean] [size : Var] [v1 : Var] [v2 : Var]))
  (define-type CompareMemArg
    (compare-mem-arg-val [v : Var])
    (compare-mem-arg-ptr [v : Var]))

  (define-type Terminal
    (return [name : Var])
    (halt)
    (cond-jump [cond : Condition]
               [true : Int] [true-args : (List Var)]
               [false : Int] [false-args : (List Var)])
    (tail-call [f : FunctionKind] [args : (List Var)])
    (jump [index : Int] [args : (List Var)])))

(define (output-var [i : Instruction]) : (Maybe Var)
  (case i
    [(int-literal output _)
     (just output)]
    [(bin-op-inst output _ _ _)
     (just output)]
    [(function-call-inst output _ _)
     (just output)]
    [(unary-op-inst output _ _)
     (just output)]
    [(pointer-set!-inst _ _ _ _)
     (nothing)]
    [(pointer-ref-inst output _ _ _)
     (just output)]
    [(fill-memory-inst _ _ _ _ _)
     (nothing)]
    [(memory-compare-inst output _ _ _)
     (just output)]
    [(symbol-address-inst output _)
     (just output)]
    [(function-allocate-inst output _)
     (just output)]
    [(heap-allocate-inst output _)
     (just output)]
    [(numeric-op-immediate-inst output _ _ _)
     (just output)]
    [(comparison-op-immediate-inst output _ _ _)
     (just output)]))
