#:module (register-language)
#:import {
  (block-number)
  (data indexed-set)
  (list)
  (maybe)
  (prim)
  (prim-language variant-info)
  (yaspl math-operations)
  (yaspl var)
}
(export
  (#:types FunctionDefinition BasicBlock Instruction Terminal Condition UnaryOp CallingConvention
           FunctionKind MemoryOperationSize CompareArgs CompareMemArg PointerOffset PointerOffsetScale
           VarOrS32 VarOrS8 JumpTarget)
  (#:values function-definition return basic-block bin-op-inst
            cond-jump jump block-call tail-call jump-target
            int-literal var-condition comparison-condition negated-var-condition
            negated-variant-tag-condition
            indirect-function-call syscall
            pointer-set!-inst pointer-ref-inst variant-ref-inst
            eight-byte-mem-op four-byte-mem-op two-byte-mem-op one-byte-mem-op
            unary-op-inst truncate-unsigned-int64->unsigned-int8 boolean-not symbol-address-inst
            closure-call variant-allocate-inst closure-allocate-inst memory-compare-condition halt
            plain-function-convention
            closure-convention direct-call output-var
            compare-s64 compare-s8 compare-mem-arg-val compare-mem-arg-ptr fixed-pointer-offset
            scaled-pointer-offset one-byte-scale eight-byte-scale
            basic-block-number basic-block-terminal var-or-s32-s32 var-or-s32-var var-or-s8-var var-or-s8-s8
            calling-convention-entry-block terminal-next-blocks)
  (#:patterns function-definition basic-block
              direct-call indirect-function-call closure-call syscall
              closure-convention plain-function-convention
              eight-byte-mem-op four-byte-mem-op two-byte-mem-op one-byte-mem-op
              int-literal bin-op-inst unary-op-inst pointer-set!-inst
              pointer-ref-inst variant-ref-inst memory-compare-inst symbol-address-inst
              function-allocate-inst variant-allocate-inst closure-allocate-inst
              return halt jump cond-jump block-call tail-call jump-target
              var-condition negated-var-condition comparison-condition memory-compare-condition
              negated-variant-tag-condition
              truncate-unsigned-int64->unsigned-int8 boolean-not
              compare-s64 compare-s8 compare-mem-arg-val compare-mem-arg-ptr
              fixed-pointer-offset scaled-pointer-offset one-byte-scale eight-byte-scale
              var-or-s32-s32 var-or-s32-var var-or-s8-var var-or-s8-s8))
(types
  (define-type FunctionDefinition
    (function-definition
      [calling-convention : CallingConvention]
      [blocks : (IndexedSet BasicBlock BlockNumber)]
      [next-var-number : VarNumber]
      [next-block-number : BlockNumber]))
  (define-type CallingConvention
    (plain-function-convention [num-args : Int] [block : BlockNumber])
    (closure-convention [num-args : Int] [block : BlockNumber] [num-free-args : Int]))
  (define-type BasicBlock
    (basic-block
      [number : BlockNumber]
      [args : (List Var)]
      [instructions : (List Instruction)]
      [terminal : Terminal]))

  (define-type Instruction
    (int-literal [output : Var] [v : Int])
    (bin-op-inst [output : Var] [op : BinOp] [left : Var] [right : Var])
    (unary-op-inst [output : Var] [op : UnaryOp] [input : Var])
    (pointer-set!-inst [size : MemoryOperationSize] [pointer : Var] [offset : PointerOffset]
                       [val : Var])
    (pointer-ref-inst [output : Var] [size : MemoryOperationSize] [pointer : Var]
                      [offset : PointerOffset])
    (variant-ref-inst [output : Var] [variant : Var] [field-index : Int])
    (memory-compare-inst [output : Var] [size : Var] [v1 : Var] [v2 : Var])
    (symbol-address-inst [output : Var] [name : Bytes])
    (function-allocate-inst [output : Var] [num-bytes : Var])
    (closure-allocate-inst [output : Var] [fields : (List Var)])
    (variant-allocate-inst [output : Var] [info : VariantInfo] [tag : Var] [fields : (List Var)]))

  (define-type PointerOffset
    (fixed-pointer-offset [offset : Int])
    (scaled-pointer-offset [amount : Var] [scale : PointerOffsetScale] [fixed-offset : Int]))
  (define-type PointerOffsetScale
    (one-byte-scale)
    (eight-byte-scale))

  (define-type FunctionKind
    (direct-call [f : Bytes])
    (indirect-function-call [f : Var])
    (closure-call [c : Var])
    (syscall [n : Int]))

  (define-type UnaryOp
    (truncate-unsigned-int64->unsigned-int8)
    (boolean-not))

  (define-type MemoryOperationSize
    (one-byte-mem-op)
    (two-byte-mem-op)
    (four-byte-mem-op)
    (eight-byte-mem-op))

  (define-type Condition
    (var-condition [name : Var])
    (negated-var-condition [name : Var])
    (negated-variant-tag-condition [name : Var] [tag : U8])
    (comparison-condition [op : ComparisonBinOp] [args : CompareArgs])
    (memory-compare-condition [negated? : Boolean] [size : Var] [v1 : Var] [v2 : Var]))
  (define-type CompareArgs
    (compare-s64 [left : CompareMemArg] [right : VarOrS32])
    (compare-s8 [left : CompareMemArg] [right : VarOrS8]))
  (define-type CompareMemArg
    (compare-mem-arg-val [v : Var])
    (compare-mem-arg-ptr [v : Var]))
  (define-type VarOrS32
    (var-or-s32-var [v : Var])
    (var-or-s32-s32 [v : S32]))
  (define-type VarOrS8
    (var-or-s8-var [v : Var])
    (var-or-s8-s8 [v : S8]))

  (define-type Terminal
    (return [name : Var])
    (halt)
    (cond-jump [cond : Condition] [true : JumpTarget] [false : JumpTarget])
    (block-call [f : FunctionKind] [args : (List Var)] [block-number : BlockNumber])
    (tail-call [f : FunctionKind] [args : (List Var)])
    (jump [target : JumpTarget]))
  (define-type JumpTarget
    (jump-target [block-number : BlockNumber] [args : (List Var)])))

;; These are the variables that have useful defined values after the instruction
(define (output-var [i : Instruction]) : (Maybe Var)
  (case i
    [(int-literal output _)
     (just output)]
    [(bin-op-inst output _ _ _)
     (just output)]
    [(unary-op-inst output _ _)
     (just output)]
    [(pointer-set!-inst _ _ _ _)
     (nothing)]
    [(pointer-ref-inst output _ _ _)
     (just output)]
    [(variant-ref-inst output _ _)
     (just output)]
    [(memory-compare-inst output _ _ _)
     (just output)]
    [(symbol-address-inst output _)
     (just output)]
    [(function-allocate-inst output _)
     (just output)]
    [(closure-allocate-inst output _)
     (just output)]
    [(variant-allocate-inst output _ _ _)
     (just output)]))

(define (calling-convention-entry-block [conv : CallingConvention]) : BlockNumber
  (case conv
    [(plain-function-convention _ block) block]
    [(closure-convention _ block _) block]))

(define (terminal-next-blocks [term : Terminal]) : (List BlockNumber)
  (case term
    [(return _) (empty)]
    [(halt) (empty)]
    [(cond-jump _cond (jump-target t _t-args) (jump-target f _f-args))
     (varargs list t f)]
    [(block-call _ _ i) (varargs list i)]
    [(tail-call _ _) (empty)]
    [(jump (jump-target i _args)) (varargs list i)]))
