#:module (prim-language-compiler-main)
#:import {
  (either)
  (io)
  (list)
  (machine-code)
  (machine-code-writer)
  (maybe)
  (prim)
  (prim-language-compiler)
  (prim-language-runtime)
  (yaspl x86-64-assembly)
}
(export)
(types
  (define-type Args
    (args
      [input-file-name : Bytes]
      [output-file-name : Bytes]
      [output-format : MachineCodeOutputFormat])))

(define (parse-args [input : (List Bytes)]) : (Either Bytes Args)
  (case input
    [(empty)
     (left #"No executable???")]
    [(cons _ (empty))
     (left #"No output format provided.")]
    [(cons _ (cons _ (empty)))
     (left #"No file name provided.")]
    [(cons _ (cons _ (cons _ (empty))))
     (left #"No output file name provided.")]
    [(cons _ (cons _ (cons _ (cons _ (cons _ _)))))
     (left #"Too many arguments.")]
    [(cons _ (cons output-format-bytes (cons file-name (cons out-file-name (empty)))))
     (case (parse-machine-code-output-format output-format-bytes)
       [(nothing) (left #"Unknown output format")]
       [(just output-format)
        (right (args file-name out-file-name output-format))])]))

(define (run [args : (Array Bytes)]) : (Maybe Bytes)
  (case (parse-args (array->list args))
    [(left v) (just v)]
    [(right (args file-name out-file-name output-format))
     (match-define abi (machine-code-output-format-abi output-format))
     (case (compile-prim-program (sourced-bytes-from-file file-name) abi)
       [(left v) (just v)]
       [(right text-fragments)
        (match-define text-fragments
          (append
            (case abi
              [(osx-abi) (osx-start-function-fragments)]
              [(linux-abi) (linux-start-function-fragments)])
            text-fragments))
        (begin
          (write-library-machine-code
            (assembly-fragments text-fragments (empty))
            output-format
            out-file-name)
          (nothing))])]))

(define (main [args : (Array Bytes)] [stdin : InputPort] [stdout : OutputPort] [stderr : OutputPort]): Int
  (case (run args)
    [(just v)
     (begin
       (write-line v stderr)
       1)]
    [(nothing)
     0]))
