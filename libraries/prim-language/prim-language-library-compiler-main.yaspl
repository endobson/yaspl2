#:module (prim-language-library-compiler-main)
#:import {
  (bytes)
  (either)
  (io)
  (list)
  (machine-code)
  (machine-code-writer)
  (maybe)
  (prim)
  (prim-language-compiler)
  (yaspl x86-64-assembly)
}
(export
  (#:values main))
(types
  (define-type Args
    (args
      [input-file-name : Bytes]
      [output-file-name : Bytes]
      [output-format : MachineCodeOutputFormat])))

(define (parse-args [input : (List Bytes)]) : (Either Bytes Args)
  (case input
    [(empty)
     (left #"No executable???")]
    [(cons _ (empty))
     (left #"No output format provided.")]
    [(cons _ (cons _ (empty)))
     (left #"No file name provided.")]
    [(cons _ (cons _ (cons _ (empty))))
     (left #"No output file name provided.")]
    [(cons _ (cons _ (cons _ (cons _ (cons _ _)))))
     (left #"Too many arguments.")]
    [(cons _ (cons output-format-bytes (cons file-name (cons out-file-name (empty)))))
     (case (parse-machine-code-output-format output-format-bytes)
       [(nothing) (left #"Unknown output format")]
       [(just output-format)
        (right (args file-name out-file-name output-format))])]))

(define (run [args : Bytes]) : (Maybe Bytes)
  (case (parse-args (args->list args))
    [(left msg) (just msg)]
    [(right (args file-name out-file-name output-format))
     (case (compile-prim-program (sourced-bytes-from-file file-name)
                                 (machine-code-output-format-abi output-format))
       [(left msg) (just msg)]
       [(right text-fragments)
        (begin
          (write-library-machine-code
            (assembly-fragments text-fragments (empty))
            output-format
            out-file-name)
          (nothing))])]))

(define (main [args : Bytes] [stdin : InputPort] [stdout : OutputPort] [stderr : OutputPort]): Int
  (case (run args)
    [(just v)
     (begin
       (write-line v stderr)
       1)]
    [(nothing)
     0]))
