#:module (prim-language-library-compiler-main)
#:import {
  (assembler)
  (either)
  (io)
  (list)
  (mach-o-writer)
  (maybe)
  (prim)
  (prim-language-compiler)
}
(export)
(types
  (define-type Args
    (args
      [input-file-name : Bytes]
      [output-file-name : Bytes])))

(define (parse-args [input : (List Bytes)]) : (Either Bytes Args)
  (case input
    [(empty)
     (left #"No executable???")]
    [(cons _ (empty))
     (left #"No file name provided.")]
    [(cons _ (cons _ (empty)))
     (left #"No output file name provided.")]
    [(cons _ (cons _ (cons _ (cons _ _))))
     (left #"Too many arguments.")]
    [(cons _ (cons file-name (cons out-file-name (empty))))
     (right (args file-name out-file-name))]))

(define (run [args : (Array Bytes)]) : (Maybe Bytes)
  (case (parse-args (array->list args))
    [(left msg) (just msg)]
    [(right (args file-name out-file-name))
     (case (compile-prim-program (sourced-bytes-from-file file-name))
       [(left msg) (just msg)]
       [(right text-fragments)
        (match-define text-section (assemble-text-section text-fragments))
        (match-define const-section (assemble-const-section (empty)))
        (begin
          (write-mach-o-library text-section const-section out-file-name)
          (nothing))])]))

(define (main [args : (Array Bytes)] [stdin : InputPort] [stdout : OutputPort] [stderr : OutputPort]): Int
  (case (run args)
    [(just v)
     (begin
       (write-line v stderr)
       1)]
    [(nothing)
     0]))
