#:module (print-register-language)
#:import {
  (block-number)
  (bytes)
  (data indexed-set)
  (join-list)
  (list)
  (numbers)
  (prim)
  {(prim-language)
    {#:types
      BinOp
    }
    {#:patterns
      comparison-bin-op
      logical-bin-op
      numeric-bin-op
    }
  }
  (register-language)
  (yaspl math-operations)
  (yaspl var)
}
(export
  (#:types)
  (#:values print-function print-basic-block)
  (#:patterns))
(types)

(define (print-function [f : FunctionDefinition]) : Bytes
  (case f
    [(function-definition name _ blocks _ _)
     (bytes-append
       (jl->list
         (append-jl
           (varargs join-list
              name
              #"\n")
           (concat-jl
             (map print-basic-block (indexed-set->list blocks))))))]))

(define (print-basic-block [b : BasicBlock]) : (JoinList Bytes)
  (case b
    [(basic-block block-num args instructions terminal)
     (varargs append-jl*
       (varargs join-list
         #"block: "
         (block-number->bytes block-num)
         #"(")
       (concat-jl (interleave (map print-var args) (single-jl #", ")))
       (single-jl #")\n")
       (concat-jl (map print-instruction instructions))
       (print-terminal terminal))]))

(define (print-instruction [i : Instruction]) : (JoinList Bytes)
  (case i
    [(bin-op-inst var op left right)
     (varargs append-jl*
       (print-var var)
       (varargs join-list #" = (" (print-bin-op op) #" ")
       (print-var left)
       (single-jl #" ")
       (print-var right)
       (single-jl #")\n"))]
    [(unary-op-inst var op val)
     (varargs append-jl*
       (print-var var)
       (varargs join-list #" = (" (print-unary-op op) #" ")
       (print-var val)
       (single-jl #")\n"))]
    [(int-literal var v)
     (append-jl
       (print-var var)
       (varargs join-list #" = " (integer->decimal-bytes v) #"\n"))]
    [(function-allocate-inst var v)
     (varargs append-jl*
       (print-var var)
       (single-jl #" = function-allocate(")
       (print-var v)
       (single-jl #")\n"))]
    [(heap-allocate-inst/static var v)
     (varargs append-jl*
       (print-var var)
       (varargs join-list #" = heap-allocate("  (integer->decimal-bytes (s32->s64 v)) #")\n"))]
    [(variant-allocate-inst var tag args)
     (varargs append-jl*
       (print-var var)
       (single-jl #" = variant[")
       (single-jl (integer->decimal-bytes (u8->s64 tag)))
       (single-jl #"](")
       (concat-jl (interleave (map print-var args) (single-jl #", ")))
       (single-jl #")\n"))]
    [(memory-compare-inst var size v1 v2)
     (varargs append-jl*
       (print-var var)
       (single-jl #" = compare-memory(")
       (print-var size)
       (single-jl #", ")
       (print-var v1)
       (single-jl #", ")
       (print-var v2)
       (single-jl #")\n"))]
    [(symbol-address-inst var sym)
     (varargs append-jl*
       (print-var var)
       (single-jl #" = symbol(")
       (single-jl sym)
       (single-jl #")\n"))]
    [(function-call-inst var f args)
     (varargs append-jl*
       (case f
         [(direct-call f)
          (varargs append-jl*
            (print-var var)
            (single-jl #" = call[")
            (single-jl f)
            (single-jl #"]"))]
         [(indirect-function-call f)
          (varargs append-jl*
            (print-var var)
            (single-jl #" = call[")
            (print-var f)
            (single-jl #"]"))]
         [(closure-call c)
          (varargs append-jl*
            (print-var var)
            (single-jl #" = closure-call[")
            (print-var c)
            (single-jl #"]"))]
         [(syscall s)
          (varargs join-list #" = syscall[" (integer->hex-bytes s) #"]")])
       (single-jl #"(")
       (concat-jl (interleave (map print-var args) (single-jl #", ")))
       (single-jl #")\n"))]
    [(pointer-set!-inst mem ptr (fixed-pointer-offset offset) val)
     (varargs append-jl*
       (single-jl #"*(")
       (print-var ptr)
       (single-jl #" + ")
       (single-jl (integer->decimal-bytes offset))
       (single-jl #") := ")
       (print-var val)
       (case mem
         [(eight-byte-mem-op) (single-jl #"\n")]
         [(one-byte-mem-op) (single-jl #" (byte)\n")]))]
    [(pointer-ref-inst var mem ptr (fixed-pointer-offset offset))
     (varargs append-jl*
       (print-var var)
       (single-jl #" = *(")
       (print-var ptr)
       (single-jl #" + ")
       (single-jl (integer->decimal-bytes offset))
       (case mem
         [(eight-byte-mem-op) (single-jl #") \n")]
         [(one-byte-mem-op) (single-jl #") (byte)\n")]))]
    [(pointer-set!-inst mem ptr (scaled-pointer-offset index scale offset) val)
     (varargs append-jl*
       (single-jl #"*(")
       (print-var ptr)
       (single-jl #" + (")
       (print-var index)
       (single-jl #" * ")
       (single-jl (case scale
                    [(one-byte-scale) #"1"]
                    [(eight-byte-scale) #"8"]))
       (single-jl #") + ")
       (single-jl (integer->decimal-bytes offset))
       (single-jl #") := ")
       (print-var val)
       (case mem
         [(eight-byte-mem-op) (single-jl #"\n")]
         [(one-byte-mem-op) (single-jl #" (byte)\n")]))]
    [(pointer-ref-inst var mem ptr (scaled-pointer-offset index scale offset))
     (varargs append-jl*
       (print-var var)
       (single-jl #" = *(")
       (print-var ptr)
       (single-jl #" + (")
       (print-var index)
       (single-jl #" * ")
       (single-jl (case scale
                    [(one-byte-scale) #"1"]
                    [(eight-byte-scale) #"8"]))
       (single-jl #") + ")
       (single-jl (integer->decimal-bytes offset))
       (case mem
         [(eight-byte-mem-op) (single-jl #") \n")]
         [(one-byte-mem-op) (single-jl #"  (byte)\n")]))]
    [(variant-ref-inst output variant index)
     (varargs append-jl*
       (print-var output)
       (single-jl #" = ")
       (print-var variant)
       (single-jl #"[")
       (single-jl (integer->decimal-bytes index))
       (single-jl #"]\n"))]
    ))


(define (print-unary-op [op : UnaryOp]) : Bytes
  (case op
    [(truncate-unsigned-int64->unsigned-int8) #"truncate"]
    [(boolean-not) #"not"]))

(define (print-bin-op [op : BinOp]) : Bytes
  (case op
    [(logical-bin-op op)
     (logical-bin-op->bytes op)]
    [(numeric-bin-op op)
     (numeric-bin-op->bytes op)]
    [(comparison-bin-op op)
     (comparison-bin-op->bytes op)]))

(define (print-var [v : Var]) : (JoinList Bytes)
  (single-jl (var->bytes v)))

(define (print-jump-target [jt : JumpTarget]) : (JoinList Bytes)
  (match-define (jump-target block-num args) jt)
  (varargs append-jl*
    (single-jl (block-number->bytes block-num))
    (single-jl #"(")
    (concat-jl (interleave (map print-var args) (single-jl #", ")))
    (single-jl #")")))

(define (print-terminal [t : Terminal]) : (JoinList Bytes)
  (case t
    [(return var)
     (snoc-jl (cons-jl #"return " (print-var var)) #"\n")]
    [(halt)
     (single-jl #"halt\n")]
    [(jump jt)
     (varargs append-jl*
       (single-jl #"jump ")
       (print-jump-target jt)
       (single-jl #"\n"))]
    [(tail-call f args)
     (varargs append-jl*
       (case f
         [(direct-call f)
          (varargs append-jl*
            (single-jl #"call[")
            (single-jl f)
            (single-jl #"]"))]
         [(indirect-function-call f)
          (varargs append-jl*
            (single-jl #"call[")
            (print-var f)
            (single-jl #"]"))]
         [(closure-call c)
          (varargs append-jl*
            (single-jl #"closure-call[")
            (print-var c)
            (single-jl #"]"))]
         [(syscall s)
          (varargs join-list #"syscall[" (integer->hex-bytes s) #"]")])
       (single-jl #"(")
       (concat-jl (interleave (map print-var args) (single-jl #", ")))
       (single-jl #")\n"))]
    [(cond-jump (var-condition var) t f)
     (varargs append-jl*
       (single-jl #"cond-jump ")
       (print-var var)
       (single-jl #" ")
       (print-jump-target t)
       (single-jl #" ")
       (print-jump-target f)
       (single-jl #"\n"))]
    [(cond-jump (negated-var-condition var) t f)
     (varargs append-jl*
       (single-jl #"cond-jump (not ")
       (print-var var)
       (single-jl #") ")
       (print-jump-target t)
       (single-jl #" ")
       (print-jump-target f)
       (single-jl #"\n"))]
    [(cond-jump (negated-variant-tag-condition var tag) t f)
     (varargs append-jl*
       (single-jl #"cond-jump (tag!=? ")
       (print-var var)
       (single-jl #" ")
       (single-jl (integer->decimal-bytes (u8->s64 tag)))
       (single-jl #") ")
       (print-jump-target t)
       (single-jl #" ")
       (print-jump-target f)
       (single-jl #"\n"))]
    [(cond-jump (comparison-condition op (compare-s64 left right-imm)) t f)
     (varargs append-jl*
       (single-jl #"cond-jump (")
       (single-jl (comparison-bin-op->bytes op))
       (single-jl #" ")
       (case left
         [(compare-mem-arg-val v) (print-var v)]
         [(compare-mem-arg-ptr v) (cons-jl #"*" (print-var v))])
       (single-jl #" ")
       (case right-imm
         [(var-or-s32-var v) (print-var v)]
         [(var-or-s32-s32 imm) (single-jl (integer->decimal-bytes (s32->s64 imm)))])
       (single-jl #") ")
       (print-jump-target t)
       (single-jl #" ")
       (print-jump-target f)
       (single-jl #"\n"))]
    [(cond-jump (comparison-condition op (compare-s8 left right-imm)) t f)
     (varargs append-jl*
       (single-jl #"cond-jump (")
       (single-jl (comparison-bin-op->bytes op))
       (single-jl #" ")
       (case left
         [(compare-mem-arg-val v) (print-var v)]
         [(compare-mem-arg-ptr v) (cons-jl #"*" (print-var v))])
       (single-jl #" ")
       (case right-imm
         [(var-or-s8-var v) (print-var v)]
         [(var-or-s8-s8 imm) (single-jl (integer->decimal-bytes (s8->s64 imm)))])
       (single-jl #") ")
       (print-jump-target t)
       (single-jl #" ")
       (print-jump-target f)
       (single-jl #") (byte)\n"))]
    [(cond-jump (memory-compare-condition negated size v1 v2) t f)
     (varargs append-jl*
       (single-jl #"cond-jump (compare-memory-")
       (single-jl (if negated #"!= " #"= "))
       (print-var size)
       (single-jl #" ")
       (print-var v1)
       (single-jl #" ")
       (print-var v2)
       (single-jl #") ")
       (print-jump-target t)
       (single-jl #" ")
       (print-jump-target f)
       (single-jl #"\n"))]

))
