#:module (prim-language-compiler)
#:import {
  (bytes)
  (data source-location)
  (dict)
  (either)
  (list)
  (machine-code)
  (maybe)
  (prim)
  {(prim-language)
    {#:types
      [FunctionDefinition p:FunctionDefinition]
    }
    {#:patterns
      [module p:module]
    }
  }
  (prim-language-parser)
  (prim-language-type-checker)
  (prim-to-register)
  (register-language-compiler)
  (sexp-parser)
  (simplify-register-language)
  (yaspl x86-64-assembly)
}
(export
  (#:types)
  (#:values compile-prim-program)
  (#:patterns))
(types)

(define (compile-functions [fs : (List p:FunctionDefinition)] [abi : SystemAbi] [acc : (List TextFragment)])
  : (Either Bytes (List TextFragment))
  (case fs
    [(empty) (right acc)]
    [(cons f fs)
     (case (compile-function (simplify-function-definition (convert-function f abi)
                                                           (static-info (make-dict bytes-cmp))))
       [(left v) (left v)]
       [(right fragment) (compile-functions fs abi (cons fragment acc))])]))

(define (compile-prim-program [sbytes : SourcedBytes] [abi : SystemAbi]) : (Either Bytes (List TextFragment))
  (case (parse-single-sexp sbytes)
    [(left (sexp-error v _)) (left v)]
    [(right sexp)
     (case (parse-module sexp)
       [(left v) (left v)]
       [(right module)
        (case (type-check module)
          [(just v) (left v)]
          [(nothing)
           (case module
             [(p:module _ _ functions)
              (compile-functions functions abi (empty))])])])]))
