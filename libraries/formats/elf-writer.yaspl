#:module (formats elf-writer)
#:import {
  (aligned-bytes)
  (bytes)
  (data indexed-set)
  (data lifted-primitives)
  (dict)
  (io)
  (join-list)
  (list)
  (maybe)
  (numbers)
  (prim)
  (set)
  (structured-elf)
  (structured-native-code)
  (tuples)
}
(export
  (#:values
     serialize-elf-library
     write-elf-executable))
(types
  (define-type SymbolReference
    (symbol-reference
      [symbol : Bytes]
      [address : Int])))

(define (serialize-elf-library
          [text-section : (ProgramSection AbstractRelocationKind)]
          [const-section : (ProgramSection AbstractRelocationKind)])
  : Bytes
  (match-define (program-section (aligned-bytes text-section-contents text-section-alignment)
                                 text-relocations text-global-symbols text-local-symbols)
    text-section)
  (match-define text-symbols (append text-global-symbols text-local-symbols))
  (match-define text-section-length (bytes-length text-section-contents))

  (match-define (program-section (aligned-bytes const-section-contents const-section-alignment)
                                 const-relocations const-global-symbols const-local-symbols)
    const-section)
  (match-define const-symbols (append const-global-symbols const-local-symbols))
  (match-define const-section-length (bytes-length const-section-contents))

  (match-define defined-symbols
    (map defined-symbol-name
         (append* (varargs list
                    text-local-symbols text-global-symbols
                    const-local-symbols const-global-symbols))))

  (match-define undefined-symbols
    (let ([defined-symbols-set (set-add-all (make-set bytes-cmp) defined-symbols)])
      (remove-duplicates
        (filter-map
          (lambda ([r : (Relocation AbstractRelocationKind)]) : (Maybe Bytes)
            (let ([sym (relocation-symbol r)])
              (if (set-member? defined-symbols-set sym)
                  (nothing)
                  (just sym))))
          (append text-relocations const-relocations)))))

  (match-define ordered-symbols
    (append defined-symbols undefined-symbols))

  (match-define indexed-text-symbols
    (indexed-set-add-all
      (make-indexed-set defined-symbol-name bytes-cmp)
      text-symbols))
  (match-define indexed-const-symbols
    (indexed-set-add-all
      (make-indexed-set defined-symbol-name bytes-cmp)
      const-symbols))
  (match-define text-global-symbol-names-set
    (set-add-all
      (make-set bytes-cmp)
      (map defined-symbol-name text-global-symbols)))
  (match-define const-global-symbol-names-set
    (set-add-all
      (make-set bytes-cmp)
      (map defined-symbol-name const-global-symbols)))

  (match-define ordered-text-relocations
    (order-relocations text-relocations))

  (match-define (tuple2 offset-symbols (lifted-int string-table-size))
    (compute-string-table-offsets ordered-symbols))

  (match-define named-sections : (List LiftedInt)
    (varargs list (lifted-int 1) (lifted-int 3) (lifted-int 4) (lifted-int 5)))

  (match-define symbol-indices : (Dict Bytes LiftedInt)
    (dict-add-all
      (make-dict bytes-cmp)
      (map/indexed
        (lambda ([s : Bytes] [index : Int])
          (tuple2 s (lifted-int (+ 1 (+ (length named-sections) index)))))
        ordered-symbols)))

  (match-define serialize-relocation
    (lambda ([relocation : (Relocation AbstractRelocationKind)]) : (JoinList Bytes)
      (case relocation
        [(relocation kind offset sym)
         (match-define kind (abstract-relocation->elf-relocation kind))
         (varargs join-list
           (bytes/s64-le offset)
           (bytes/u32-le
             (case kind
               [(elf-pc32-relocation) (u32 #x02)]
               [(elf-64-relocation _) (u32 #x01)]))
           (bytes/u32-le
             (case (dict-ref/panic symbol-indices sym)
               [(lifted-int v) (u32 v)]))
           (bytes/s64-le
             (case kind
               ;; pc relative relocations need to account for the 4 bytes offset
               [(elf-pc32-relocation) (- 0 4)]
               [(elf-64-relocation v) v])))])))


  (match-define text-section-start #x40)
  (match-define text-section-end (+ text-section-start text-section-length))
  (match-define const-section-start
    (round-up text-section-end (logical-shift-left 1 const-section-alignment)))
  (match-define const-section-end (+ const-section-start const-section-length))

  (match-define symbol-table-size (* (+ 1 (+ (length named-sections) (dict-count symbol-indices))) #x18))

  (match-define section-string-table-strings
    (map make-null-terminated
      (varargs list
        #""
        #".symtab"
        #".strtab"
        #".shstrtab"
        #".rela.text"
        #".data"
        #".bss"
        #".rela.rodata")))

  (match-define section-string-table-size
    (sum bytes-length section-string-table-strings))

  (match-define symbol-table-start (round-up const-section-end 8))
  (match-define string-table-start (round-up (+ symbol-table-start symbol-table-size) 8))

  (match-define string-table-end (+ string-table-start string-table-size))
  (match-define rela-text-section-start (round-up string-table-end 8))
  (match-define rela-text-section-size
    (* #x18 (length text-relocations)))
  (match-define rela-text-section-end
    (+ rela-text-section-start rela-text-section-size))

  (match-define rela-const-section-start (round-up rela-text-section-end 8))
  (match-define rela-const-section-size
    (* #x18 (length const-relocations)))
  (match-define rela-const-section-end
    (+ rela-const-section-start rela-const-section-size))

  (match-define section-string-table-start
    (round-up rela-const-section-end 1))

  (match-define section-string-table-end
    (+ section-string-table-start section-string-table-size))
  (match-define section-header-start
    (round-up section-string-table-end 8))

  (match-define section-headers : (List (JoinList Bytes))
    (varargs list
      (padding (* 16 4)) ;; Empty section header
      ;; .text section
      (section-header/bytes 32 1 6 0 text-section-start text-section-length 0 0
                      (logical-shift-left 1 text-section-alignment) 0)
      ;; .rela.text section
      (section-header/bytes 27 4 64 0 rela-text-section-start rela-text-section-size
                      7 1 8 #x18)
      ;; .data section
      (section-header/bytes 38 1 3 0 text-section-end 0 0 0 1 0)
      ;; .bss section
      (section-header/bytes 44 8 3 0 text-section-end 0 0 0 1 0)
      ;; .rodata section
      (section-header/bytes 54 1 2 0 const-section-start const-section-length 0 0
                            (logical-shift-left 1 const-section-alignment) 0)
      ;; .rela.rodata section
      (section-header/bytes 49 4 64 0 rela-const-section-start rela-const-section-size
                            7 5 8 #x18)
      ;; .symtab section
      (section-header/bytes 1 2 0 0 symbol-table-start symbol-table-size
                            8 (+ 5 (+ (length text-local-symbols) (length const-local-symbols))) 8 24)
      ;; .strtab section
      (section-header/bytes 9 3 0 0 string-table-start string-table-size 0 0 1 0)
      ;; .shstrtab section
      (section-header/bytes 17 3 0 0 section-string-table-start section-string-table-size 0 0 1 0)))

  (bytes-append
    (jl->list
      (varargs append-jl*
        (varargs join-list
          #"\x7FELF" ;; Magic
          #"\x02"    ;; Class (64 bit)
          #"\x01"    ;; Data encoding (LSB)
          #"\x01"    ;; Version
          #"\x00"    ;; OS ABI (SysV)
          #"\x00")   ;; OS ABI version
        (padding 7) ;; Padding
        (varargs join-list
          (bytes/u16-le (u16 1))                               ;; Relocatable object file
          (bytes/u16-le (u16 #x3E))                            ;; Machine type X86_64
          (bytes/u32-le (u32 1))                               ;; Version
          (bytes/u64-le (u64 0))                               ;; No start address
          (bytes/u64-le (u64 0))                               ;; Program header offset
          (bytes/u64-le (u64 section-header-start))            ;; Section header offset
          (bytes/u32-le (u32 0))                               ;; FLAGS
          (bytes/u16-le (u16 #x40))                            ;; ELF header size
          (bytes/u16-le (u16 #x00))                            ;; Size of program header entry
          (bytes/u16-le (u16 #x00))                            ;; Number of program header entries
          (bytes/u16-le (u16 #x40))                            ;; size of section header entry
          (bytes/u16-le (u16 (length section-headers)))        ;; Number of section header entries
          (bytes/u16-le (u16 (- (length section-headers) 1)))) ;; Index of section name section
        (single-jl text-section-contents)
        (padding (- const-section-start text-section-end))
        (single-jl const-section-contents)
        (padding (- symbol-table-start const-section-end))
        (symbol-table-entry 0 0 0 0)
        (concat-map-jl/list
          (lambda ([named-section-index : LiftedInt])
            (case named-section-index
              [(lifted-int v)
               (symbol-table-entry 0 3 v 0)]))
          named-sections)
        (concat-map-jl/list
          (lambda ([entry : (Tuple2 Bytes LiftedInt)])
            (case entry
              [(tuple2 sym (lifted-int name-offset))
               (case (indexed-set-ref indexed-text-symbols sym)
                 [(just (defined-symbol _ addr))
                  (symbol-table-entry name-offset
                                      (if (set-member? text-global-symbol-names-set sym) #x10 #x00)
                                      1
                                      addr)]
                 [(nothing)
                  (case (indexed-set-ref indexed-const-symbols sym)
                    [(just (defined-symbol _ addr))
                     (symbol-table-entry name-offset
                                         (if (set-member? const-global-symbol-names-set sym) #x10 #x00)
                                         5
                                         addr)]
                    [(nothing)
                     (symbol-table-entry name-offset #x10 0 0)])])]))
          offset-symbols)
        (single-jl (make-null-terminated #""))
        (concat-map-jl/list
          (lambda ([s : Bytes])
            (single-jl (make-null-terminated s)))
          ordered-symbols)
        (padding (- rela-text-section-start string-table-end))
        (concat-map-jl/list serialize-relocation ordered-text-relocations)
        (concat-map-jl/list serialize-relocation const-relocations)
        (concat-map-jl/list (lambda ([v : Bytes]) (single-jl v)) section-string-table-strings)
        (padding (- section-header-start section-string-table-end))
        (concat-jl section-headers)))))

(define (padding [size : Int]) : (JoinList Bytes)
  (single-jl (make-bytes size)))

(define (remove-duplicates [l : (List Bytes)]) : (List Bytes)
  (remove-duplicates* l (empty) (make-set bytes-cmp)))

(define (remove-duplicates* [l : (List Bytes)] [rev-acc : (List Bytes)] [seen : (Set Bytes)])
  : (List Bytes)
  (case l
    [(empty) (reverse rev-acc)]
    [(cons e l)
     (if (set-member? seen e)
         (remove-duplicates* l rev-acc seen)
         (remove-duplicates* l (cons e rev-acc) (set-add seen e)))]))

(define (bytes/s64-le [v : Int]) : Bytes
  (match-define b (make-bytes 8))
  (begin
    (if (>= v 0)
        (bytes-set!/u64-le b 0 (u64 v))
        (begin
          (bytes-set!/s32-le b 0 (s32 v))
          (bytes-set!/s32-le b 4 (s32 (- 0 1)))))
    b))

(define (section-header/bytes
          [name-offset : Int]
          [section-type : Int]
          [section-flags : Int]
          [section-addr : Int]
          [section-offset : Int]
          [section-size : Int]
          [section-link : Int]
          [section-info : Int]
          [section-align : Int]
          [entry-size : Int]) : (JoinList Bytes)
  (varargs join-list
    (bytes/u32-le (u32 name-offset))
    (bytes/u32-le (u32 section-type))
    (bytes/u64-le (u64 section-flags))
    (bytes/u64-le (u64 section-addr))
    (bytes/u64-le (u64 section-offset))
    (bytes/u64-le (u64 section-size))
    (bytes/u32-le (u32 section-link))
    (bytes/u32-le (u32 section-info))
    (bytes/u64-le (u64 section-align))
    (bytes/u64-le (u64 entry-size))))

(define (symbol-table-entry
          [symbol-name-offset : Int]
          [binding-info : Int]
          [section-table-index : Int]
          [symbol-value : Int]) : (JoinList Bytes)
  (varargs join-list
    (bytes/u32-le (u32 symbol-name-offset))
    (bytes/2 (u8 binding-info) (u8 0))
    (bytes/u16-le (u16 section-table-index))
    (bytes/u64-le (u64 symbol-value))
    (make-bytes 8)))

(define (order-relocations [relocations : (List (Relocation AbstractRelocationKind))])
  : (List (Relocation AbstractRelocationKind))
  (order-relocations* relocations (empty) (empty)))

(define (order-relocations*
          [relocations : (List (Relocation AbstractRelocationKind))]
          [rev-non-branch-relocations : (List (Relocation AbstractRelocationKind))]
          [rev-branch-relocations : (List (Relocation AbstractRelocationKind))])
  : (List (Relocation AbstractRelocationKind))
  (case relocations
    [(empty)
     (append (reverse rev-non-branch-relocations) (reverse rev-branch-relocations))]
    [(cons rel relocations)
     (case rel
       [(relocation (branch-relocation) _ _)
        (order-relocations* relocations rev-non-branch-relocations
                            (cons rel rev-branch-relocations))]
       [_
        (order-relocations* relocations (cons rel rev-non-branch-relocations)
                            rev-branch-relocations)])]))

(define (compute-string-table-offsets [strings : (List Bytes)])
  : (Tuple2 (List (Tuple2 Bytes LiftedInt)) LiftedInt)
  (match-define (tuple2 rev-offset-strings string-table-size)
    (foldl
      (lambda ([s : Bytes] [acc : (Tuple2 (List (Tuple2 Bytes LiftedInt)) LiftedInt)])
        (case acc
          [(tuple2 offset-strings (lifted-int current-offset))
           (tuple2
             (cons (tuple2 s (lifted-int current-offset)) offset-strings)
             (lifted-int (+ current-offset (+ 1 (bytes-length s)))))]))
      strings
      (tuple2
        (ann (List (Tuple2 Bytes LiftedInt)) (empty))
        (lifted-int 1))))
  (tuple2 (reverse rev-offset-strings) string-table-size))


(define (write-elf-executable
          [text-section : (ProgramSection ElfRelocationKind)]
          [const-section : (ProgramSection ElfRelocationKind)]
          [ordered-symbols : (List Bytes)]
          [output-file-name : Bytes]) : (Maybe Bytes)
  (call-with-output-file output-file-name
    (lambda ([p : OutputPort])
      (begin
        (write-all-bytes
          (serialize-elf-executable text-section const-section ordered-symbols) p)
        (nothing)))))

(define (serialize-elf-executable
          [text-section : (ProgramSection ElfRelocationKind)]
          [const-section : (ProgramSection ElfRelocationKind)]
          [ordered-symbols : (List Bytes)]) : Bytes

  (match-define
    (program-section (aligned-bytes text-section-contents text-section-alignment)
                     text-relocations text-global-symbols text-local-symbols)
    text-section)
  (match-define text-section-length (bytes-length text-section-contents))

  (match-define
    (program-section (aligned-bytes const-section-contents const-section-alignment)
                     const-relocations const-global-symbols const-local-symbols)
    const-section)
  (match-define const-section-length (bytes-length const-section-contents))

  ;; Move the local symbols to earlier in the symbol list.
  (match-define local-symbols (append const-local-symbols text-local-symbols))
  (match-define ordered-symbols
    (remove-duplicates
      (append
        (map defined-symbol-name local-symbols)
        ordered-symbols)))

  (match-define (tuple2 offset-symbols (lifted-int string-table-size))
    (compute-string-table-offsets ordered-symbols))

  (match-define offset-symbols-dict : (Dict Bytes LiftedInt)
    (dict-add-all (make-dict bytes-cmp) offset-symbols))

  (match-define const-symbols-set
    (set-add-all
       (make-set bytes-cmp)
       (map defined-symbol-name
            (append const-local-symbols const-global-symbols))))

  (match-define local-symbols-set
    (set-add-all
       (make-set bytes-cmp)
       (map defined-symbol-name local-symbols)))


  (match-define address-offset #x200000)
  (match-define const-section-start (round-up #x120 (logical-shift-left 1 const-section-alignment)))
  (match-define const-section-end (+ const-section-start const-section-length))
  (match-define text-section-start (round-up const-section-end #x1000))
  (match-define text-section-end (+ text-section-start text-section-length))
  (match-define linker-comment-start (round-up text-section-end #x1000))
  (match-define linker-comment-size 18)
  (match-define linker-comment-end (+ linker-comment-start linker-comment-size))

  (match-define symbol-addresses : (Dict Bytes LiftedInt)
    (dict-add-all
      (make-dict bytes-cmp)
      (append
        (map
          (lambda ([s : DefinedSymbol])
            (case s
              [(defined-symbol name addr)
               (tuple2 name (lifted-int (+ (+ address-offset text-section-start) addr)))]))
          (append text-local-symbols text-global-symbols))
        (map
          (lambda ([s : DefinedSymbol])
            (case s
              [(defined-symbol name addr)
               (tuple2 name (lifted-int (+ (+ address-offset const-section-start) addr)))]))
          (append const-local-symbols const-global-symbols)))))

  (match-define symbol-table-start (round-up linker-comment-end 8))
  (match-define num-symbol-table-entries (+ 1 (length offset-symbols)))
  (match-define symbol-table-size (* #x18 num-symbol-table-entries))
  (match-define symbol-table-end (+ symbol-table-start symbol-table-size))


  (match-define section-string-table-start symbol-table-end)
  (match-define section-string-table-size #x3d)
  (match-define section-string-table-end
    (+ section-string-table-start section-string-table-size))

  (match-define string-table-start section-string-table-end)
  (match-define string-table-end (+ string-table-start string-table-size))

  (match-define section-header-start (round-up string-table-end 8))

  (match-define text-section-contents
    (apply-elf-relocations
      text-relocations
      symbol-addresses
      (+ address-offset text-section-start)
      text-section-contents))

  (match-define const-section-contents
    (apply-elf-relocations
      const-relocations
      symbol-addresses
      (+ address-offset const-section-start)
      const-section-contents))

  (match-define start-address : Int
    (case (dict-ref/panic symbol-addresses #"_start")
      [(lifted-int v) v]))

  (bytes-append
    (jl->list
      (varargs append-jl*
        (executable-program-header
          start-address
          address-offset
          const-section-start
          const-section-length
          text-section-start
          text-section-length
          section-header-start)

        (padding (- const-section-start #x120))
        (single-jl const-section-contents)
        (padding (- text-section-start const-section-end))
        (single-jl text-section-contents)
        (single-jl (make-bytes/fill (- linker-comment-start text-section-end) (u8 #xCC)))

        ;; TODO remove this once we don't need byte for byte compatibility
        (single-jl #"Linker: LLD 6.0.0\x00")

        ;; Symbol tables are 8 byte aligned, so add padding as needed
        (padding (- symbol-table-start linker-comment-end))
        (symbol-table-entry 0 0 0 0)
        (concat-map-jl/list
          (lambda ([sym : Bytes])
            (symbol-table-entry
              (case (dict-ref/panic offset-symbols-dict sym)
                [(lifted-int v) v])
              (if (set-member? local-symbols-set sym) #x00 #x10)
              (if (set-member? const-symbols-set sym) 1 2)
              (case (dict-ref/panic symbol-addresses sym)
                [(lifted-int v) v])))
          ordered-symbols)


        (concat-map-jl/list
          (lambda ([s : Bytes])
            (single-jl (make-null-terminated s)))
          (varargs list
            #""
            #".rodata"
            #".text"
            #".data"
            #".bss"
            #".comment"
            #".symtab"
            #".shstrtab"
            #".strtab"))

        (single-jl (make-null-terminated #""))
        (concat-map-jl/list
          (lambda ([t : (Tuple2 Bytes LiftedInt)])
            (case t
              [(tuple2 s _)
               (single-jl (make-null-terminated s))]))
          offset-symbols)

        (padding (- section-header-start string-table-end))
        (padding (* 16 4)) ;; Empty section header
        (section-header/bytes 1 1 2 (+ address-offset const-section-start)
                              const-section-start const-section-length
                              0 0 (logical-shift-left 1 const-section-alignment) 0)
        (section-header/bytes 9 1 6 (+ address-offset text-section-start)
                              text-section-start text-section-length
                              0 0 (logical-shift-left 1 text-section-alignment) 0)
        (section-header/bytes 15 1 3 (+ address-offset linker-comment-start) linker-comment-start 0 0 0 1 0)
        (section-header/bytes 21 8 3 (+ address-offset linker-comment-start) linker-comment-start 0 0 0 1 0)
        (section-header/bytes 26 1 #x30 0 linker-comment-start #x12 0 0 1 1)
        (section-header/bytes 35 2 0 0 symbol-table-start symbol-table-size 8
                              (+ (length text-local-symbols) 1) #x08 #x18)
        (section-header/bytes 43 3 0 0 section-string-table-start section-string-table-size 0 0 1 0)
        (section-header/bytes 53 3 0 0 string-table-start string-table-size 0 0 1 0)))))

(define (executable-program-header
          [start-address : Int]
          [address-offset : Int]
          [const-section-start : Int]
          [const-section-length : Int]
          [text-section-start : Int]
          [text-section-length : Int]
          [section-header-start : Int])
  : (JoinList Bytes)
  (varargs append-jl*
    (varargs join-list
      #"\x7FELF" ;; Magic
      #"\x02"    ;; Class (64 bit)
      #"\x01"    ;; Data encoding (LSB)
      #"\x01"    ;; Version
      #"\x00"    ;; OS ABI (SysV)
      #"\x00")   ;; OS ABI version
    (padding 7) ;; Padding

    (varargs join-list
      (bytes/u16-le (u16 2)) ;; Executable file
      (bytes/u16-le (u16 #x3E)) ;; Machine type X86_64
      (bytes/u32-le (u32 1)) ;; Version
      (bytes/u64-le (u64 start-address)) ;; Start address
      (bytes/u64-le (u64 #x40)) ;; Program header offset
      (bytes/u64-le (u64 section-header-start)) ;; Section header offset
      (bytes/u32-le (u32 0)) ;; FLAGS
      (bytes/u16-le (u16 #x40)) ;; ELF header size
      (bytes/u16-le (u16 #x38)) ;; Size of program header entry
      (bytes/u16-le (u16 #x04)) ;; Number of program header entries

      (bytes/u16-le (u16 #x40)) ;; size of section header entry
      (bytes/u16-le (u16 #x09)) ;; Number of section header entries
      (bytes/u16-le (u16 #x07)) ;; Index of section name section

      (bytes/u32-le (u32 #x06))  ;; Segment type (Program Header)
      (bytes/u32-le (u32 #x04))  ;; Segment flags (Read)
      (bytes/u64-le (u64 #x40))  ;; Segment offset
      (bytes/u64-le (u64 (+ address-offset #x40)))  ;; Segment virtual address
      (bytes/u64-le (u64 (+ address-offset #x40)))  ;; Segment physical address
      (bytes/u64-le (u64 #x118))  ;; Segment file size
      (bytes/u64-le (u64 #x118))  ;; Segment memory size
      (bytes/u64-le (u64 #x08))  ;; Segment alignment

      ;; The const segment includes all the elf and program headers.
      (bytes/u32-le (u32 #x01))  ;; Segment type (Loadable)
      (bytes/u32-le (u32 #x04))  ;; Segment flags (Read)
      (bytes/u64-le (u64 #x00))  ;; Segment offset
      (bytes/u64-le (u64 (+ address-offset #x00)))  ;; Segment virtual address
      (bytes/u64-le (u64 (+ address-offset #x00)))  ;; Segment physical address
      (bytes/u64-le (u64 (+ const-section-start const-section-length)))  ;; Segment file size
      (bytes/u64-le (u64 (+ const-section-start const-section-length)))  ;; Segment memory size
      (bytes/u64-le (u64 #x1000))  ;; Segment alignment

      (bytes/u32-le (u32 #x01))  ;; Segment type (Loadable)
      (bytes/u32-le (u32 #x05))  ;; Segment flags (Read/Execute)
      (bytes/u64-le (u64 text-section-start))  ;; Segment offset
      (bytes/u64-le (u64 (+ address-offset text-section-start)))  ;; Segment virtual address
      (bytes/u64-le (u64 (+ address-offset text-section-start)))  ;; Segment physical address
      (bytes/u64-le (u64 (round-up text-section-length #x1000)))  ;; Segment file size
      (bytes/u64-le (u64 (round-up text-section-length #x1000)))  ;; Segment memory size
      (bytes/u64-le (u64 #x1000))  ;; Segment alignment

      (bytes/u32-le (u32 #x6474e551))  ;; Segment type (GNU Stack)
      (bytes/u32-le (u32 #x06))  ;; Segment flags (Read/Write)
      (bytes/u64-le (u64 #x00))  ;; Segment offset
      (bytes/u64-le (u64 #x00))  ;; Segment virtual address
      (bytes/u64-le (u64 #x00))  ;; Segment physical address
      (bytes/u64-le (u64 #x00))  ;; Segment file size
      (bytes/u64-le (u64 #x00))  ;; Segment memory size
      (bytes/u64-le (u64 #x00000)))))  ;; Segment alignment

(define (apply-elf-relocations [relocations : (List (Relocation ElfRelocationKind))]
                               [symbol-addresses : (Dict Bytes LiftedInt)]
                               [section-start-address : Int]
                               [section-contents : Bytes])
  : Bytes
  (case relocations
    [(empty) section-contents]
    [(cons
       (relocation (elf-pc32-relocation) offset symbol)
       relocations)
     (begin
       (bytes-set!/s32-le
         section-contents
         offset
         (case (dict-ref/panic symbol-addresses symbol)
           [(lifted-int v)
            (s32 (- (- v (+ section-start-address offset)) 4))]))
       (apply-elf-relocations relocations symbol-addresses section-start-address section-contents))]
    [(cons
       (relocation (elf-64-relocation addend) offset symbol)
       relocations)
     (begin
       (bytes-set!/u64-le
         section-contents
         offset
         (case (dict-ref/panic symbol-addresses symbol)
           [(lifted-int v) (u64 (+ v addend))]))
       (apply-elf-relocations relocations symbol-addresses section-start-address section-contents))]))
