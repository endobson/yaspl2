#:module (free-variables)
#:import {
  (bytes)
  (data source-location)
  (dict)
  (list)
  (maybe)
  (prim)
  (set)
  (source-language)
}
(export
  (#:types FreeVar)
  (#:values free-variables/module free-types/module free-patterns/module free-statics/module
            free-variables/function-definition free-types/function-definition
            free-patterns/function-definition free-statics/function-definition
            free-types/type-definitions
            free-variables-by-function-definition
            free-var-name)
  (#:patterns free-var))
(types
  (define-type FreeVar
    (free-var [name : Bytes] [source : SourceSpan])))

(define (free-variables-by-function-definition [mod : Module]) : (Dict Bytes (Set Bytes))
  (foldl
    (lambda ([def : FunctionDefinition] [acc : (Dict Bytes (Set Bytes))])
      (dict-add
        acc
        (function-definition-name def)
        (set-add-all
          (make-set bytes-cmp)
          (map free-var-name (free-variables/function-definition def (make-set bytes-cmp) (empty))))))
    (module-definitions mod)
    (ann (Dict Bytes (Set Bytes)) (make-dict bytes-cmp))))

(define (free-variables/module [mod : Module] [env : (Set Bytes)]) : (List FreeVar)
  (free-variables/function-definitions (module-definitions mod) env (empty)))

(define (free-variables/function-definition
          [def : FunctionDefinition]
          [env : (Set Bytes)]
          [acc : (List FreeVar)]) : (List FreeVar)
  (match-define (function-definition _ _ args _ body) def)
  (free-variables/block body (add-function-args-bindings args env) acc))


(define (free-variables/function-definitions
          [defs : (List FunctionDefinition)]
          [env : (Set Bytes)]
          [acc : (List FreeVar)]) : (List FreeVar)
  (case defs
    [(empty) acc]
    [(cons def defs)
     (free-variables/function-definitions
       defs env (free-variables/function-definition def env acc))]))

(define (free-variables/block [block : Block] [env : (Set Bytes)] [acc : (List FreeVar)])
  : (List FreeVar)
  (match-define (block defs body) block)
  (free-variables/block* defs body env acc))

(define (free-variables/block*
          [defs : (List BlockDefinition)]
          [final-expr : Expression]
          [env : (Set Bytes)]
          [acc : (List FreeVar)]) : (List FreeVar)
  (case defs
    [(empty) (free-variables/expr final-expr env acc)]
    [(cons (match-def pattern _ expr) defs)
     (free-variables/block*
       defs final-expr
       (add-pattern-bindings pattern env)
       (free-variables/expr expr env acc))]))

(define (free-variables/expr [expr : Expression] [env : (Set Bytes)] [acc : (List FreeVar)])
  : (List FreeVar)
  (case expr
    [(int-literal _ _) acc]
    [(bytes-literal _ _) acc]
    [(boolean-literal _ _) acc]
    [(annotated-expr _ expr _)
     (free-variables/expr expr env acc)]
    [(if-expr c t f _)
     (free-variables/list (cons c (cons t (cons f (empty)))) env acc)]
    [(begin-expr es e _)
     (free-variables/list (cons e es) env acc)]
    [(app-expr _ op args _)
     (free-variables/list (cons op args) env acc)]
    [(varargs-app-expr _ op args _)
     (free-variables/list (cons op args) env acc)]
    [(let-expr (source-span-annotated name _) expr body _)
     (free-variables/block body (set-add env name) (free-variables/expr expr env acc))]
    [(case-expr expr clauses _)
     (free-variables/clauses clauses env (free-variables/expr expr env acc))]
    [(lambda-expr args _ body _)
     (free-variables/block body (add-function-args-bindings args env) acc)]
    [(var-expr v src)
     (if (set-member? env v)
         acc
         (cons (free-var v src) acc))]))

(define (free-variables/list
          [exprs : (List Expression)]
          [env : (Set Bytes)]
          [acc : (List FreeVar)]) : (List FreeVar)
  (case exprs
    [(empty) acc]
    [(cons expr exprs)
     (free-variables/list exprs env (free-variables/expr expr env acc))]))

(define (free-variables/clauses
          [exprs : (List CaseClause)]
          [env : (Set Bytes)]
          [acc : (List FreeVar)]) : (List FreeVar)
  (case exprs
    [(empty) acc]
    [(cons (case-clause pattern body) clauses)
     (free-variables/clauses clauses env (free-variables/block body (add-pattern-bindings pattern env) acc))]))

(define (add-function-args-bindings [args : (List FunctionArg)] [env : (Set Bytes)])
  : (Set Bytes)
  (case args
    [(empty) env]
    [(cons (function-arg pattern _type) args)
     (add-function-args-bindings args
       (add-pattern-bindings pattern env))]))

(define (add-pattern-bindings [p : Pattern] [env : (Set Bytes)]) : (Set Bytes)
  (case p
    [(bytes-pattern _ _) env]
    [(int-pattern _ _) env]
    [(ignore-pattern _) env]
    [(variable-pattern v _) (set-add env v)]
    [(abstraction-pattern _ pats _) (add-pattern-bindings/list pats env)]))

(define (add-pattern-bindings/list [ps : (List Pattern)] [env : (Set Bytes)]) : (Set Bytes)
  (case ps
    [(empty) env]
    [(cons p ps) (add-pattern-bindings/list ps (add-pattern-bindings p env))]))

(define (free-types/module [mod : Module] [env : (Set Bytes)])
  : (List FreeVar)
  (free-types/type-definitions (module-types mod) env
    (free-types/function-definitions (module-definitions mod) env (empty))))

(define (free-types/function-definitions
          [defs : (List FunctionDefinition)]
          [env : (Set Bytes)]
          [acc : (List FreeVar)]) : (List FreeVar)
  (case defs
    [(empty) acc]
    [(cons def defs)
     (free-types/function-definitions defs env
       (free-types/function-definition def env acc))]))

(define (free-types/function-definition
          [def : FunctionDefinition]
          [env : (Set Bytes)]
          [acc : (List FreeVar)]) : (List FreeVar)
  (match-define (function-definition _ type-vars args result-type  body) def)
  (match-define env (set-add-all env type-vars))
  (free-types/block body env
    (free-types/types
      (map function-arg-pre-type args)
      env
      (free-types/type result-type env acc))))

(define (free-types/type-definitions
          [defs : (List TypeDefinition)]
          [env : (Set Bytes)]
          [acc : (List FreeVar)]) : (List FreeVar)
  (case defs
    [(empty) acc]
    [(cons (type-definition _name vars variants) defs)
     (free-types/type-definitions defs env
        (free-types/variants variants
                             (set-add-all
                               env
                               (case vars
                                 [(nothing) (empty)]
                                 [(just v) v]))
                             acc))]))

(define (free-types/variants
          [variants : (List VariantDefinition)]
          [env : (Set Bytes)]
          [acc : (List FreeVar)]) : (List FreeVar)
  (case variants
    [(empty) acc]
    [(cons (variant-definition _name fields) variants)
     (free-types/variants variants env
       (free-types/types
         (map variant-field-type fields) env acc))]))

(define (free-types/expr [expr : Expression] [env : (Set Bytes)] [acc : (List FreeVar)])
  : (List FreeVar)
  (case expr
    [(int-literal _ _) acc]
    [(bytes-literal _ _) acc]
    [(boolean-literal _ _) acc]
    [(var-expr _ _) acc]
    [(if-expr c t f _)
     (free-types/exprs (cons c (cons t (cons f (empty)))) env acc)]
    [(begin-expr es e _)
     (free-types/exprs (cons e es) env acc)]
    [(app-expr _ op args _)
     (free-types/exprs (cons op args) env acc)]
    [(varargs-app-expr _ op args _)
     (free-types/exprs (cons op args) env acc)]
    [(let-expr _name expr body _)
     (free-types/block body env (free-types/expr expr env acc))]
    [(case-expr expr clauses _)
     (free-types/blocks (map case-clause-body clauses) env
       (free-types/expr expr env acc))]
    [(annotated-expr type expr _)
     (free-types/type type env (free-types/expr expr env acc))]
    [(lambda-expr args return body _)
     (free-types/types
       (case return
         [(nothing) (map function-arg-pre-type args)]
         [(just return-ty) (cons return-ty (map function-arg-pre-type args))])
       env
       (free-types/block body env acc))]))

(define (free-types/exprs [exprs : (List Expression)] [env : (Set Bytes)] [acc : (List FreeVar)])
  : (List FreeVar)
  (case exprs
    [(empty) acc]
    [(cons expr exprs)
     (free-types/exprs exprs env (free-types/expr expr env acc))]))

(define (free-types/blocks [blocks : (List Block)] [env : (Set Bytes)] [acc : (List FreeVar)])
  : (List FreeVar)
  (case blocks
    [(empty) acc]
    [(cons block blocks)
     (free-types/blocks blocks env
       (free-types/block block env acc))]))

(define (free-types/block [b : Block] [env : (Set Bytes)] [acc : (List FreeVar)])
  : (List FreeVar)
  (case b
    [(block defs expr)
     (free-types/expr expr env
       (free-types/block-definitions defs env acc))]))

(define (free-types/block-definitions [defs : (List BlockDefinition)] [env : (Set Bytes)]
                                      [acc : (List FreeVar)])
  : (List FreeVar)
  (case defs
    [(empty) acc]
    [(cons (match-def _pattern type expr) defs)
     (free-types/block-definitions defs env
       (free-types/expr expr env
         (case type
           [(nothing) acc]
           [(just t) (free-types/type t env acc)])))]))

(define (free-types/types [types : (List PreType)] [env : (Set Bytes)] [acc : (List FreeVar)])
  : (List FreeVar)
  (case types
    [(empty) acc]
    [(cons type types)
     (free-types/types types env (free-types/type type env acc))]))

(define (free-types/type [type : PreType] [env : (Set Bytes)] [acc : (List FreeVar)])
  : (List FreeVar)
  (case type
    [(var-pre-type v src)
     (if (set-member? env v)
         acc
         (cons (free-var v src) acc))]
    [(fun-pre-type type-vars args result _)
     (let ([env (set-add-all env type-vars)])
       (free-types/types args env
          (free-types/type result env acc)))]
    [(type-app-pre-type (source-span-annotated constructor src) args _)
     (let ([acc (free-types/types args env acc)])
       (if (set-member? env constructor)
           acc
           (cons (free-var constructor src) acc)))]))

(define (free-patterns/module [mod : Module] [env : (Set Bytes)]) : (List FreeVar)
  (free-patterns/function-definitions (module-definitions mod) env (empty)))

(define (free-patterns/function-definitions
          [defs : (List FunctionDefinition)]
          [env : (Set Bytes)]
          [acc : (List FreeVar)]) : (List FreeVar)
  (case defs
    [(empty) acc]
    [(cons def defs)
     (free-patterns/function-definitions defs env
       (free-patterns/function-definition def env acc))]))


(define (free-patterns/function-definition
          [def : FunctionDefinition]
          [env : (Set Bytes)]
          [acc : (List FreeVar)]) : (List FreeVar)
  (match-define (function-definition _ _ args _ body) def)
  (free-patterns/function-args args env
    (free-patterns/block body env acc)))

(define (free-patterns/function-args
          [args : (List FunctionArg)]
          [env : (Set Bytes)]
          [acc : (List FreeVar)]) : (List FreeVar)
  (case args
    [(empty) acc]
    [(cons (function-arg pattern _type) args)
     (free-patterns/function-args args env
       (free-patterns/pattern pattern env acc))]))

(define (free-patterns [expr : Expression] [env : (Set Bytes)] [acc : (List FreeVar)])
  : (List FreeVar)
  (case expr
    [(int-literal _ _) acc]
    [(bytes-literal _ _) acc]
    [(boolean-literal _ _) acc]
    [(var-expr _ _) acc]
    [(if-expr c t f _)
     (free-patterns/list (cons c (cons t (cons f (empty)))) env acc)]
    [(begin-expr es e _)
     (free-patterns/list (cons e es) env acc)]
    [(app-expr _ op args _)
     (free-patterns/list (cons op args) env acc)]
    [(varargs-app-expr _ op args _)
     (free-patterns/list (cons op args) env acc)]
    [(let-expr _name expr body _)
     (free-patterns/block body env (free-patterns expr env acc))]
    [(case-expr expr clauses _)
     (free-patterns/clauses clauses env (free-patterns expr env acc))]
    [(annotated-expr _ expr _)
     (free-patterns expr env acc)]
    [(lambda-expr _ _ body _)
     (free-patterns/block body env acc)]))

(define (free-patterns/block [b : Block] [env : (Set Bytes)] [acc : (List FreeVar)])
  : (List FreeVar)
  (case b
    [(block defs expr)
     (free-patterns expr env
       (free-patterns/block-definitions defs env acc))]))

(define (free-patterns/block-definitions
          [defs : (List BlockDefinition)]
          [env : (Set Bytes)]
          [acc : (List FreeVar)])
  : (List FreeVar)
  (case defs
    [(empty) acc]
    [(cons (match-def pattern _type expr) defs)
     (free-patterns/block-definitions defs env
       (free-patterns expr env
         (free-patterns/pattern pattern env acc)))]))

(define (free-patterns/list
          [exprs : (List Expression)]
          [env : (Set Bytes)]
          [acc : (List FreeVar)]) : (List FreeVar)
  (case exprs
    [(empty) acc]
    [(cons expr exprs)
     (free-patterns/list exprs env (free-patterns expr env acc))]))

(define (free-patterns/clauses
          [exprs : (List CaseClause)]
          [env : (Set Bytes)]
          [acc : (List FreeVar)]) : (List FreeVar)
  (case exprs
    [(empty) acc]
    [(cons (case-clause pattern body) clauses)
     (free-patterns/clauses clauses env
       (free-patterns/block body env
         (free-patterns/pattern pattern env acc)))]))

(define (free-patterns/pattern [p : Pattern] [env : (Set Bytes)] [acc : (List FreeVar)])
  : (List FreeVar)
  (case p
    [(bytes-pattern _ _) acc]
    [(int-pattern _ _) acc]
    [(ignore-pattern _) acc]
    [(variable-pattern _ _) acc]
    [(abstraction-pattern (source-span-annotated name src) pats _)
     (let ([acc (free-patterns/patterns pats env acc)])
       (if (set-member? env name)
           acc
           (cons (free-var name src) acc)))]))

(define (free-patterns/patterns [ps : (List Pattern)] [env : (Set Bytes)] [acc : (List FreeVar)])
  : (List FreeVar)
  (case ps
    [(empty) acc]
    [(cons p ps) (free-patterns/patterns ps env (free-patterns/pattern p env acc))]))

(define (free-statics/module [mod : Module] [env : (Set Bytes)]) : (List FreeVar)
  (free-statics/function-definitions (module-definitions mod) env (empty)))

(define (free-statics/function-definition
          [def : FunctionDefinition]
          [env : (Set Bytes)]
          [acc : (List FreeVar)]) : (List FreeVar)
  (match-define (function-definition _ _ _ _ body) def)
  (free-statics/block body env acc))


(define (free-statics/function-definitions
          [defs : (List FunctionDefinition)]
          [env : (Set Bytes)]
          [acc : (List FreeVar)]) : (List FreeVar)
  (case defs
    [(empty) acc]
    [(cons def defs)
     (free-statics/function-definitions
       defs env (free-statics/function-definition def env acc))]))

(define (free-statics/block [block : Block] [env : (Set Bytes)] [acc : (List FreeVar)])
  : (List FreeVar)
  (match-define (block defs body) block)
  (free-statics/block* defs body env acc))

(define (free-statics/block*
          [defs : (List BlockDefinition)]
          [final-expr : Expression]
          [env : (Set Bytes)]
          [acc : (List FreeVar)]) : (List FreeVar)
  (case defs
    [(empty) (free-statics/expr final-expr env acc)]
    [(cons (match-def _ _ expr) defs)
     (free-statics/block* defs final-expr env
       (free-statics/expr expr env acc))]))

(define (free-statics/expr [expr : Expression] [env : (Set Bytes)] [acc : (List FreeVar)])
  : (List FreeVar)
  (case expr
    [(var-expr _ _) acc]
    [(int-literal _ _) acc]
    [(bytes-literal _ _) acc]
    [(boolean-literal _ _) acc]
    [(annotated-expr _ expr _)
     (free-statics/expr expr env acc)]
    [(if-expr c t f _)
     (free-statics/list (cons c (cons t (cons f (empty)))) env acc)]
    [(begin-expr es e _)
     (free-statics/list (cons e es) env acc)]
    [(app-expr _ op args _)
     (free-statics/list (cons op args) env acc)]
    [(varargs-app-expr _ op args _)
     (free-statics/list (cons op args) env acc)]
    [(let-expr _ expr body _)
     (free-statics/block body env (free-statics/expr expr env acc))]
    [(case-expr expr clauses _)
     (free-statics/clauses clauses env (free-statics/expr expr env acc))]
    [(lambda-expr _ _ body _)
     (free-statics/block body env acc)]))

(define (free-statics/list
          [exprs : (List Expression)]
          [env : (Set Bytes)]
          [acc : (List FreeVar)]) : (List FreeVar)
  (case exprs
    [(empty) acc]
    [(cons expr exprs)
     (free-statics/list exprs env (free-statics/expr expr env acc))]))

(define (free-statics/clauses
          [exprs : (List CaseClause)]
          [env : (Set Bytes)]
          [acc : (List FreeVar)]) : (List FreeVar)
  (case exprs
    [(empty) acc]
    [(cons (case-clause _ body) clauses)
     (free-statics/clauses clauses env (free-statics/block body env acc))]))
