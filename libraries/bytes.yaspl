(module bytes
  (import
    (prim 
      #:types (Byte Bytes Boolean Void)
      #:values (+ - = bytes-set! bytes-ref void make-bytes bytes-length)
      #:patterns ())
    (list
      #:types (List)
      #:values (map)
      #:patterns (cons empty)))
  (export bytes-copy! bytes=? subbytes bytes-append make-null-terminated)
  (types)

  (define (bytes-copy! [src : Bytes] [s-start : Byte] [s-end : Byte] [dest : Bytes] [d-start : Byte])
    : Void
    (if (= s-start s-end)
        (void)
        (begin
          (bytes-set! dest d-start (bytes-ref src s-start))
          (bytes-copy! src (+ s-start 1) s-end dest (+ d-start 1)))))

  (define (subbytes [src : Bytes] [start : Byte] [end : Byte]) : Bytes
    (let ([new-bytes (make-bytes (- end start))])
      (begin
        (bytes-copy! src start end new-bytes 0)
        new-bytes)))

 (define (bytes=? [b1 : Bytes] [b2 : Bytes]) : Boolean
   (if (= (bytes-length b1) (bytes-length b2))
       (inner-bytes=? b1 b2 0)
       #f))
 (define (inner-bytes=? [b1 : Bytes] [b2 : Bytes] [offset : Byte]) : Boolean
   (if (= offset (bytes-length b1))
       #t
       (if (= (bytes-ref b1 offset) (bytes-ref b2 offset))
           (inner-bytes=? b1 b2 (+ 1 offset))
           #f)))

 (define (sum [nums : (List Byte)]) : Byte
   (sum-helper nums 0))
 (define (sum-helper [nums : (List Byte)] [acc : Byte]) : Byte
   (case nums
     [(empty) acc]
     [(cons num nums) (sum-helper nums (+ num acc))]))


 (define (bytes-append [bytess : (List Bytes)]) : Bytes
   (let ([acc (make-bytes (sum (map bytes-length bytess)))])
     (bytes-append-helper acc 0 bytess)))
 (define (bytes-append-helper [acc : Bytes] [offset : Byte] [bytess : (List Bytes)]) : Bytes
   (case bytess
     [(empty) acc]
     [(cons input bytess)
      (let ([len (bytes-length input)])
        (begin
          (bytes-copy! input 0 len acc offset)
          (bytes-append-helper acc (+ offset len) bytess)))]))

 (define (make-null-terminated [bytes : Bytes]) : Bytes
   (let ([new-bytes (make-bytes (+ (bytes-length bytes) 1))])
     (begin
       (bytes-copy! bytes 0 (bytes-length bytes) new-bytes 0)
       new-bytes))))
