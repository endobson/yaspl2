#:module (unused-local-bindings)
#:import {
  (bytes)
  (data source-location)
  (dict)
  (join-list)
  (list)
  (maybe)
  (prim)
  (tuples)
  (yaspl languages source-language)
}
(export
  (#:types UnusedLocalBindings UnusedFunctionLocalBindings UnusedVariable)
  (#:values module-unused-local-bindings unused-variable-name)
  (#:patterns unused-local-bindings unused-function-local-bindings unused-variable))
(types
  (define-type UnusedLocalBindings
    (unused-local-bindings
      [functions : (List UnusedFunctionLocalBindings)]))
  (define-type UnusedFunctionLocalBindings
    (unused-function-local-bindings
      [function-name : Bytes]
      [variables : (List UnusedVariable)]))
  (define-type State
    (state
      [bindings : (List (Dict Bytes (Maybe UnusedVariable)))]))
  (define-type UnusedVariable
    (unused-variable
      [name : Bytes]
      [source : SourceSpan])))


(define (module-unused-local-bindings [mod : Module])
  : UnusedLocalBindings
  (case mod
    [(module _name _imports _exports _types definitions _statics)
     (unused-local-bindings
       (filter-map
         function-unused-local-bindings
         definitions))]))

(define (function-unused-local-bindings [fun : FunctionDefinition])
  : (Maybe UnusedFunctionLocalBindings)
  (match-define
    (function-definition (source-span-annotated name _) _type-vars _args _return-type body)
    fun)
  (match-define (tuple2 _state unused)
    (unused-local-bindings/block body (state (empty))))
  (case (jl->list unused)
    [(empty) (nothing)]
    [unused
     (just (unused-function-local-bindings name unused))]))


(define (unused-local-bindings/expr [e : Expression] [state : State])
  : (Tuple2 State (JoinList UnusedVariable))
  (case e
    [(int-literal _ _)
     (tuple2 state (empty-jl))]
    [(bytes-literal _ _)
     (tuple2 state (empty-jl))]
    [(boolean-literal _ _)
     (tuple2 state (empty-jl))]
    [(var-expr (identifier v _))
     (tuple2 (mark-var-used state v) (empty-jl))]
    [(annotated-expr _type e _)
     (unused-local-bindings/expr e state)]
    [(if-expr cond true false _)
     (match-define (tuple2 state cond-unused)
       (unused-local-bindings/expr cond state))
     (match-define (tuple2 state true-unused)
       (unused-local-bindings/expr true state))
     (match-define (tuple2 state false-unused)
       (unused-local-bindings/expr false state))
     (tuple2
       state
       (append-jl cond-unused (append-jl true-unused false-unused)))]
    [(begin-expr exprs last-expr _)
     (unused-local-bindings/exprs (cons last-expr exprs) state)]
    [(app-expr _tail-position op args _)
     (unused-local-bindings/exprs (cons op args) state)]
    [(varargs-app-expr _tail-position _ args _)
     (unused-local-bindings/exprs args state)]
    [(let-expr (identifier name name-src) expr body _)
     (match-define (tuple2 state expr-unused)
       (unused-local-bindings/expr expr state))
     (match-define (tuple2 state body-unused)
       (unused-local-bindings/binding
         (varargs list (unused-variable name name-src))
         state
         body))
     (tuple2 state (append-jl expr-unused body-unused))]
    [(lambda-expr args _type body _)
     (unused-local-bindings/binding
       (function-args-bound-variables args)
       state
       body)]
    [(case-expr expr clauses _)
     (match-define (tuple2 state expr-unused)
       (unused-local-bindings/expr expr state))
     (match-define (tuple2 state clauses-unused)
       (unused-local-bindings/clauses clauses state))
     (tuple2 state (append-jl clauses-unused expr-unused))]))

(define (unused-local-bindings/exprs [es : (List Expression)] [state : State])
  : (Tuple2 State (JoinList UnusedVariable))
  (case es
    [(empty) (tuple2 state (empty-jl))]
    [(cons e es)
     (match-define (tuple2 state e-unused)
       (unused-local-bindings/expr e state))
     (match-define (tuple2 state es-unused)
       (unused-local-bindings/exprs es state))
     (tuple2 state (append-jl e-unused es-unused))]))

(define (unused-local-bindings/clauses [cs : (List CaseClause)] [state : State])
  : (Tuple2 State (JoinList UnusedVariable))
  (case cs
    [(empty) (tuple2 state (empty-jl))]
    [(cons c cs)
     (match-define (tuple2 state c-unused)
       (unused-local-bindings/clause c state))
     (match-define (tuple2 state cs-unused)
       (unused-local-bindings/clauses cs state))
     (tuple2 state (append-jl c-unused cs-unused))]))

(define (unused-local-bindings/clause [clause : CaseClause] [state : State])
  : (Tuple2 State (JoinList UnusedVariable))
  (match-define (case-clause pattern body) clause)
  (match-define vars (pattern-bound-variables pattern (empty)))
  (unused-local-bindings/binding
    vars
    state
    body))

(define (function-args-bound-variables [args : (List FunctionArg)])
  : (List UnusedVariable)
  (foldl
    (lambda ([arg : FunctionArg] [acc : (List UnusedVariable)])
      (pattern-bound-variables (function-arg-pattern arg) acc))
    args
    (ann (List UnusedVariable) (empty))))

(define (pattern-bound-variables [p : Pattern] [acc : (List UnusedVariable)])
  : (List UnusedVariable)
  (case p
    [(bytes-pattern _ _) acc]
    [(int-pattern _ _) acc]
    [(ignore-pattern _) acc]
    [(variable-pattern v src) (cons (unused-variable v src) acc)]
    [(abstraction-pattern _ pats _) (pattern-bound-variables/list pats acc)]))

(define (pattern-bound-variables/list [ps : (List Pattern)] [acc : (List UnusedVariable)])
  : (List UnusedVariable)
  (case ps
    [(empty) acc]
    [(cons p ps) (pattern-bound-variables/list ps (pattern-bound-variables p acc))]))

(define (unused-local-bindings/block [b : Block] [state : State])
  : (Tuple2 State (JoinList UnusedVariable))
  (case b
    [(block (empty) expr)
     (unused-local-bindings/expr expr state)]
    [(block (cons (match-def pat _type expr) defs) body-expr)
     (match-define (tuple2 state _expr-unused)
       (unused-local-bindings/expr expr state))
     (unused-local-bindings/binding
       (pattern-bound-variables pat (empty))
       state
       (block defs body-expr))]))

(define (unused-local-bindings/binding
          [vars : (List UnusedVariable)]
          [init-state : State]
          [body : Block])
  : (Tuple2 State (JoinList UnusedVariable))
  (match-define (state bindings) init-state)

  (match-define inner-state
    (state
      (cons
        (foldl
          (lambda ([v : UnusedVariable] [acc : (Dict Bytes (Maybe UnusedVariable))])
            (dict-add acc (unused-variable-name v) (just v)))
          vars
          (ann (Dict Bytes (Maybe UnusedVariable))
            (make-dict bytes-cmp)))
       bindings)))

  (match-define (tuple2 inner-state body-unused)
    (unused-local-bindings/block body inner-state))

  (match-define (tuple2 frame outer-bindings)
    : (Tuple2 (Dict Bytes (Maybe UnusedVariable)) (List (Dict Bytes (Maybe UnusedVariable))))
    (case inner-state
      [(state (empty)) (panic #"State lost a frame")]
      [(state (cons frame outer-bindings))
       (tuple2 frame outer-bindings)]))

  (match-define unused-vars
    (list->jl
      (filter-map (lambda ([v : (Maybe UnusedVariable)]) v) (dict-values frame))))

  (tuple2
    (state outer-bindings)
    (append-jl unused-vars body-unused)))

(define (mark-var-used [s : State] [v : Bytes]) : State
  (match-define (state bindings) s)
  (state (mark-var-used/bindings bindings v)))

(define (mark-var-used/bindings [bindings : (List (Dict Bytes (Maybe UnusedVariable)))] [v : Bytes])
  : (List (Dict Bytes (Maybe UnusedVariable)))
  (case bindings
    [(empty) (empty)]
    [(cons frame bindings)
     (case (dict-ref frame v)
       [(just (just _))
        (cons (dict-set frame v (nothing)) bindings)]
       [(just (nothing))
        (cons frame bindings)]
       [(nothing)
        (cons frame (mark-var-used/bindings bindings v))])]))
