(module sexp-printer
  (import
    (bytes bytes-append)
    (join-list
      #:types (JoinList)
      #:values (single-jl jl->list join-list cons-jl snoc-jl concat-jl)
      #:patterns ())
    (list
      #:types ()
      #:values (map interleave)
      #:patterns ())
    (numbers integer->decimal-bytes)
    (prim
      #:types (Bytes)
      #:values ()
      #:patterns ())
    (sexp-parser
      #:types (Sexp)
      #:values ()
      #:patterns (node symbol-sexp bytes-sexp keyword-sexp boolean-sexp number-sexp)))
  (export
    #:types ()
    #:values (print-sexp)
    #:patterns ())
  (types)

  (define (print-sexp [sexp : Sexp]) : Bytes
    (bytes-append (jl->list (print-sexp* sexp))))


  (define (print-sexp* [sexp : Sexp]) : (JoinList Bytes)
    (case sexp
      [(boolean-sexp v) (single-jl (if v #"#t" #"#f"))]
      [(bytes-sexp v) (varargs join-list #"#\"" v #"\"")]
      [(keyword-sexp v) (varargs join-list #"#:" v)]
      [(symbol-sexp v) (single-jl v)]
      [(number-sexp v) (single-jl (integer->decimal-bytes v))]
      [(node v)
       (cons-jl
         #"("
         (snoc-jl
           (concat-jl (interleave (map print-sexp* v) (single-jl #" ")))
           #")"))])))
