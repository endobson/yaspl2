#:module (yaspl top-level-objects-to-x86-64)
#:import {
  (bytes)
  (either)
  (list)
  (prim)
  (prim-names)
  {(register-language-compiler)
    {#:values
      [compile-function r:compile-function]
    }
  }
  (yaspl top-level-objects)
  (yaspl x86-64-assembly)
}
(export
  (#:values convert-objects))
(types)

(define (convert-objects [objects : (List TopLevelObject)]) : AssemblyFragments
  (combine-fragments (reverse-map convert-object objects)))

(define (convert-object [object : TopLevelObject]) : AssemblyFragment
  (case object
    [(register-function-tlo name fun)
     (case (r:compile-function name fun)
       [(left v) (panic v)]
       [(right v) (assembly-text-fragment v)])]
    [(bytes-tlo name value)
     (match-define parts
       (varargs list
         (address-fragment (bytes-variant-descriptor-symbol) 0)
         (bytes-fragment (bytes/u64-le (u64 (bytes-length value))))
         (bytes-fragment value)))
     ;; The alignment is 3 so that the tag and length fields are naturally aligned
     (assembly-const-fragment (const-fragment name 3 parts))]
    [(variant-descriptor-tlo name variant-name num-star-fields num-i64-fields)
     (match-define parts
       (varargs list
         (address-fragment variant-name 0)
         (bytes-fragment (bytes/u64-le (u64 num-star-fields)))
         (bytes-fragment (bytes/u64-le (u64 num-i64-fields)))))
     ;; The descriptor needs to have an alignment of 8, so that it can be combined with a tag.
     (assembly-const-fragment (const-fragment name 8 parts))]
    [(trivial-variant-tlo name desc-name tag)
     ;; The alignment is 3 so that the variant-descriptor/tag is naturally aligned.
     ;; The tag is the address of the variant descriptor combined with the tag.
     (assembly-const-fragment
       (const-fragment name 3 (varargs list (address-fragment desc-name (u8->s64 tag)))))]
    [(trivial-closure-tlo name fun-name)
     (assembly-const-fragment
       (closure-fragment name (trivial-closure-variant-descriptor-symbol) fun-name))]
    [(mutable-buffer-tlo name alignment size)
     (assembly-zero-data-fragment (zero-data-fragment name alignment size))]))
