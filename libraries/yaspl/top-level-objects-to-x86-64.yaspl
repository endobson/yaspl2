#:module (yaspl top-level-objects-to-x86-64)
#:import {
  (bytes)
  (either)
  (list)
  (prim)
  (prim-implementation)
  {(register-language-compiler)
    {#:values
      [compile-function r:compile-function]
    }
  }
  (tuples)
  (yaspl top-level-objects)
  (yaspl x86-64-assembly)
}
(export
  (#:values convert-objects))
(types)

(define (convert-objects [objects : (List TopLevelObject)]) : AssemblyFragments
  (match-define (tuple2 text-fragments const-fragments)
    (partition-eithers (reverse-map convert-object objects)))
  (assembly-fragments text-fragments const-fragments))

(define (convert-object [object : TopLevelObject]) : (Either TextFragment ConstFragment)
  (case object
    [(register-function-tlo _name fun)
     (case (r:compile-function fun)
       [(left v) (panic v)]
       [(right v) (left v)])]
    [(bytes-tlo name value)
     (match-define parts
       (varargs list
         (address-fragment (bytes-variant-descriptor-symbol) 0)
         (bytes-fragment (bytes/u64-le (u64 (bytes-length value))))
         (bytes-fragment value)))
     ;; The alignment is 3 so that the tag and length fields are naturally aligned
     (right (const-fragment name 3 parts))]
    [(variant-descriptor-tlo name variant-name num-star-fields num-i64-fields)
     (match-define parts
       (varargs list
         (address-fragment variant-name 0)
         (bytes-fragment (bytes/u64-le (u64 num-star-fields)))
         (bytes-fragment (bytes/u64-le (u64 num-i64-fields)))))
     ;; The descriptor needs to have an alignment of 8, so that it can be combined with a tag.
     (right (const-fragment name 8 parts))]
    [(trivial-variant-tlo name desc-name tag)
     ;; The alignment is 3 so that the variant-descriptor/tag is naturally aligned.
     ;; The tag is the address of the variant descriptor combined with the tag.
     (right (const-fragment name 3 (varargs list (address-fragment desc-name (u8->s64 tag)))))]
    [(trivial-closure-tlo name fun-name)
     (right (closure-fragment name (trivial-closure-descriptor-symbol) fun-name))]))
