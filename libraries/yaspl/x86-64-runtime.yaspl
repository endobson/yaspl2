#:module (x86-64-runtime)
#:import {
  (bytes)
  (join-list)
  (list)
  (prim)
  (tuples)
  (yaspl x86-64-assembly)
}
(export
  #:types ()
  #:values (yaspl-runtime)
  #:patterns ())
(types)

(define (yaspl-runtime [main-function-name : Bytes]) : AssemblyFragments
  (match-define (tuple2 text-fragments const-fragments) (unzip (prim-function-fragments)))
  (assembly-fragments
    (append (start-function-fragments main-function-name) text-fragments)
    const-fragments))

(define (start-function-fragments [main-function-name : Bytes]) : (List TextFragment)
  (varargs list
    (function-fragment
      #"start"
      (varargs list
        (section
          (varargs list
            (basic-block #"start"
              (varargs join-list
                (jmp #"start_break")))))))

    (function-fragment
      #"start_break"
      (varargs list
        (section
          (varargs list
            (basic-block #"start_break"
              (varargs join-list
                ;; Allocate Heap (Call mmap)
                ;; No fixed address desired
                (movq (imm 0) (rm-reg (rdi)))
                ;; We want a large heap but cannot load more than an 32 bits
                ;; currently so do a multiplication
                ;; Currently this is 8 * 1024 * 1MiB
                ;; 2^20 = 1MiB
                (movq (imm #x100000) (rm-reg (rsi)))
                ;; 2^13 = 8k
                (movq (imm #x2000) (rm-reg (rax)))
                (imulq (rax) (rsi))
                ;; 0x1 = PROT_READ
                ;; 0x2 = PROT_WRITE
                ;; 0x4 = PROT_EXEC
                (movq (imm #x7) (rm-reg (rdx)))
                ;; 0x0002 = MAP_SHARED
                ;; 0x1000 = MAP_ANON
                (movq (imm #x1002) (rm-reg (r10)))
                ;; FD = -1 for no FD
                (movq (imm (- 0 1)) (rm-reg (r8)))
                ;; offset which is ignored because of MAP_ANON
                (movq (imm 0) (rm-reg (r9)))
                ;; 0xC5 is mmap, plus 0x2000000 because xnu
                (movq (imm #x20000C5) (rm-reg (rax)))
                (syscall)
                (jb #"yprim_start_panic")
                ;; Save heap to %r15 which is our bump pointer
                (movq (rmi-reg (rax)) (rm-reg (r15)))
                ;; Save initial value to r14 for setting up the thread control block later.
                (movq (rmi-reg (rax)) (rm-reg (r14)))

                ;; Ignore number of args as we want to reuse code for environment.
                (addq (imm 8) (rm-reg (rsp)))
                ;; Clear direction for all the scans that will be done when
                (cld)

                ;; Count number of args
                (movq (rmi-reg (rsp)) (rm-reg (rdi)))
                (movq (imm 0) (rm-reg (rax)))
                ;; Assumption: Strings cannot be longer than 2^64-1
                ;; Loads treat the immediate as a 32 bit signed number, which is sign extended.
                ;; Then repne-scasq treats it as unsigned which means -1 => 2^64-1
                (movq (imm (- 0 1)) (rm-reg (rcx)))
                (repne-scasq)
                (notq (rcx))
                (decq (rcx))
                (movq (rmi-reg (rcx)) (rm-reg (r8)))


                (movq (rmi-reg (r15)) (rm-reg (r10))) ; r10 is the array of bytes
                (movq (rmi-reg (r10)) (rm-reg (r11))) ; r11 is the address of the last bytes put into the array

                (movq (rmi-reg (r8)) (rm-mem (mem (r10) 0)))
                (leaq (scaled (r10) (scale-8) (r8) 8) (r15))

                (jmp #"start_copy_args_test")))

            (basic-block #"start_copy_args_test"
              (varargs join-list
                (popq (rm-reg (r9))) ; r9 is now the current string
                (cmpq (imm 0) (rm-reg (r9)))
                (je #"start_copy_args_end")))

            (basic-block #"start_copy_args_body"
              (varargs join-list

                ;; Count string length
                (movq (rmi-reg (r9)) (rm-reg (rdi)))
                (movq (imm 0) (rm-reg (rax)))
                ;; Assumption: Strings cannot be longer than 2^64-1
                ;; Loads treat the immediate as a 32 bit signed number, which is sign extended.
                ;; Then repne-scasb treats it as unsigned which means -1 => 2^64-1
                (movq (imm (- 0 1)) (rm-reg (rcx)))
                (repne-scasb)
                (notq (rcx))
                (decq (rcx))

                ;; Copy bytes
                (addq (imm 8) (rm-reg (r11)))
                (movq (rmi-reg (r15)) (rm-mem (mem (r11) 0)))
                (movq (rmi-reg (rcx)) (rm-mem (mem (r15) 0)))
                (addq (imm 8) (rm-reg (r15)))
                (movq (rmi-reg (r9)) (rm-reg (rsi)))
                (movq (rmi-reg (r15)) (rm-reg (rdi)))
                (rep-movsb)
                (movq (rmi-reg (rdi)) (rm-reg (r15)))
                (jmp #"start_copy_args_test")))

            (basic-block #"start_copy_args_end"
              (varargs join-list

                ;; Count number of env
                (movq (rmi-reg (rsp)) (rm-reg (rdi)))
                (movq (imm 0) (rm-reg (rax)))
                ;; Assumption: Strings cannot be longer than 2^64-1
                ;; Loads treat the immediate as a 32 bit signed number, which is sign extended.
                ;; Then repne-scasq treats it as unsigned which means -1 => 2^64-1
                (movq (imm (- 0 1)) (rm-reg (rcx)))
                (repne-scasq)
                (notq (rcx))
                (decq (rcx))
                (movq (rmi-reg (rcx)) (rm-reg (r8)))

                (movq (rmi-reg (r15)) (rm-reg (r13))) ; r13 is the array of bytes
                (movq (rmi-reg (r13)) (rm-reg (r11))) ; r11 is the address of the last bytes put into the array

                (movq (rmi-reg (r8)) (rm-mem (mem (r13) 0)))
                (leaq (scaled (r13) (scale-8) (r8) 8) (r15))

                (jmp #"start_copy_env_test")))

            (basic-block #"start_copy_env_test"
              (varargs join-list
                (popq (rm-reg (r9))) ; r9 is now the current string
                (cmpq (imm 0) (rm-reg (r9)))
                (je #"start_copy_env_end")))

            (basic-block #"start_copy_env_body"
              (varargs join-list

                ;; Count string length
                (movq (rmi-reg (r9)) (rm-reg (rdi)))
                (movq (imm 0) (rm-reg (rax)))
                ;; Assumption: Strings cannot be longer than 2^64-1
                ;; Loads treat the immediate as a 32 bit signed number, which is sign extended.
                ;; Then repne-scasb treats it as unsigned which means -1 => 2^64-1
                (movq (imm (- 0 1)) (rm-reg (rcx)))
                (repne-scasb)
                (notq (rcx))
                (decq (rcx))

                ;; Copy bytes
                (addq (imm 8) (rm-reg (r11)))
                (movq (rmi-reg (r15)) (rm-mem (mem (r11) 0)))
                (movq (rmi-reg (rcx)) (rm-mem (mem (r15) 0)))
                (addq (imm 8) (rm-reg (r15)))
                (movq (rmi-reg (r9)) (rm-reg (rsi)))
                (movq (rmi-reg (r15)) (rm-reg (rdi)))
                (rep-movsb)
                (movq (rmi-reg (rdi)) (rm-reg (r15)))
                (jmp #"start_copy_env_test")))


            (basic-block #"start_copy_env_end"
              (varargs join-list
                (nop)))


            ;; Call main and then exit
            (basic-block #"start_call_main"
              (varargs join-list
                ;; Set up the thread control block
                (subq (imm #x08) (rsp))
                ;; Make our initial allocation look like 2 Mebibytes.
                (addq (imm #x200000) (rm-reg (r14)))
                (movq (rmi-reg (r14)) (rm-mem (mem (rsp) 0)))
                (movq (rmi-reg (rsp)) (rm-reg (r14)))


                ;; Set up initial base pointer
                (movq (imm 0) (rm-reg (rbp)))
                (movq (rmi-reg (r10)) (rm-reg (rdi))) ;; Arguments
                (movq (imm 0) (rm-reg (rsi)))
                (movq (imm 1) (rm-reg (rdx)))
                (movq (imm 2) (rm-reg (rcx)))
                (movq (rmi-reg (r13)) (rm-reg (r8))) ;; Environment
                (pushq (imm 0)) ;; Bogus closure
                (call (bytes-append (varargs list #"y" main-function-name #"_code")))
                ;; Closure is still on the stack
                (movq (rmi-reg (rax)) (rm-reg (rdi)))
                (movq (imm #x2000001) (rm-reg (rax)))
                (syscall)))

            (basic-block #"yprim_start_panic"
              (varargs join-list
                (ud2)))))))))


(define (prim-function-fragments) : (List (Tuple2 TextFragment ConstFragment))
  (varargs list
    ;; write-bytes
    (prim-assembly-function/panic #"write_bytes"
      (varargs list
        ;; Swap %rdi and %rsi
        (movq (rmi-reg (rdi)) (rm-reg (rax)))
        (movq (rmi-reg (rsi)) (rm-reg (rdi)))
        (movq (rmi-reg (rax)) (rm-reg (rsi)))

        ;; %rdi is now the file descriptor
        ;; Make %rsi the start of the buffer
        (leaq (scaled (rsi) (scale-1) (rdx) 8) (rsi))
        ;; Make %rdx the length to write
        (subq (rmi-reg (rdx)) (rcx))
        (movq (rmi-reg (rcx)) (rm-reg (rdx)))
        (movq (imm #x2000004) (rm-reg (rax)))
        (syscall)
        (jb #"yprim_write_bytes_panic")
        (ret)))

    ;; read-bytes
    (prim-assembly-function/panic #"read_bytes"
      (varargs list
        ;; Swap %rdi and %rsi
        (movq (rmi-reg (rdi)) (rm-reg (rax)))
        (movq (rmi-reg (rsi)) (rm-reg (rdi)))
        (movq (rmi-reg (rax)) (rm-reg (rsi)))

        ;; %rdi is the file descriptor
        ;; Make %rsi the start of the buffer
        (leaq (scaled (rsi) (scale-1) (rdx) 8) (rsi))
        ;; Make %rdx the length to read
        (subq (rmi-reg (rdx)) (rcx))
        (movq (rmi-reg (rcx)) (rm-reg (rdx)))
        (movq (imm #x2000003) (rm-reg (rax)))
        (syscall)
        (jb #"yprim_read_bytes_panic")
        (ret)))

    ;; open-input-file
    (prim-assembly-function/panic #"open_input_file"
      (varargs list
        ;; Make %rdi point at start of null terminated path
        (addq (imm 8) (rm-reg (rdi)))
        (movq (imm 0) (rm-reg (rsi))) ;; 0 is read only
        (movq (imm #x2000005) (rm-reg (rax)))
        (syscall)
        (jb #"yprim_open_input_file_panic")
        (ret)))

    ;; open-output-file
    (prim-assembly-function/panic #"open_output_file"
      (varargs list
        ;; Make %rdi point at start of null terminated path
        (addq (imm 8) (rm-reg (rdi)))
        (movq (imm #x0a01) (rm-reg (rsi))) ;; 0x01 is write only, 0x0a00 is exclusive create
        (movq (imm 504) (rm-reg (rdx))) ;; 770 for mode
        (movq (imm #x2000005) (rm-reg (rax)))
        (syscall)
        (jb #"yprim_open_output_file_panic")
        (ret)))

    ;; close-output-port
    ;; close-input-port
    (prim-assembly-function/panic #"close_port"
      (varargs list
        (movq (imm #x2000006) (rm-reg (rax)))
        (syscall)
        (jb #"yprim_close_port_panic")
        (ret)))

    ;; delete-file
    (prim-assembly-function/panic #"delete_file"
      (varargs list
        ;; Make %rdi point at start of null terminated path
        (addq (imm 8) (rm-reg (rdi)))
        (movq (imm #x200000a) (rm-reg (rax)))
        (syscall)
        (jb #"yprim_delete_file_panic")
        (ret)))

    ;; make-directory
    (prim-assembly-function/panic #"make_directory"
      (varargs list
        ;; Make %rdi point at start of null terminated path
        (addq (imm 8) (rm-reg (rdi)))
        (movq (imm 504) (rm-reg (rsi))) ;; 770 for mode
        (movq (imm #x2000088) (rm-reg (rax)))
        (syscall)
        (jb #"yprim_delete_file_panic")
        (ret)))


    ;; set-file-or-directory-permissions
    (prim-assembly-function/panic #"set_file_or_directory_permissions"
      (varargs list
        ;; Make %rdi point at start of null terminated path
        (addq (imm 8) (rm-reg (rdi)))
        ;; %rsi is already the permissions
        (movq (imm #x200000f) (rm-reg (rax)))
        (syscall)
        (jb #"yprim_set_file_or_directory_permissions_panic")
        (ret)))


    ;; socket
    (prim-assembly-function/panic #"socket"
      (varargs list
        ;; Argument 1: domain
        ;; Argument 2: type
        ;; Argument 3: protocol
        (movq (imm #x2000061) (rm-reg (rax)))
        (syscall)
        (jb #"yprim_socket_panic")
        (ret)))

    ;; bind
    (prim-assembly-function/panic #"bind"
      (varargs list
        ;; Argument 1: socket
        ;; Argument 2: sockaddr
        ;; Argument 3: length
        (addq (imm 8) (rm-reg (rsi)))
        (movq (imm #x2000068) (rm-reg (rax)))
        (syscall)
        (jb #"yprim_bind_panic")
        (ret)))

    ;; sendto
    (prim-assembly-function/panic #"sendto"
      (varargs list
        ;; Argument 1: socket
        ;; Argument 2: buffer
        ;; Argument 3: buffer-length
        ;; Argument 4: flags
        ;; Argument 5: sockaddr
        ;; Argument 6: sockaddr-length
        (addq (imm 8) (rm-reg (rsi)))
        (addq (imm 8) (rm-reg (r8)))
        (movq (rmi-reg (rcx)) (rm-reg (r10)))
        (movq (imm #x2000085) (rm-reg (rax)))
        (syscall)
        (jb #"yprim_sendto_panic")
        (ret)))

    ;; recvfrom
    (prim-assembly-function/panic #"recvfrom"
      (varargs list
        ;; Argument 1: socket
        ;; Argument 2: buffer
        ;; Argument 3: buffer-length
        ;; Argument 4: flags
        ;; Argument 5: sockaddr
        ;; Argument 6: sockaddr-length
        (addq (imm 8) (rm-reg (rsi)))
        (addq (imm 8) (rm-reg (r8)))
        (addq (imm 8) (rm-reg (r9)))
        (movq (rmi-reg (rcx)) (rm-reg (r10)))
        (movq (imm #x200001d) (rm-reg (rax)))
        (syscall)
        (jb #"yprim_recvfrom_panic")
        (ret)))

    ;; connect
    (prim-assembly-function/panic #"connect"
      (varargs list
        ;; Argument 1: socket
        ;; Argument 2: sockaddr
        ;; Argument 3: sockaddr-length
        (addq (imm 8) (rm-reg (rsi)))
        (movq (imm #x2000062) (rm-reg (rax)))
        (syscall)
        (jb #"yprim_connect_panic")
        (ret)))

    ;; shutdown
    (prim-assembly-function/panic #"shutdown"
      (varargs list
        ;; Argument 1: socket
        ;; Argument 2: how
        (movq (imm #x2000086) (rm-reg (rax)))
        (syscall)
        (jb #"yprim_shutdown_panic")
        (ret)))


    ;; listen
    (prim-assembly-function/panic #"listen"
      (varargs list
        ;; Argument 1: socket
        ;; Argument 2: backlog
        (movq (imm #x200006A) (rm-reg (rax)))
        (syscall)
        (jb #"yprim_listen_panic")
        (ret)))

    ;; accept
    (prim-assembly-function/panic #"accept"
      (varargs list
        ;; Argument 1: socket
        ;; Argument 2: sockaddr
        ;; Argument 3: sockaddr-length
        (addq (imm 8) (rm-reg (rsi)))
        (addq (imm 8) (rm-reg (rdx)))
        (movq (imm #x200001E) (rm-reg (rax)))
        (syscall)
        (jb #"yprim_accept_panic")
        (ret)))


    ;; setsockopt
    (prim-assembly-function/panic #"setsockopt"
      (varargs list
        ;; Argument 1: socket
        ;; Argument 2: level
        ;; Argument 3: option name
        ;; Argument 4: option value
        ;; Argument 5: option length
        (movq (rmi-reg (rcx)) (rm-reg (r10)))
        (addq (imm 8) (rm-reg (r10)))
        (movq (imm #x2000069) (rm-reg (rax)))
        (syscall)
        (jb #"yprim_setsockopt_panic")
        (ret)))


    ;; mach_task_self
    (prim-assembly-function/panic #"mach_task_self"
      (varargs list
        (movq (imm #x100001c) (rm-reg (rax)))
        (syscall)
        (cmpq (imm 0) (rm-reg (rax)))
        (je #"yprim_mach_task_self_panic")
        (ret)))


    ;; mach_thread_self
    (prim-assembly-function/panic #"mach_thread_self"
      (varargs list
        (movq (imm #x100001b) (rm-reg (rax)))
        (syscall)
        (cmpq (imm 0) (rm-reg (rax)))
        (je #"yprim_mach_thread_self_panic")
        (ret)))


    ;; mach_reply_port
    (prim-assembly-function/panic #"mach_reply_port"
      (varargs list
        (movq (imm #x100001a) (rm-reg (rax)))
        (syscall)
        (cmpq (imm 0) (rm-reg (rax)))
        (je #"yprim_mach_reply_port_panic")
        (ret)))


    ;; mach_port_mod_refs
    (prim-assembly-function/panic #"mach_port_mod_refs"
      (varargs list
        ;; Argument 1: task
        ;; Argument 2: port
        ;; Argument 3: right
        ;; Argument 4: delta
        (movq (rmi-reg (rcx)) (rm-reg (r10)))
        (movq (imm #x1000013) (rm-reg (rax)))
        (syscall)
        (cmpq (imm 0) (rm-reg (rax)))
        (jne #"yprim_mach_port_mod_refs_panic")
        (ret)))

    ;; mach_msg
    (prim-assembly-function/panic #"mach_msg"
      (varargs list
        (movq (rmi-mem (mem (rax) 0)) (rm-reg (rax))) ;; Notify Port
        (movq (rmi-reg (rcx)) (rm-reg (r10)))

        (addq (imm 8) (rm-reg (rdi))) ;; Adjust past buffer's length field
        (pushq (rmi-reg (rax))) ;; Push back the notify port
        (pushq (imm 0)) ;; Dummy Return Address
        (movq (imm #x100001f) (rm-reg (rax)))
        (syscall)
        (cmpq (imm 0) (rm-reg (rax)))
        (jne #"yprim_mach_msg_panic")
        (addq (imm #x10) (rm-reg (rsp))) ;; 2 pop
        (ret)))


    ;; and
    (prim-assembly-function #"and"
      (varargs list
        (andq (rmi-reg (rdi)) (rsi))
        (movq (rmi-reg (rsi)) (rm-reg (rax)))
        (ret)))
    ;; or
    (prim-assembly-function #"or"
      (varargs list
        (orq (rdi) (rsi))
        (movq (rmi-reg (rsi)) (rm-reg (rax)))
        (ret)))
    ;; not
    (prim-assembly-function #"not"
      (varargs list
        (xorq (imm 1) (rdi))
        (movq (rmi-reg (rdi)) (rm-reg (rax)))
        (ret)))


    ;; bytes-ref
    (prim-assembly-function #"bytes_ref"
      (varargs list
        (movzbq (r8m-mem (scaled (rdi) (scale-1) (rsi) 8)) (rax))
        (ret)))
    ;; make-bytes
    (prim-assembly-function #"make_bytes"
      (varargs list
        (movq (rmi-reg (rdi)) (rm-reg (rax)))
        (addq (imm 15) (rm-reg (rax)))
        (andq (imm (- 0 8)) (rax))
        (call #"yprim_heap_allocate")

        (movq (rmi-reg (rax)) (rm-reg (rdx)))
        (movq (rmi-reg (rdi)) (rm-mem (mem (rax) 0)))
        (movq (rmi-reg (rdi)) (rm-reg (rcx)))
        (movq (rmi-reg (rax)) (rm-reg (rdi)))
        (addq (imm 8) (rm-reg (rdi)))
        (movq (imm 0) (rm-reg (rax)))
        (cld)
        (rep-stosb)
        (movq (rmi-reg (rdx)) (rm-reg (rax)))
        (ret)))

    ;; bytes-set!
    (prim-assembly-function #"bytes_set"
      (varargs list
        (movb (dl) (scaled (rdi) (scale-1) (rsi) 8))
        (movq (imm 0) (rm-reg (rax)))
        (ret)))
    ;; bytes-length
    (prim-assembly-function #"bytes_length"
      (varargs list
        (movq (rmi-mem (mem (rdi) 0)) (rm-reg (rax)))
        (ret)))

    ;; void
    (prim-assembly-function #"void"
      (varargs list
        (movq (imm 0) (rm-reg (rax)))
        (ret)))

    ;; u8
    ;; TODO(endobson) add check that it is in the right range
    (prim-assembly-function #"u8"
      (varargs list
        (movq (rmi-reg (rdi)) (rm-reg (rax)))
        (ret)))

    ;; s32
    ;; TODO(endobson) add check that it is in the right range
    (prim-assembly-function #"s32"
      (varargs list
        (movq (rmi-reg (rdi)) (rm-reg (rax)))
        (ret)))

    ;; u32
    ;; TODO(endobson) add check that it is in the right range
    (prim-assembly-function #"u32"
      (varargs list
        (movq (rmi-reg (rdi)) (rm-reg (rax)))
        (ret)))

    ;; u8->s64
    (prim-assembly-function #"u8_s64"
      (varargs list
        (movq (rmi-reg (rdi)) (rm-reg (rax)))
        (ret)))

    ;; s32->s64
    (prim-assembly-function #"s32_s64"
      (varargs list
        (movq (rmi-reg (rdi)) (rm-reg (rax)))
        (ret)))

    ;; u32->s64
    (prim-assembly-function #"u32_s64"
      (varargs list
        (movq (rmi-reg (rdi)) (rm-reg (rax)))
        (ret)))

    ;; u32/le-byte#
    ;; s32/le-byte#
    (prim-assembly-function #"quad_le_byte0"
      (varargs list
        (andq (imm #xFF) (rdi))
        (movq (rmi-reg (rdi)) (rm-reg (rax)))
        (ret)))
    (prim-assembly-function #"quad_le_byte1"
      (varargs list
        (movq (imm #x8) (rm-reg (rcx)))
        (shrq (rdi))
        (andq (imm #xFF) (rdi))
        (movq (rmi-reg (rdi)) (rm-reg (rax)))
        (ret)))
    (prim-assembly-function #"quad_le_byte2"
      (varargs list
        (movq (imm #x10) (rm-reg (rcx)))
        (shrq (rdi))
        (andq (imm #xFF) (rdi))
        (movq (rmi-reg (rdi)) (rm-reg (rax)))
        (ret)))
    (prim-assembly-function #"quad_le_byte3"
      (varargs list
        (movq (imm #x18) (rm-reg (rcx)))
        (shrq (rdi))
        (andq (imm #xFF) (rdi))
        (movq (rmi-reg (rdi)) (rm-reg (rax)))
        (ret)))

    (prim-assembly-function/panic #"ioctl"
      (varargs list
        ;; Make %rdx point at the actual buffer and not the length
        (addq (imm 8) (rm-reg (rdx)))
        (movq (imm #x2000036) (rm-reg (rax)))
        (syscall)
        (jb #"yprim_ioctl_panic")
        (ret)))

    ;; panic
    (prim-assembly-function #"panic"
      (varargs list
        (pushq (rmi-reg (rbp)))
        (movq (rmi-reg (rsp)) (rm-reg (rbp)))
        (addq (imm 8) (rm-reg (rdi)))
        (ud2)))

    ;; These are not native functions, but just native subroutines
    ;; They do not follow standard calling conventions
    (tuple2
      (function-fragment
        #"yprim_heap_allocate"
        (varargs list
          (section
            (varargs list
              (basic-block #"yprim_heap_allocate"
                (varargs join-list
                  (xaddq (rax) (r15))
                  (cmpq (rmi-reg (r15)) (rm-mem (mem (r14) 0)))
                  (jbe #"yprim_heap_allocate_slow")
                  (ret)))
              (basic-block #"yprim_heap_allocate_slow"
                (varargs join-list
                 ;; Reverse the xadd
                 (subq (rmi-reg (r15)) (rax))
                 (negq (rax))
                 ;; 'allocate' 2MiB more
                 (movq (rmi-mem (mem (r14) 0)) (rm-reg (r15)))
                 (addq (imm #x200000) (rm-mem (mem (r14) 0)))
                 (jmp #"yprim_heap_allocate")))))))
      (address-fragment
        #"yprim_heap_allocate_closure"
        #"yprim_heap_allocate"))

    ;; variant-panic
    (prim-assembly-function #"variant_panic"
      (varargs list
        (ud2)))

    ;; fill-stack
    (prim-assembly-function #"fill_stack"
      (varargs list
        (pushq (rmi-reg (rbp)))
        (movq (rmi-reg (rsp)) (rm-reg (rbp)))
        (movq (rmi-reg (rbp)) (rm-reg (rsi)))
        (call #"yprim_fill_stack_inner")
        (popq (rm-reg (rbp)))
        (ret)))


    ;; thread_bootstrap
    (prim-assembly-function #"thread_bootstrap"
      (varargs list
        (pushq (rmi-reg (rax)))
        (movq (rmi-reg (rax)) (rm-reg (r11)))
        (call/indirect (rm-mem (mem (rax) 0)))
        ;; Closure is still on the stack
        (ud2)))))

(define (prim-assembly-function
          [name : Bytes]
          [instructions : (List Instruction)]) : (Tuple2 TextFragment ConstFragment)
  (prim-assembly-function* name instructions #f))
(define (prim-assembly-function/panic
          [name : Bytes]
          [instructions : (List Instruction)]) : (Tuple2 TextFragment ConstFragment)
  (prim-assembly-function* name instructions #t))

(define (prim-assembly-function*
          [name : Bytes]
          [instructions : (List Instruction)]
          [panic : Boolean]) : (Tuple2 TextFragment ConstFragment)
  (tuple2
    (function-fragment (bytes-append (varargs list #"yprim_" name))
      (let ([main-block
             (basic-block (bytes-append (varargs list #"yprim_" name))
               (list->jl instructions))])
      (varargs list
        (section
          (if panic
              (varargs list
                main-block
                (basic-block (bytes-append (varargs list #"yprim_" name #"_panic"))
                  (varargs join-list (ud2))))
              (varargs list main-block))))))
    (address-fragment
      (bytes-append (varargs list #"yprim_" name #"_closure"))
      (bytes-append (varargs list #"yprim_" name)))))
