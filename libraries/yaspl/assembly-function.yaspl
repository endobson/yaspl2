#:module (yaspl assembly-function)
#:import {
  (bytes)
  (join-list)
  (list)
  (prim)
  (prim-names)
  (tuples)
  (yaspl x86-64-assembly)
}
(export
  (#:types PrimAssemblyFunction)
  (#:values prim-assembly-function prim-assembly-function/panic prim-assembly-function->fragments))
(types
  (define-type PrimAssemblyFunction
    (prim-assembly-function*
      [name : Bytes]
      [instructions : (JoinList Instruction)]
      [panic : Boolean])))

(define (prim-assembly-function
          [name : Bytes]
          [instructions : (JoinList Instruction)]) : PrimAssemblyFunction
  (prim-assembly-function* name instructions #f))

(define (prim-assembly-function/panic
          [name : Bytes]
          [instructions : (JoinList Instruction)]) : PrimAssemblyFunction
  (prim-assembly-function* name instructions #t))

(define (prim-assembly-function->fragments [f : PrimAssemblyFunction])
  : (Tuple2 TextFragment ConstFragment)
  (match-define (prim-assembly-function* name instructions panic) f)
  (tuple2
    (function-fragment (bytes-append (varargs list #"yprim_" name))
      (varargs append-jl*
        (named-block
          (bytes-append (varargs list #"yprim_" name #"_0"))
          instructions)
        (if panic
            (named-block
              (bytes-append (varargs list #"yprim_" name #"_panic"))
              (varargs join-list (ud2)))
            (empty-jl))))
    (closure-fragment
      (bytes-append (varargs list #"yprim_" name #"_closure"))
      (trivial-closure-variant-descriptor-symbol)
      (bytes-append (varargs list #"yprim_" name)))))
