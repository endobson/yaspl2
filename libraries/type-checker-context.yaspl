#:module (type-checker-context)
#:import {
  (bytes)
  (dict)
  (list)
  (maybe)
  (prim)
  (resolved-types-language)
  (tuples)
  (types)
}
(export
  (#:types Context)
  (#:values context merge-contexts merge-contexts*
            context-ref-value context-set-values context-set-value
            context-ref-pattern context-values)
  (#:patterns))
(types
  (define-type Context
    (context
      [patterns : (Dict Bytes PatternDescriptor)]
      [values : (Dict Bytes Type)])))


(define (context-set-values [context : Context] [vs : (List (Tuple2 Bytes Type))]) : Context
  (case vs
    [(empty) context]
    [(cons (tuple2 name type) vs)
     (context-set-values
       (context-set-value context name type)
       vs)]))

(define (context-set-value [ctx : Context] [name : Bytes] [type : Type]) : Context
  (case ctx
    [(context patterns values)
     (context patterns (dict-set values name type))]))

(define (context-ref-value [ctx : Context] [name : Bytes]) : (Maybe Type)
  (case ctx
    [(context _ values)
     (dict-ref values name)]))

(define (context-ref-pattern [ctx : Context] [name : Bytes]) : (Maybe PatternDescriptor)
  (case ctx
    [(context patterns _)
     (dict-ref patterns name)]))

(define (merge-contexts* [cs : (List Context)]) : Context
  (case cs
    [(empty)
     (context
       (make-dict bytes-cmp)
       (make-dict bytes-cmp))]
    [(cons c (empty)) c]
    [(cons c1 (cons c2 cs))
     (merge-contexts* (cons (merge-contexts c1 c2) cs))]))

(define (merge-contexts [c1 : Context] [c2 : Context]) : Context
  (case c1
    [(context p1 v1)
     (case c2
       [(context p2 v2)
        (context
          (merge-dicts p1 p2)
          (merge-dicts v1 v2))])]))
