#:module (type-checker-context)
#:import {
  (bytes)
  (dict)
  (list)
  (maybe)
  (prim)
  (tuples)
  (types)
}
(export
  (#:types Context StaticInfo)
  (#:values context merge-contexts merge-contexts*
            context-ref-value context-set-values context-set-value
            context-values
            varargs-info)
  (#:patterns))
(types
  (define-type Context
    (context
      [values : (Dict Bytes Type)]
      [statics : (Dict Bytes StaticInfo)]))
  (define-type StaticInfo
    (varargs-info
      [type-vars : (List Bytes)]
      [arg-type : Type]
      [return-type : Type])))

(define (context-set-values [context : Context] [vs : (List (Tuple2 Bytes Type))]) : Context
  (case vs
    [(empty) context]
    [(cons (tuple2 name type) vs)
     (context-set-values
       (context-set-value context name type)
       vs)]))

(define (context-set-value [ctx : Context] [name : Bytes] [type : Type]) : Context
  (case ctx
    [(context values statics)
     (context (dict-set values name type) statics)]))

(define (context-ref-value [ctx : Context] [name : Bytes]) : (Maybe Type)
  (case ctx
    [(context values _)
     (dict-ref values name)]))

(define (context-ref-static [ctx : Context] [name : Bytes]) : (Maybe StaticInfo)
  (case ctx
    [(context _ statics)
     (dict-ref statics name)]))

(define (merge-contexts* [cs : (List Context)]) : Context
  (case cs
    [(empty)
     (context (make-dict bytes-cmp) (make-dict bytes-cmp))]
    [(cons c (empty)) c]
    [(cons c1 (cons c2 cs))
     (merge-contexts* (cons (merge-contexts c1 c2) cs))]))

(define (merge-contexts [c1 : Context] [c2 : Context]) : Context
  (case c1
    [(context v1 s1)
     (case c2
       [(context v2 s2)
        (context (merge-dicts v1 v2) (merge-dicts s1 s2))])]))
