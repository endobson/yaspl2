#:module (type-checker-context)
#:import {
  (bytes)
  (dict)
  (list)
  (maybe)
  (module-name)
  (prim)
  (tuples)
  (types)
}
(export
  (#:types Context StaticInfo PatternInfo)
  (#:values context merge-contexts merge-contexts*
            context-ref-value context-set-values context-set-value
            context-ref-static
            context-ref-pattern
            context-values
            varargs-info pattern-info)
  (#:patterns varargs-info pattern-info))
(types
  (define-type Context
    (context
      [values : (Dict Bytes Type)]
      [patterns : (Dict Bytes PatternInfo)]
      [statics : (Dict Bytes StaticInfo)]))
  (define-type StaticInfo
    (varargs-info
      [type-vars : (List Bytes)]
      [arg-type : Type]
      [return-type : Type]))
  (define-type PatternInfo
    (pattern-info
      [matched-name : Bytes]
      [all-variants : (List Bytes)]
      [mod-name : ModName]
      [type-name : Bytes]
      [type-vars : (List Bytes)]
      [field-types : (List Type)])))

(define (context-set-values [context : Context] [vs : (List (Tuple2 Bytes Type))]) : Context
  (case vs
    [(empty) context]
    [(cons (tuple2 name type) vs)
     (context-set-values
       (context-set-value context name type)
       vs)]))

(define (context-set-value [ctx : Context] [name : Bytes] [type : Type]) : Context
  (case ctx
    [(context values patterns statics)
     (context (dict-set values name type) patterns statics)]))

(define (context-ref-value [ctx : Context] [name : Bytes]) : (Maybe Type)
  (case ctx
    [(context values _ _)
     (dict-ref values name)]))

(define (context-ref-static [ctx : Context] [name : Bytes]) : (Maybe StaticInfo)
  (case ctx
    [(context _ _ statics)
     (dict-ref statics name)]))

(define (context-ref-pattern [ctx : Context] [name : Bytes]) : (Maybe PatternInfo)
  (case ctx
    [(context _ patterns _)
     (dict-ref patterns name)]))

(define (merge-contexts* [cs : (List Context)]) : Context
  (case cs
    [(empty)
     (context (make-dict bytes-cmp) (make-dict bytes-cmp) (make-dict bytes-cmp))]
    [(cons c (empty)) c]
    [(cons c1 (cons c2 cs))
     (merge-contexts* (cons (merge-contexts c1 c2) cs))]))

(define (merge-contexts [c1 : Context] [c2 : Context]) : Context
  (case c1
    [(context v1 p1 s1)
     (case c2
       [(context v2 p2 s2)
        (context (merge-dicts v1 v2) (merge-dicts p1 p2) (merge-dicts s1 s2))])]))
