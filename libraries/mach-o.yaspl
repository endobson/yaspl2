(module mach-o
  (import
    (prim
      #:types (Bytes Byte InputPort OutputPort Array Void)
      #:values (- < = array-length array-ref read-bytes make-bytes
                panic void bytes-ref)
      #:patterns ())
    (boolean not)
    (either
      #:types (Either)
      #:values (right left)
      #:patterns ())
    (io write-line write-all-bytes call-with-input-file)
    (numbers integer->hex-bytes integer->decimal-bytes)
    (bytes bytes-ref/quad-le make-null-terminated))
  (export
    #:types (MachOFile)
    #:values (read-mach-o read-load-command)
    #:patterns (mach-o-file mach-o-load-command mach-o-header))
  (types
    (define-type MachOFile
      (mach-o-file
        [header MachOHeader]
        [raw-contents Bytes]))
    (define-type MachOHeader
      (mach-o-header
        [magic Byte]
        [cpu-type Byte]
        [cpu-subtype Byte]
        [file-type Byte]
        [number-commands Byte]
        [commands-size Byte]
        [flags Byte]
        [reserved Byte]))
    (define-type MachOLoadCommand
      (mach-o-load-command
        [number Byte]
        [raw-contents Bytes])))


  (define (read-load-command [input : InputPort]) : (Either Bytes MachOLoadCommand)
    (let ([standard-header (make-bytes 8)])
      (if (not (= 8 (read-bytes standard-header input 0 8)))
          (left #"Not enough bytes")
          (let ([number (bytes-ref/quad-le standard-header 0)])
            (let ([command-size (bytes-ref/quad-le standard-header 4)])
              (let ([command-buffer (make-bytes (- command-size 8))])
                (if (= (- command-size 8) (read-bytes command-buffer input 0 (- command-size 8)))
                    (right (mach-o-load-command number command-buffer))
                    (left #"Not enough bytes"))))))))

  (define (read-mach-o [input : InputPort]) : (Either Bytes MachOFile)
    (let ([header-bytes (make-bytes 32)])
      (if (not (= 32 (read-bytes header-bytes input 0 32)))
          (left #"Not enough bytes")
          (let ([header
                 (mach-o-header
                   (bytes-ref/quad-le header-bytes 0)
                   (bytes-ref/quad-le header-bytes 4)
                   (bytes-ref/quad-le header-bytes 8)
                   (bytes-ref/quad-le header-bytes 12)
                   (bytes-ref/quad-le header-bytes 16)
                   (bytes-ref/quad-le header-bytes 20)
                   (bytes-ref/quad-le header-bytes 24)
                   (bytes-ref/quad-le header-bytes 28))])
            (right (mach-o-file header (make-bytes 0))))))))
