(module library-compiler-main
  (import
    (compiler compile-main-module parse-module-file)
    (either
      #:types (Either)
      #:values (left right)
      #:patterns (left right))
    (io call-with-input-file call-with-output-file read-all-bytes write-all-bytes write-line)
    (list
      #:types (List)
      #:values (array->list cons empty)
      #:patterns (cons empty))
    (mach-o-writer2 write-mach-o-library/fragments)
    (module-signature
      #:types (ModuleSignature)
      #:values ()
      #:patterns ())
    (module-signature-serialization deserialize-module-signature serialize-module-signature)
    (prim
      #:types (Array Bytes InputPort Int OutputPort)
      #:values ()
      #:patterns ())
    (tuples
      #:types ()
      #:values ()
      #:patterns (tuple2))
    (x86-64-stack-machine convert-objects))
  (export)
  (types)

  (define (parse-signature-files [files : (List Bytes)]) : (Either Bytes (List ModuleSignature))
    (case files
      [(empty) (right (empty))]
      [(cons name files)
       (case (deserialize-module-signature (call-with-input-file name read-all-bytes))
         [(left v) (left v)]
         [(right sig)
          (case (parse-signature-files files)
            [(left v) (left v)]
            [(right sigs) (right (cons sig sigs))])])]))

  (define (main [args : (Array Bytes)] [stdin : InputPort] [stdout : OutputPort] [stderr : OutputPort]) : Int
    (case (array->list args)
      [(empty)
       (begin
         (write-line #"No executable specified???" stderr)
         1)]
      [(cons _ (empty))
       (begin
         (write-line #"No output object file name supplied." stderr)
         1)]
      [(cons _ (cons _ (empty)))
       (begin
         (write-line #"No output signature file name supplied." stderr)
         1)]
      [(cons _ (cons _ (cons _ (empty))))
       (begin
         (write-line #"No main-moudle file name supplied." stderr)
         1)]
      [(cons _ (cons output-object-file (cons output-signature-file (cons main-module-file sig-files))))
       (case (parse-module-file main-module-file)
         [(left v)
          (begin
            (write-line v stderr)
            1)]
         [(right main-module)
          (case (parse-signature-files sig-files)
            [(left v)
             (begin
               (write-line v stderr)
               1)]
            [(right signatures)
             (case (compile-main-module main-module signatures)
               [(left v)
                (begin
                  (write-line v stderr)
                  1)]
               [(right (tuple2 objects sig))
                (case (write-mach-o-library/fragments output-object-file (convert-objects objects))
                  [(left v)
                   (begin
                     (write-line v stderr)
                     1)]
                  [(right _)
                   (begin
                     (call-with-output-file output-signature-file
                       (lambda ([output-port : OutputPort])
                         (write-all-bytes (serialize-module-signature sig) output-port)))
                     0)])])])])])))
