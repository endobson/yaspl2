#:module (os gen-msabi-main)
#:import {
  (bytes)
  (either)
  (io)
  (list)
  (machine-code-writer)
  (maybe)
  (module-signature-serialization)
  (os msabi)
  (prim)
  (system-abi)
  (tuples)
  (yaspl x86-64-assembly)
}
(export
  (#:values main))
(types
  (define-type Args
    (args
      [abi : SystemAbi]
      [output-file : Bytes]
      [signature-file : Bytes])))


(define (parse-args [input : (List Bytes)]) : (Either Bytes Args)
  (case input
    [(empty)
     (left #"No executable specified???")]
    [(cons _ (empty))
     (left #"No abi supplied")]
    [(cons _ (cons _ (empty)))
     (left #"No output file name supplied")]
    [(cons _ (cons _ (cons _ (empty))))
     (left #"No signature file name supplied")]
    [(cons _ (cons _ (cons _ (cons _ (cons _ _)))))
     (left #"Too many arguments.")]
    [(cons _ (cons abi (cons output-file (cons signature-file (empty)))))
     (case (parse-system-abi abi)
       [(nothing) (left #"Unknown output format")]
       [(just abi)
        (right (args abi output-file signature-file))])]))


(define (run [input : (List Bytes)]) : (Maybe Bytes)
  (case (parse-args input)
    [(left v) (just v)]
    [(right (args abi output-file sig-file))
     (match-define
       (tuple2 (assembly-fragments text-fragments const-fragments zero-data-fragments)
               signature)
       (make-assembly-fragments/signature))
     (match-define fragments
       (assembly-fragments text-fragments const-fragments zero-data-fragments))
     (begin
       (write-library-object-file fragments abi output-file)
       (call-with-output-file/void sig-file
         (lambda ([p : OutputPort])
           (write-all-bytes (serialize-module-signature signature) p)))
       (nothing))]))


(define (main [args : Bytes] [stdin : InputPort] [stdout : OutputPort] [stderr : OutputPort]) : Int
  (case (run (args->list args))
    [(just v)
     (begin
       (write-all-bytes v stderr)
       1)]
    [(nothing)
     0]))
