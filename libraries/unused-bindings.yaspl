(module unused-imports
  (import
    (prim
      #:types (Bytes)
      #:values ()
      #:patterns ())
    (bytes bytes=?)
    (source-language
      #:types (Module)
      #:values ()
      #:patterns (module))
    (validator
      #:types ()
      #:values (free-variables/definitions free-patterns/definitions free-types/module
                module-imported-pattern-environment module-imported-environment
                module-imported-type-environment)
      #:patterns ())
    (multi-set multi-set-keys)
    (set make-set set-remove-all set->list)
    (list
      #:types (List)
      #:values ()
      #:patterns ()))
  (export
    #:types (UnusedBindings)
    #:values (module-unused-bindings)
    #:patterns (unused-bindings))
  (types
    (define-type UnusedBindings
      (unused-bindings
        [type-imports (List Bytes)]
        [value-imports (List Bytes)]
        [pattern-imports (List Bytes)])))


  (define (module-unused-bindings [mod : Module]) : UnusedBindings
    (case mod
      [(module name imports exports types defs)
       (unused-bindings
         (let ([env (multi-set-keys (module-imported-type-environment mod))])
           (let ([used-types (free-types/module mod (make-set bytes=?) (make-set bytes=?))])
             (set->list (set-remove-all env (set->list used-types)))))
         (let ([env (multi-set-keys (module-imported-environment mod))])
           (let ([used-vars (free-variables/definitions defs (make-set bytes=?) (make-set bytes=?)) ])
             (set->list (set-remove-all env (set->list used-vars)))))
         (let ([env (multi-set-keys (module-imported-pattern-environment mod))])
           (let ([used-patterns (free-patterns/definitions defs (make-set bytes=?) (make-set bytes=?)) ])
             (set->list (set-remove-all env (set->list used-patterns))))))])))
