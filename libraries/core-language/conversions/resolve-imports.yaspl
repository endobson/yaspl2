#:module (core-language conversions resolve-imports)
#:import {
  (bytes)
  (core-language languages resolved-imports-language)
  {(core-language source-language)
    {#:types
      [Definition s:Definition]
      [Module s:Module]
    }
    {#:patterns
      [external-definition s:external-definition]
      [function-definition s:function-definition]
      [module s:module]
    }
  }
  (dict)
  (list)
  (maybe)
  (prim)
  (tuples)
}
(export
  (#:types)
  (#:values resolve-imports)
  (#:patterns))
(types)



(define (resolve-imports [mod : s:Module]) : Module
  (match-define (s:module mod-name definitions) mod)

  (match-define imports : (Dict Bytes Import)
    (dict-add-all
      (make-dict bytes-cmp)
      (filter-map
        (lambda ([def : s:Definition]) : (Maybe (Tuple2 Bytes Import))
          (case def
            [(s:function-definition _ _ _) (nothing)]
            [(s:external-definition name)
             (just (tuple2 name (import-external name)))]))
        definitions)))

  (match-define functions : (List FunctionDefinition)
    (filter-map
      (lambda ([def : s:Definition]): (Maybe FunctionDefinition)
        (case def
          [(s:external-definition _) (nothing)]
          [(s:function-definition name params body)
           (just (function-definition name params body))]))
      definitions))

  (module mod-name imports functions))
