#:module (core-language conversions type-check)
#:import {
  (bytes)
  (core-language languages resolved-types-language)
  (dict)
  (either)
  (list)
  (maybe)
  (prim)
  (prim-types)
  (set)
  (sexp-printer)
  (tuples)
  (types)
  (yaspl math-operations)
}
(export
  (#:types)
  (#:values type-check)
  (#:patterns))
(types
  (define-type GlobalEnvironment
    (global-environment))
  (define-type Environment
    (environment
      [return-type : Type]
      [var-types : (Dict Bytes Type)])))

(define (type-check [mod : Module]) : (Either Bytes Module)
  (match-define (module name imports functions) mod)

  (match-define function-names
    (set-add-all (make-set bytes-cmp) (map function-definition-name functions)))

  (if (not (= (set-count function-names) (length functions)))
      (left #"Duplicate function name")
      (case (map/failure/env type-check/function functions (global-environment))
        [(left v) (left v)]
        [(right functions)
         (right (module name imports functions))])))



(define (type-check/function [f : FunctionDefinition] [genv : GlobalEnvironment])
  : (Either Bytes FunctionDefinition)
  (match-define (function-definition name parameters return-type body) f)

  (match-define initial-var-types : (Dict Bytes Type)
    (dict-add-all
      (make-dict bytes-cmp)
      (map
        (lambda ([p : Parameter])
          (match-define (parameter name type) p)
          (tuple2 name type))
        parameters)))

  (match-define env (environment return-type initial-var-types))

  (case (type-check/statements body env)
    [(left v) (left v)]
    [(right body)
     (right (function-definition name parameters return-type body))]))


(define (type-check/statements [ss : (List Statement)] [env : Environment])
  : (Either Bytes (List Statement))
  (case ss
    [(empty) (right (empty))]
    [(cons s ss)
     (case (type-check/statement s env)
       [(left v) (left v)]
       [(right (tuple2 s env))
        (case (type-check/statements ss env)
          [(left v) (left v)]
          [(right ss)
           (right (cons s ss))])])]))

(define (type-check/statement [s : Statement] [env : Environment])
  : (Either Bytes (Tuple2 Statement Environment))
  (case s
    [(return-statement e)
     (case (type-check/expression e (environment-return-type env) env)
       [(left v) (left v)]
       [(right e)
        (right (tuple2 (return-statement e) env))])]
    [(define-local-variable-statement v type e)
     (case (type-check/expression e type env)
       [(left v) (left v)]
       [(right e)
        (match-define env (environment-add-local env v type))
        (right (tuple2 (define-local-variable-statement v type e) env))])]
    [(assignment-statement v e)
     (case (dict-ref (environment-var-types env) v)
       [(nothing)
        (left #"Cannot assign to unbound variable.")]
       [(just ty)
        (case (type-check/expression e ty env)
          [(left v) (left v)]
          [(right e)
           (right (tuple2 (assignment-statement v e) env))])])]
    [(effect-statement e)
     (case (type-infer/expression e env)
       [(left v) (left v)]
       [(right (tuple2 e _))
        (right (tuple2 (effect-statement e) env))])]
    [(pointer-assignment-statement p e)
     (case (type-infer/expression p env)
       [(left v) (left v)]
       [(right (tuple2 p p-ty))
        (case (pointer-type->element-type p-ty)
          [(nothing)
           (left (bytes-append
                   (varargs list
                     #"Expected Pointer Type, got: "
                     (print-sexp (type->sexp p-ty)))))]
          [(just e-ty)
           (case (type-check/expression e e-ty env)
             [(left v) (left v)]
             [(right e)
              (right (tuple2 (pointer-assignment-statement p e) env))])])])]
    [(while-statement e body)
     (case (type-check/expression e (boolean-type) env)
       [(left v) (left v)]
       [(right e)
        (case (type-check/statements body env)
          [(left v) (left v)]
          [(right body)
           (right (tuple2 (while-statement e body) env))])])]
    [(if-statement c t f)
     (case (type-check/expression c (boolean-type) env)
       [(left v) (left v)]
       [(right c)
        (case (type-check/statements t env)
          [(left v) (left v)]
          [(right t)
           (case (type-check/statements f env)
             [(left v) (left v)]
             [(right f)
              (right (tuple2 (if-statement c t f) env))])])])]))

(define (type-check/expression [e : Expression] [expected-type : Type] [env : Environment])
  : (Either Bytes Expression)
  (case (type-infer/expression e env)
    [(left v) (left v)]
    [(right (tuple2 e actual-type))
     (if (type=? actual-type expected-type)
         (right e)
         (left
           (bytes-append
             (varargs list
               #"Types don't match:\nGot: "
               (print-sexp (type->sexp actual-type))
               #"\nExpected: "
               (print-sexp (type->sexp expected-type))))))]))

(define (type-infer/expression [e : Expression] [env : Environment])
  : (Either Bytes (Tuple2 Expression Type))
  (case e
    [(int-expr _)
     (right (tuple2 e (u64-type)))]
    [(bytes-expr _)
     (right (tuple2 e (bytes-type)))]
    [(cast-expr t e)
     (case (type-infer/expression e env)
       [(left v) (left v)]
       [(right (tuple2 e _))
        (right (tuple2 e t))])]
    [(array-index-expr a i)
     (case (type-infer/expression a env)
       [(left v) (left v)]
       [(right (tuple2 a pa-ty))
        (case (pointer-type->element-type pa-ty)
          [(nothing)
           (left (bytes-append
                   (varargs list
                     #"Expected Pointer Type, got: "
                     (print-sexp (type->sexp pa-ty)))))]
          [(just a-ty)
           (case (array-type->element-type a-ty)
             [(nothing)
              (left (bytes-append
                      (varargs list
                        #"Expected Array Type, got: "
                        (print-sexp (type->sexp a-ty)))))]
             [(just elem-ty)
              (case (type-check/expression i (u64-type) env)
                [(left v) (left v)]
                [(right i)
                 (right (tuple2 (array-index-expr a i) (pointer-type elem-ty)))])])])])]
    [(var-expr v)
     (case (dict-ref (environment-var-types env) v)
       [(nothing)
        ;; TODO make this an error
        (right (tuple2 e (u64-type)))]
       [(just ty)
        (right (tuple2 e ty))])]
    [(bin-op-expr (comparison-bin-op op) l r)
     (case (type-check/expression l (u64-type) env)
       [(left v) (left v)]
       [(right l)
        (case (type-check/expression r (u64-type) env)
          [(left v) (left v)]
          [(right r)
           (right (tuple2 (bin-op-expr (comparison-bin-op op) l r) (boolean-type)))])])]
    [(bin-op-expr (logical-bin-op op) l r)
     (case (type-check/expression l (boolean-type) env)
       [(left v) (left v)]
       [(right l)
        (case (type-check/expression r (boolean-type) env)
          [(left v) (left v)]
          [(right r)
           (right (tuple2 (bin-op-expr (logical-bin-op op) l r) (boolean-type)))])])]

    [_
     ;; TODO handle other expressions
     (right (tuple2 e (u64-type)))]))

(define (environment-add-local [env : Environment] [v : Bytes] [ty : Type])
  : Environment
  (match-define (environment return-type vars) env)
  (environment return-type (dict-set vars v ty)))
