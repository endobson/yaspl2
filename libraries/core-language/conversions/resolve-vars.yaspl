#:module (core-language conversions resolve-vars)
#:import {
  (bytes)
  {(core-language languages resolved-types-language)
    {#:types
      [Expression rt:Expression]
      [FunctionDefinition rt:FunctionDefinition]
      [Import rt:Import]
      [Module rt:Module]
      [Parameter rt:Parameter]
      [Statement rt:Statement]
    }
    {#:values
      [parameter-type rt:parameter-type]
    }
    {#:patterns
      [array-index-expr rt:array-index-expr]
      [assignment-statement rt:assignment-statement]
      [bin-op-expr rt:bin-op-expr]
      [bytes-expr rt:bytes-expr]
      [call-expr rt:call-expr]
      [cast-expr rt:cast-expr]
      [define-local-variable-statement rt:define-local-variable-statement]
      [deref-expr rt:deref-expr]
      [effect-statement rt:effect-statement]
      [function-definition rt:function-definition]
      [if-statement rt:if-statement]
      [import-external rt:import-external]
      [int-expr rt:int-expr]
      [module rt:module]
      [parameter rt:parameter]
      [pointer-assignment-statement rt:pointer-assignment-statement]
      [return-statement rt:return-statement]
      [var-expr rt:var-expr]
      [while-statement rt:while-statement]
    }
  }
  (core-language languages resolved-vars-language)
  (dict)
  (list)
  (maybe)
  (module-name)
  (prim)
  (top-level-name)
  (tuples)
  (types)
  (yaspl var)
}
(export
  (#:types)
  (#:values resolve-vars)
  (#:patterns))
(types
  (define-type NamedFunction
    (named-function
      [func-name : TopLevelName]
      [closure-name : TopLevelName]
      [params : (List rt:Parameter)]
      [return-type : Type]
      [body : (List rt:Statement)]))


  (define-type Environment
    (environment
      [local-variables : (Dict Bytes Var)]
      [global-variables : (Dict Bytes Global)]
      [module-name : ModName]))

  (define-type State
    (state [counter : VarNumber]))
  (define-type Global
    (global/top-level [v : TopLevelName])
    (global/external [v : Bytes])))

(define (resolve-vars [mod : rt:Module]) : Module
  (match-define (rt:module mod-name imports functions) mod)

  (match-define named-functions : (Dict Bytes NamedFunction)
    (dict-add-all
      (make-dict bytes-cmp)
      (map
        (lambda ([f : rt:FunctionDefinition])
          (match-define (rt:function-definition name params return-type statements) f)
          (tuple2
            name
            (named-function
              (top-level-name mod-name name)
              (top-level-name
                mod-name
                (bytes-append (varargs list name #"_closure")))
              params
              return-type
              statements)))
        functions)))

  (match-define imported-globals : (Dict Bytes Global)
    (dict-value-map imports
      (lambda ([i : rt:Import])
        (match-define (rt:import-external ext) i)
        (global/external ext))))
  (match-define defined-globals : (Dict Bytes Global)
    (dict-value-map
      named-functions
      (lambda ([f : NamedFunction])
        (match-define (named-function _ closure _ _ _) f)
        (global/top-level closure))))
  (match-define globals (merge-dicts imported-globals defined-globals))

  (match-define initial-env
    (environment
      (make-dict bytes-cmp)
      globals
      mod-name))

  (match-define functions
    (dict-map
      named-functions
      (lambda ([_ : Bytes] [f : NamedFunction])
        (resolve-vars/function f initial-env))))

  (match-define exports : (Dict Bytes Export)
    (dict-value-map
      named-functions
      (lambda ([f : NamedFunction])
        (match-define (named-function _ closure params return-type _) f)
        (export closure (fun-type (empty) (map rt:parameter-type params) return-type)))))

  (module mod-name exports functions))

(define (resolve-vars/function [f : NamedFunction] [env : Environment])
  : FunctionDefinition
  (match-define (named-function func-name closure-name params _return-type statements) f)

  (match-define state (state (initial-var-number)))
  (match-define (tuple3 params env state)
    (resolve-vars/parameters params env state))
  (match-define (tuple3 statements _env _state)
    (resolve-vars/statements statements env state))

  (function-definition func-name closure-name params statements))

(define (resolve-vars/parameters
          [params : (List rt:Parameter)]
          [env : Environment]
          [state : State])
  : (Tuple3 (List Parameter) Environment State)
  (map/env/state resolve-vars/parameter params env state))

(define (resolve-vars/parameter
          [param : rt:Parameter]
          [env : Environment]
          [state : State])
  : (Tuple3 Parameter Environment State)
  (match-define (rt:parameter name type) param)
  (match-define (tuple3 var env state) (environment-add-local env name state))
  (tuple3 (parameter var type) env state))

(define (resolve-vars/statements
          [ss : (List rt:Statement)]
          [env : Environment]
          [state : State])
  : (Tuple3 (List Statement) Environment State)
  (map/env/state resolve-vars/statement ss env state))

(define (resolve-vars/statement
          [s : rt:Statement]
          [env : Environment]
          [state : State])
  : (Tuple3 Statement Environment State)
  (case s
    [(rt:define-local-variable-statement v t e)
     (match-define e (resolve-vars/expression e env))
     (match-define (tuple3 v env state) (environment-add-local env v state))
     (tuple3 (define-local-variable-statement v t e) env state)]
    [(rt:return-statement e)
     (match-define e (resolve-vars/expression e env))
     (tuple3 (return-statement e) env state)]
    [(rt:effect-statement e)
     (match-define e (resolve-vars/expression e env))
     (tuple3 (effect-statement e) env state)]
    [(rt:assignment-statement v e)
     (match-define v (environment-ref/local env v))
     (match-define e (resolve-vars/expression e env))
     (tuple3 (assignment-statement v e) env state)]
    [(rt:pointer-assignment-statement p e)
     (match-define p (resolve-vars/expression p env))
     (match-define e (resolve-vars/expression e env))
     (tuple3 (pointer-assignment-statement p e) env state)]
    [(rt:while-statement e body)
     (match-define e (resolve-vars/expression e env))
     (match-define (tuple3 body _ state) (resolve-vars/statements body env state))
     (tuple3 (while-statement e body) env state)]
    [(rt:if-statement c t f)
     (match-define c (resolve-vars/expression c env))
     (match-define (tuple3 t _ state) (resolve-vars/statements t env state))
     (match-define (tuple3 f _ state) (resolve-vars/statements f env state))
     (tuple3 (if-statement c t f) env state)]))


(define (resolve-vars/expressions
          [es : (List rt:Expression)]
          [env : Environment])
  : (List Expression)
  (map (lambda ([e : rt:Expression]) (resolve-vars/expression e env)) es))

(define (resolve-vars/expression
          [e : rt:Expression]
          [env : Environment])
  : Expression
  (case e
    [(rt:var-expr v)
     (resolve-var v env)]
    ;; Recur structually
    [(rt:int-expr v)
     (int-expr v)]
    [(rt:bytes-expr v)
     (bytes-expr v)]
    [(rt:deref-expr e)
     (deref-expr
       (resolve-vars/expression e env))]
    [(rt:array-index-expr e i)
     (array-index-expr
       (resolve-vars/expression e env)
       (resolve-vars/expression i env))]
    [(rt:bin-op-expr op l r)
     (bin-op-expr
       op
       (resolve-vars/expression l env)
       (resolve-vars/expression r env))]
    [(rt:cast-expr t e)
     (cast-expr
       t
       (resolve-vars/expression e env))]
    [(rt:call-expr f args)
     (call-expr
       (resolve-vars/expression f env)
       (resolve-vars/expressions args env))]))

(define (resolve-var [v : Bytes] [env : Environment]) : Expression
  (match-define (environment locals globals _) env)
  (case (dict-ref locals v)
    [(just v) (local-var-expr v)]
    [(nothing)
     (case (dict-ref globals v)
       [(just (global/top-level v)) (global-var-expr v)]
       [(just (global/external v)) (external-var-expr v)]
       [(nothing) (panic #"Unbound variable")])]))

(define (get-next-index [acc : State]) : (Tuple2 VarNumber State)
  (case acc
    [(state counter)
     (tuple2 counter (state (next-var-number counter)))]))

(define (environment-add-local [env : Environment] [local : Bytes] [state : State])
  : (Tuple3 Var Environment State)
  (match-define (environment locals globals name) env)
  (match-define (tuple2 var-num state) (get-next-index state))
  (match-define v (var local var-num))
  (tuple3
    v
    (environment (dict-set locals local v) globals name)
    state))

(define (environment-ref/local [env : Environment] [name : Bytes]) : Var
  (match-define (environment locals _ _) env)
  (case (dict-ref locals name)
    [(nothing) (panic #"Not a local")]
    [(just v) v]))




(define [A B] (map/env/state
                [f : (A Environment State -> (Tuple3 B Environment State))]
                [l : (List A)]
                [env : Environment]
                [state : State])
  : (Tuple3 (List B) Environment State)
  (case l
    [(empty) (tuple3 (empty) env state)]
    [(cons a l)
     (match-define (tuple3 b env state) (f a env state))
     (match-define (tuple3 bs env state) (map/env/state f l env state))
     (tuple3 (cons b bs) env state)]))
