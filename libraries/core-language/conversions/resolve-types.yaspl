#:module (core-language conversions resolve-types)
#:import {
  (bytes)
  {(core-language languages resolved-imports-language)
    {#:types
      [Expression ri:Expression]
      [FunctionDefinition ri:FunctionDefinition]
      [GlobalDefinition ri:GlobalDefinition]
      [Import ri:Import]
      [Module ri:Module]
      [Parameter ri:Parameter]
      PreSpaceType
      PreType
      [Statement ri:Statement]
      [StructDefinition ri:StructDefinition]
      [StructField ri:StructField]
    }
    {#:patterns
      [array-index-expr ri:array-index-expr]
      [array-pre-type ri:array-pre-type]
      [assignment-statement ri:assignment-statement]
      [bin-op-expr ri:bin-op-expr]
      [bytes-expr ri:bytes-expr]
      [call-expr ri:call-expr]
      [cast-expr ri:cast-expr]
      [define-local-variable-statement ri:define-local-variable-statement]
      [deref-expr ri:deref-expr]
      [effect-statement ri:effect-statement]
      [fixed-array-pre-space-type ri:fixed-array-pre-space-type]
      [function-definition ri:function-definition]
      [function-pre-type ri:function-pre-type]
      [global-definition ri:global-definition]
      [if-statement ri:if-statement]
      [import-external ri:import-external]
      [import-value ri:import-value]
      [int-expr ri:int-expr]
      [module ri:module]
      [parameter ri:parameter]
      [pointer-assignment-statement ri:pointer-assignment-statement]
      [pointer-pre-type ri:pointer-pre-type]
      [raw-pre-space-type ri:raw-pre-space-type]
      [return-statement ri:return-statement]
      [struct-definition ri:struct-definition]
      [struct-field ri:struct-field]
      [struct-field-expr ri:struct-field-expr]
      [unary-op-expr ri:unary-op-expr]
      [var-expr ri:var-expr]
      [var-pre-type ri:var-pre-type]
      [while-statement ri:while-statement]
    }
  }
  (core-language languages resolved-types-language)
  (dict)
  (either)
  (list)
  (maybe)
  (module-signature)
  (prim)
  (prim-types)
  (tuples)
  (types)
}
(export
  (#:types)
  (#:values resolve-types)
  (#:patterns))
(types
  (define-type Environment
    (environment
      [types : (Dict Bytes Type)])))

(define (resolve-types [mod : ri:Module]) : (Either Bytes Module)
  (match-define (ri:module mod-name imports exports structs globals functions) mod)

  (match-define struct-types : (Dict Bytes Type)
    (dict-add-all
      (make-dict bytes-cmp)
      (map
        (lambda ([s : ri:StructDefinition])
          (match-define (ri:struct-definition name _) s)
          (tuple2 name (abstract-type mod-name name (empty) (memory-kind))))
        structs)))

  (match-define env : Environment
    (environment
      (dict-add-all
        struct-types
        (varargs list
          (tuple2 #"U8" (u8-type))
          (tuple2 #"U16" (u16-type))
          (tuple2 #"U32" (u32-type))
          (tuple2 #"U64" (u64-type))
          (tuple2 #"Boolean" (boolean-type))
          (tuple2 #"Bytes" (bytes-type))))))

  (case (resolve-types/imports imports env)
    [(left v) (left v)]
    [(right imports)
     (case (map/failure/env resolve-types/struct structs env)
       [(left v) (left v)]
       [(right structs)
        (case (map/failure/env resolve-types/global globals env)
          [(left v) (left v)]
          [(right globals)
           (case (map/failure/env resolve-types/function functions env)
             [(left v) (left v)]
             [(right functions)
              (right (module mod-name imports exports structs globals functions))])])])]))

(define (resolve-types/imports [is : (Dict Bytes ri:Import)] [env : Environment])
  : (Either Bytes (Dict Bytes Import))
  (dict-fold
    is
    (lambda ([name : Bytes] [i : ri:Import] [acc : (Either Bytes (Dict Bytes Import))])
      : (Either Bytes (Dict Bytes Import))
      (case acc
        [(left v) (left v)]
        [(right acc)
         (case i
           [(ri:import-external sym t)
            (case (pre-type->type t env)
              [(left v) (left v)]
              [(right t)
               (right (dict-add acc name (import-external sym t)))])]
           [(ri:import-value (value-signature ty v))
            (right (dict-add acc name (import-value v ty)))])]))
    (ann (Either Bytes (Dict Bytes Import))
      (right (make-dict bytes-cmp)))))

(define (resolve-types/struct [s : ri:StructDefinition] [env : Environment])
  : (Either Bytes StructDefinition)
  (match-define (ri:struct-definition name fields) s)
  (match-define struct-type (dict-ref/panic (environment-types env) name))
  (case (map/failure/env resolve-types/struct-field fields env)
    [(left v) (left v)]
    [(right fields)
     (right (struct-definition struct-type fields))]))

(define (resolve-types/struct-field [s : ri:StructField] [env : Environment])
  : (Either Bytes StructField)
  (match-define (ri:struct-field name pre-type) s)
  (case (pre-space-type->type pre-type env)
    [(left v) (left v)]
    [(right ty)
     (right (struct-field name ty))]))

(define (resolve-types/global [s : ri:GlobalDefinition] [env : Environment])
  : (Either Bytes GlobalDefinition)
  (match-define (ri:global-definition name pre-type alignment) s)
  (case (pre-space-type->type pre-type env)
    [(left v) (left v)]
    [(right ty)
     (right (global-definition name ty alignment))]))

(define (resolve-types/function [f : ri:FunctionDefinition] [env : Environment])
  : (Either Bytes FunctionDefinition)
  (match-define (ri:function-definition name params return-type statements) f)
  (case (map/failure/env resolve-types/parameter params env)
    [(left v) (left v)]
    [(right params)
     (case (pre-type->type return-type env)
       [(left v) (left v)]
       [(right return-type)
        (case (map/failure/env resolve-types/statement statements env)
          [(left v) (left v)]
          [(right statements)
           (right (function-definition name params return-type statements))])])]))

(define (resolve-types/parameter [param : ri:Parameter] [env : Environment])
  : (Either Bytes Parameter)
  (match-define (ri:parameter name pre-type) param)
  (case (pre-type->type pre-type env)
    [(left v) (left v)]
    [(right t)
     (right (parameter name t))]))

(define (resolve-types/statement [s : ri:Statement] [env : Environment])
  : (Either Bytes Statement)
  (case s
    [(ri:define-local-variable-statement v t e)
     (case (pre-type->type t env)
       [(left v) (left v)]
       [(right t)
        (case (resolve-types/expression e env)
          [(left v) (left v)]
          [(right e)
           (right (define-local-variable-statement v t e))])])]
    [(ri:return-statement e)
     (case (resolve-types/expression e env)
       [(left v) (left v)]
       [(right e)
        (right (return-statement e))])]
    [(ri:effect-statement e)
     (case (resolve-types/expression e env)
       [(left v) (left v)]
       [(right e)
        (right (effect-statement e))])]
    [(ri:assignment-statement v e)
     (case (resolve-types/expression e env)
       [(left v) (left v)]
       [(right e)
        (right (assignment-statement v e))])]
    [(ri:pointer-assignment-statement p e)
     (case (resolve-types/expression p env)
       [(left v) (left v)]
       [(right p)
        (case (resolve-types/expression e env)
          [(left v) (left v)]
          [(right e)
           (right (pointer-assignment-statement p e))])])]
    [(ri:while-statement e body)
     (case (resolve-types/expression e env)
       [(left v) (left v)]
       [(right e)
        (case (map/failure/env resolve-types/statement body env)
          [(left v) (left v)]
          [(right body)
           (right (while-statement e body))])])]
    [(ri:if-statement c t f)
     (case (resolve-types/expression c env)
       [(left v) (left v)]
       [(right c)
        (case (map/failure/env resolve-types/statement t env)
          [(left v) (left v)]
          [(right t)
           (case (map/failure/env resolve-types/statement f env)
             [(left v) (left v)]
             [(right f)
              (right (if-statement c t f))])])])]))

(define (resolve-types/expression
          [e : ri:Expression]
          [env : Environment])
  : (Either Bytes Expression)
  (case e
    [(ri:cast-expr t e)
     (case (pre-type->type t env)
       [(left v) (left v)]
       [(right t)
        (case (resolve-types/expression e env)
          [(left v) (left v)]
          [(right e)
           (right (cast-expr t e))])])]
    ;; Recur structually
    [(ri:var-expr v)
     (right (var-expr v))]
    [(ri:int-expr v)
     (right (int-expr v))]
    [(ri:bytes-expr v)
     (right (bytes-expr v))]
    [(ri:deref-expr e)
     (case (resolve-types/expression e env)
       [(left v) (left v)]
       [(right e)
        (right (deref-expr e))])]
    [(ri:array-index-expr e i)
     (case (resolve-types/expression e env)
       [(left v) (left v)]
       [(right e)
        (case (resolve-types/expression i env)
          [(left v) (left v)]
          [(right i)
           (right (array-index-expr e i))])])]
    [(ri:struct-field-expr e f)
     (case (resolve-types/expression e env)
       [(left v) (left v)]
       [(right e)
        (right (struct-field-expr e f))])]
    [(ri:unary-op-expr op e)
     (case (resolve-types/expression e env)
       [(left v) (left v)]
       [(right e)
        (right (unary-op-expr op e))])]
    [(ri:bin-op-expr op l r)
     (case (resolve-types/expression l env)
       [(left v) (left v)]
       [(right l)
        (case (resolve-types/expression r env)
          [(left v) (left v)]
          [(right r)
           (right (bin-op-expr op l r))])])]
    [(ri:call-expr f args)
     (case (resolve-types/expression f env)
       [(left v) (left v)]
       [(right f)
        (case (map/failure/env resolve-types/expression args env)
          [(left v) (left v)]
          [(right args)
           (right (call-expr f args))])])]))

(define (pre-space-type->type [t : PreSpaceType] [env : Environment]) : (Either Bytes SpaceType)
  (case t
    [(ri:raw-pre-space-type t)
     (case (pre-type->type t env)
       [(left v) (left v)]
       [(right t)
        (right (raw-space-type t))])]
    [(ri:fixed-array-pre-space-type t len)
     (case (pre-type->type t env)
       [(left v) (left v)]
       [(right t)
        (right (fixed-array-space-type t len))])]))

(define (pre-type->type [t : PreType] [env : Environment]) : (Either Bytes Type)
  (case t
    [(ri:var-pre-type v)
     (case (dict-ref (environment-types env) v)
       [(nothing) (left (bytes-append (varargs list #"Unbound type: " v)))]
       [(just t) (right t)])]
    [(ri:function-pre-type args return-type)
     (case (map/failure/env pre-type->type args env)
       [(left v) (left v)]
       [(right args)
        (case (pre-type->type return-type env)
          [(left v) (left v)]
          [(right return-type)
           (right (fun-type (empty) args return-type))])])]
    [(ri:array-pre-type t)
     (case (pre-type->type t env)
       [(left v) (left v)]
       [(right t) (right (array-type t))])]
    [(ri:pointer-pre-type t)
     (case (pre-type->type t env)
       [(left v) (left v)]
       [(right t) (right (pointer-type t))])]))
