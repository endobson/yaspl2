#:module (core-language conversions process-structs)
#:import {
  (bytes)
  (core-language languages processed-structs-language)
  {(core-language languages resolved-types-language)
    {#:types
      [Module rt:Module]
      [StructDefinition rt:StructDefinition]
      [StructField rt:StructField]
    }
    {#:patterns
      [module rt:module]
      [struct-definition rt:struct-definition]
      [struct-field rt:struct-field]
    }
  }
  (core-language struct-info)
  (data lifted-primitives)
  (dict)
  (either)
  (list)
  (maybe)
  (prim)
  (tuples)
  (types)
}
(export
  (#:types)
  (#:values process-structs)
  (#:patterns))
(types)

(define (process-structs [mod : rt:Module]) : (Either Bytes Module)
  (match-define (rt:module mod-name imports structs functions) mod)

  (case (definitions->destruction-infos structs (make-dict type-cmp))
    [(left v) (left v)]
    [(right structs)
     (right (module mod-name imports structs functions))]))

(define (definitions->destruction-infos
          [defs : (List rt:StructDefinition)]
          [acc : (Dict Type StructDestructionInfo)])
  : (Either Bytes (Dict Type StructDestructionInfo))
  (case defs
    [(empty) (right acc)]
    [(cons def defs)
     (case (definition->destruction-info def)
       [(left v) (left v)]
       [(right (tuple2 ty info))
        (definitions->destruction-infos defs (dict-add acc ty info))])]))


(define (definition->destruction-info [def : rt:StructDefinition])
  : (Either Bytes (Tuple2 Type StructDestructionInfo))
  (match-define (rt:struct-definition type field-list) def)
  (case (process-fields field-list 0 (make-dict bytes-cmp))
    [(left v) (left v)]
    [(right field-dict)
     (right (tuple2 type (struct-destruction-info field-dict)))]))

(define (process-fields [fields : (List rt:StructField)]
                        [offset : Int]
                        [acc : (Dict Bytes StructField)])
  : (Either Bytes (Dict Bytes StructField))
  (case fields
    [(empty) (right acc)]
    [(cons (rt:struct-field name type) fields)
     (case (dict-maybe-add acc name (struct-field offset type))
       [(nothing) (left #"Duplicate field")]
       [(just acc)
        (case (type->bytes-size type)
          [(nothing) (left #"Struct field must be of sized type")]
          [(just (lifted-int size))
           (process-fields fields (+ offset size) acc)])])]))
