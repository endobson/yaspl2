#:module (type-unification)
#:import {
  (bytes)
  (data lifted-primitives)
  (data ordering)
  (dict)
  (either)
  (list)
  (maybe)
  (module-name)
  (numbers)
  (prim)
  (set)
  (sexp-printer)
  (tuples)
  (types)
}
(export
  (#:types UnificationVar UnificationVarSource Constraint Constrainable)
  (#:values unify uvar constrain-uvar constrain-var-type constrain-abstract-type
            constrain-fun-type eq-constraint star-kind-constraint uvar->bytes uvar-source-next
            type->constrainable new-uvar-source constraint->bytes))
(types
  (define-type UnificationVar
    (uvar [id : Int]))
  (define-type Constrainable
    (constrain-uvar [v : UnificationVar])
    (constrain-var-type [v : Bytes])
    (constrain-abstract-type [mod-name : ModName] [name : Bytes] [args : (List Constrainable)]
                             [kind : Kind])
    (constrain-fun-type [args : (List Constrainable)] [result : Constrainable]))
  (define-type Constraint
    (star-kind-constraint [v : UnificationVar])
    (eq-constraint [lhs : Constrainable] [rhs : Constrainable]))
  (define-type TypeConstraint
    (eq-constraint/type [lhs : Constrainable] [rhs : Constrainable]))
  (define-type KindConstraint
    (star-kind-constraint/kind [v : UnificationVar]))
  (define-type UnificationVarSource
    (uvar-source [counter : Int])))

(define (unify [constraints : (List Constraint)]) : (Maybe (Dict UnificationVar Type))
  (match-define (tuple2 type-constraints kind-constraints)
    (split-constraints constraints))
  (match-define star-kind-uvars
    (set-add-all
      (make-set uvar-cmp)
      (map star-kind-constraint/kind-v kind-constraints)))

  (case (unify* type-constraints (make-dict uvar-cmp))
    [(nothing) (nothing)]
    [(just solution)
     (case (extract-rights
             (dict-map
               solution
               (lambda ([uvar : UnificationVar] [c : Constrainable])
                 : (Either LiftedVoid (Tuple2 UnificationVar Type))
                 (case (constrainable->type c)
                   [(nothing) (left (lifted-void))]
                   [(just t)
                    (if (and (set-member? star-kind-uvars uvar)
                             (not (kind=? (star-kind) (type->kind t))))
                        (left (lifted-void))
                        (right (tuple2 uvar t)))]))))
       [(left _) (nothing)]
       [(right uvar-types)
        (just (dict-add-all (make-dict uvar-cmp) uvar-types))])]))

(define (unify*/var
          [uvar : UnificationVar]
          [c : Constrainable]
          [constraints : (List TypeConstraint)]
          [solution : (Dict UnificationVar Constrainable)])
  : (Maybe (Dict UnificationVar Constrainable))
  (unify*
    (map (subst/constraint uvar c) constraints)
    (dict-add
      (dict-value-map solution (subst uvar c))
      uvar c)))

(define (unify*
          [constraints : (List TypeConstraint)]
          [solution : (Dict UnificationVar Constrainable)])
  : (Maybe (Dict UnificationVar Constrainable))
  (case constraints
    [(empty) (just solution)]
    [(cons constraint constraints)
     (case constraint
       [(eq-constraint/type (constrain-uvar v1) (constrain-uvar v2))
        (if (uvar=? v1 v2)
            (unify* constraints solution)
            (unify*/var v1 (constrain-uvar v2) constraints solution))]
       [(eq-constraint/type (constrain-uvar v1) c2)
        (if (occurs-check? v1 c2)
            (nothing)
            (unify*/var v1 c2 constraints solution))]
       [(eq-constraint/type c1 (constrain-uvar v2))
        (if (occurs-check? v2 c1)
            (nothing)
            (unify*/var v2 c1 constraints solution))]
       [(eq-constraint/type (constrain-var-type v1) (constrain-var-type v2))
        (if (bytes=? v1 v2)
            (unify* constraints solution)
            (nothing))]
       [(eq-constraint/type
          (constrain-abstract-type mod1 name1 types1 kind1)
          (constrain-abstract-type mod2 name2 types2 kind2))
        (if (and (mod-name=? mod1 mod2)
                 (and (bytes=? name1 name2) (kind=? kind1 kind2)))
            (case (zip types1 types2)
              [(nothing) (nothing)]
              [(just zipped-types)
               (unify*
                 (append
                   (map (lambda ([t : (Tuple2 Constrainable Constrainable)])
                          (case t
                            [(tuple2 lhs rhs) (eq-constraint/type lhs rhs)]))
                        zipped-types)
                   constraints)
                 solution)])
            (nothing))]
       [(eq-constraint/type (constrain-fun-type args1 res1) (constrain-fun-type args2 res2))
        (case (zip args1 args2)
          [(nothing) (nothing)]
          [(just zipped-args)
           (unify*
             (append
               (map
                 (lambda ([t : (Tuple2 Constrainable Constrainable)])
                   (case t
                     [(tuple2 lhs rhs) (eq-constraint/type lhs rhs)]))
                 (cons (tuple2 res1 res2) zipped-args))
               constraints)
             solution)])]
       [(eq-constraint/type (constrain-abstract-type _ _ _ _) (constrain-fun-type _ _))
        (nothing)]
       [(eq-constraint/type (constrain-fun-type _ _) (constrain-abstract-type _ _ _ _))
        (nothing)]
       [(eq-constraint/type (constrain-var-type _) (constrain-fun-type _ _))
        (nothing)]
       [(eq-constraint/type (constrain-fun-type _ _) (constrain-var-type _))
        (nothing)]
       [(eq-constraint/type (constrain-var-type _) (constrain-abstract-type _ _ _ _))
        (nothing)]
       [(eq-constraint/type (constrain-abstract-type _ _ _ _) (constrain-var-type _))
        (nothing)])]))


(define (subst [uvar : UnificationVar] [c : Constrainable])
  : (Constrainable -> Constrainable)
  (lambda ([c2 : Constrainable])  (subst* uvar c c2)))

(define (subst/constraint [uvar : UnificationVar] [c : Constrainable])
  : (TypeConstraint -> TypeConstraint)
  (lambda ([c2 : TypeConstraint])
    (case c2
      [(eq-constraint/type lhs rhs)
       (eq-constraint/type
         (subst* uvar c lhs)
         (subst* uvar c rhs))])))

(define (subst* [uvar : UnificationVar] [v : Constrainable] [c : Constrainable])
  : Constrainable
  (case c
    [(constrain-uvar u)
     (if (uvar=? u uvar) v c)]
    [(constrain-var-type _) c]
    [(constrain-abstract-type mod name types kind)
     (constrain-abstract-type mod name (map (subst uvar v) types) kind)]
    [(constrain-fun-type args result)
     (constrain-fun-type (map (subst uvar v) args) (subst* uvar v result))]))

(define (occurs-check? [uvar : UnificationVar] [c : Constrainable]) : Boolean
  (case c
    [(constrain-uvar u)
     (uvar=? u uvar)]
    [(constrain-var-type _) #f]
    [(constrain-abstract-type _ _ types _)
     (ormap (lambda ([c : Constrainable]) (occurs-check? uvar c)) types)]
    [(constrain-fun-type args result)
     (ormap (lambda ([c : Constrainable]) (occurs-check? uvar c)) (cons result args))]))

(define (uvar=? [uvar1 : UnificationVar] [uvar2 : UnificationVar]) : Boolean
  (case uvar1
    [(uvar v1)
     (case uvar2
       [(uvar v2)
        (= v1 v2)])]))

(define (uvar-cmp [uvar1 : UnificationVar] [uvar2 : UnificationVar]) : Ordering
  (case uvar1
    [(uvar v1)
     (case uvar2
       [(uvar v2)
        (int-cmp v1 v2)])]))

(define (uvar->bytes [uvar : UnificationVar]) : Bytes
  (bytes-append
    (varargs list #"(uvar " (integer->decimal-bytes (uvar-id uvar)) #")")))

(define (constrainable->type [c : Constrainable]) : (Maybe Type)
  (case c
    [(constrain-uvar _) (nothing)]
    [(constrain-var-type v) (just (var-type v))]
    [(constrain-abstract-type mod-name name cs kind)
     (case (extract-justs (map constrainable->type cs))
       [(nothing) (nothing)]
       [(just types)
        (just (abstract-type mod-name name types kind))])]
    [(constrain-fun-type args result)
     (case (extract-justs (map constrainable->type args))
       [(nothing) (nothing)]
       [(just arg-types)
        (case (constrainable->type result)
          [(nothing) (nothing)]
          [(just result-type)
           (just (fun-type (empty) (empty) arg-types result-type))])])]))

(define (new-uvar-source) : UnificationVarSource
  (uvar-source 0))

(define (uvar-source-next [us : UnificationVarSource]) : (Tuple2 UnificationVar UnificationVarSource)
  (let ([counter (uvar-source-counter us)])
    (tuple2 (uvar counter) (uvar-source (+ counter 1)))))

(define (type->constrainable [t : Type] [uvars : (Dict Bytes UnificationVar)]) : Constrainable
  (case t
    [(fun-type (empty) (empty) args result-type)
     (constrain-fun-type
       (map (lambda ([t : Type]) (type->constrainable t uvars)) args)
       (type->constrainable result-type uvars))]
    [(fun-type _tvars _constraints _args _result-type)
     (panic #"polymorphic functions and function constrained type constraints not yet implemented")]
    [(var-type v)
     (case (dict-ref uvars v)
       [(nothing) (constrain-var-type v)]
       [(just u) (constrain-uvar u)])]
    [(abstract-type mod name args kind)
     (constrain-abstract-type mod name
       (map (lambda ([t : Type]) (type->constrainable t uvars)) args)
       kind)]
    [(type-constructor mod name _args)
     (panic
       (bytes-append
         (varargs list #"Type constructor constraint not yet implemented: "
                       (mod-name->bytes mod) #" " name)))]))

(define (constraint->bytes [c : Constraint]) : Bytes
  (print-sexp (constraint->sexp c)))

(define (constraint->sexp [c : Constraint]) : Sexp/P
  (case c
    [(eq-constraint lhs rhs)
     (node/p (varargs list
               (constrainable->sexp lhs)
               (symbol-sexp/p #"=")
               (constrainable->sexp rhs)))]
    [(star-kind-constraint (uvar id))
     (node/p (varargs list
               (node/p (varargs list (symbol-sexp/p #"uvar") (number-sexp/p id)))
               (symbol-sexp/p #"=")
               (symbol-sexp/p #"*")))]))

(define (constrainable->sexp [c : Constrainable]) : Sexp/P
  (case c
    [(constrain-uvar (uvar id))
     (node/p (varargs list (symbol-sexp/p #"uvar") (number-sexp/p id)))]
    [(constrain-var-type v)
     (symbol-sexp/p v)]
    [(constrain-abstract-type mod-name name cs kind)
     (node/p (cons (mod-name->sexp mod-name)
                   (cons (bytes-sexp/p name)
                         (cons (kind->sexp kind)
                               (map constrainable->sexp cs)))))]
    [(constrain-fun-type args result)
     (node/p (append
               (map constrainable->sexp args)
               (varargs list (symbol-sexp/p #"->") (constrainable->sexp result))))]))

(define (split-constraints [cs : (List Constraint)]) : (Tuple2 (List TypeConstraint)
                                                              (List KindConstraint))
  (partition-eithers
    (map
      (lambda ([c : Constraint]) : (Either TypeConstraint KindConstraint)
        (case c
          [(eq-constraint lhs rhs) (left (eq-constraint/type lhs rhs))]
          [(star-kind-constraint v) (right (star-kind-constraint/kind v))]))
      cs)))
