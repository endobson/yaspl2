#:module (compiler-main)
#:import {
  (bytes)
  (compiler)
  (either)
  (io)
  (linker)
  (list)
  (machine-code-writer)
  (maybe)
  (module-name)
  (prim)
  (prim-language-compiler)
  (system-abi)
  (top-level-name)
  (tuples)
  (x86-64-runtime)
  (yaspl frontend errors)
  (yaspl parser)
  (yaspl top-level-objects-to-x86-64)
  (yaspl x86-64-assembly)
}
(export
  (#:values main))
(types
  (define-type Args
    (args
      [abi : SystemAbi]
      [object-file-name : Bytes]
      [binary-file-name : Bytes]
      [module-name : Bytes]
      [module-file-names : (List Bytes)])))

(define (split-module-files [module-files : (List Bytes)]) : (Tuple2 (List Bytes) (List Bytes))
  (partition-eithers
    (map
      (lambda ([file : Bytes]) : (Either Bytes Bytes)
        (if (ends-with? file #".yaspl")
            (left file)
            (right file)))
      module-files)))

(define (compile-prim-files [prim-files : (List Bytes)] [abi : SystemAbi])
  : (Either Bytes AssemblyFragments)
  (compile-prim-files* prim-files abi (empty)))


(define (compile-prim-files* [prim-files : (List Bytes)] [abi : SystemAbi] [acc : (List TextFragment)])
  : (Either Bytes AssemblyFragments)
  (case prim-files
    [(empty)
     (right (assembly-fragments acc (empty) (empty)))]
    [(cons prim-file prim-files)
     (case (compile-prim-program (sourced-bytes-from-file prim-file) abi)
       [(left v)
        (left v)]
       [(right text-fragments)
        (compile-prim-files* prim-files abi (append text-fragments acc))])]))

(define (parse-args [input : (List Bytes)]) : (Either Bytes Args)
  (case input
    [(empty)
     (left #"No executable specified???")]
    [(cons _ (empty))
     (left #"No abi supplied.")]
    [(cons _ (cons _ (empty)))
     (left #"No object file name supplied.")]
    [(cons _ (cons _ (cons _ (empty))))
     (left #"No binary file name supplied.")]
    [(cons _ (cons _ (cons _ (cons _(empty)))))
     (left #"No module name supplied")]
    [(cons _ (cons abi (cons object-file-name (cons binary-file-name
                                                    (cons module-name module-files)))))
     (case (parse-system-abi abi)
       [(nothing) (left (bytes-append (varargs list #"Unknown abi:" abi)))]
       [(just abi)
        (right (args abi object-file-name binary-file-name module-name module-files))])]))


(define (run [input : (List Bytes)]) : (Maybe Bytes)
  (case (parse-args input)
    [(left v) (just v)]
    [(right (args abi object-file-name binary-file-name module-name module-files))
     (match-define (tuple2 yaspl-files prim-files) (split-module-files module-files))
     (case (parse-module-files yaspl-files)
       [(left v) (just (parser-error->bytes v))]
       [(right modules)
        (case (compile-modules modules)
          [(left v) (just (module-compilation-error->bytes v))]
          [(right code)
           (case (compile-prim-files prim-files abi)
             [(left v) (just v)]
             [(right prim-fragments)
              (let ([assembly
                      (append-fragments
                        prim-fragments
                        (append-fragments
                          (yaspl-runtime
                            (top-level-name (mod-name (varargs list module-name)) #"main_closure")
                            abi)
                          (convert-objects code)))])
                (begin
                  (write-library-object-file
                    assembly
                    abi
                    object-file-name)
                  (link-files (varargs list object-file-name) abi binary-file-name)))])])])]))

(define (main [args : Bytes] [stdin : InputPort] [stdout : OutputPort] [stderr : OutputPort]) : Int
  (case (run (args->list args))
    [(just v)
     (begin
       (write-all-bytes v stderr)
       1)]
    [(nothing)
     0]))
