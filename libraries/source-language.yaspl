(module source-language
  (import
    (prim
      #:types (Bytes InputPort OutputPort Byte Boolean)
      #:values (panic)
      #:patterns ())
    (sexp-parser
      #:types (Sexp)
      #:values (parse-sexp)
      #:patterns (node symbol-sexp number-sexp bytes-sexp keyword-sexp boolean-sexp))
    (io write-all-bytes read-all-bytes)
    (either
      #:types (Either)
      #:values (left right)
      #:patterns (left right))
    (maybe
      #:types (Maybe)
      #:values (just nothing)
      #:patterns ())
    (list
      #:types (List)
      #:values (cons empty map)
      #:patterns (cons empty)))
  (export main module-name module-types module-definitions parse-module function-definition-name
          type-definition-variants)
  (types
    (define-type Module
      (module [name Bytes]
              [imports (List Imports)]
              [exports (List Bytes)]
              [types (List TypeDefinition)]
              [definitions (List FunctionDefinition)]))
    (define-type Imports
      (imports [module-name Bytes] [types (List Import)] [values (List Import)] [patterns (List Import)]))
    (define-type Import
      (import [exported-name Bytes] [local-name Bytes]))
    (define-type TypeDefinition
      (type-definition [name Bytes] [vars (Maybe (List Bytes))] [variants (List VariantDefinition)]))
    (define-type VariantDefinition
      (variant-definition [name Bytes] [fields (List VariantField)]))
    (define-type VariantField
      (variant-field [name Bytes] [type PreType]))
    (define-type PreType
      (var-pre-type [v Bytes])
      (fun-pre-type [type-vars (List Bytes)] [args (List PreType)] [result PreType])
      (type-app-pre-type [constructor Bytes] [args (List PreType)]))
    (define-type FunctionDefinition
      (function-definition [name Bytes] [type PreType] [args (List Bytes)] [body Expression]))

    (define-type Expression
      (byte-literal [v Byte])
      (bytes-literal [v Bytes])
      (boolean-literal [v Boolean])
      (var-expr [v Bytes])
      (if-expr [cond Expression] [true Expression] [false Expression])
      (begin-expr [first-expr Expression] [exprs (List Expression)])
      (app-expr [op Expression] [args (List Expression)])
      (let-expr [name Bytes] [expr Expression] [body Expression])
      (case-expr [expr Expression] [clauses (List CaseClause)]))

    (define-type CaseClause
      (case-clause [pattern Pattern] [expr Expression]))

    (define-type Pattern
      (bytes-pattern [v Bytes])
      (variable-pattern [v Bytes])
      (ignore-pattern)
      (abstraction-pattern [name Bytes] [patterns (List Pattern)])))

  (define (parse-imports/top [sexp : Sexp]) : (List Imports)
    (case sexp
      [(node (cons (symbol-sexp #"import") imports))
       (map parse-imports imports)]
      [_ (panic #"Not a valid imports section")]))

  (define (parse-imports [sexp : Sexp]) : Imports
    (case sexp
      [(node (cons (symbol-sexp mod-name) inner))
       (case inner
         [(cons (keyword-sexp #"types")
            (cons (node type-imports)
              (cons (keyword-sexp #"values")
               (cons (node value-imports)
                  (cons (keyword-sexp #"patterns")
                    (cons (node pattern-imports)
                      (empty)))))))
          (imports
            mod-name
            (map parse-import type-imports)
            (map parse-import value-imports)
            (map parse-import pattern-imports))]
         [_
          (imports
            mod-name
            (empty)
            (map parse-import inner)
            (empty))])]
      [_ (panic #"Not a valid imports clause")]))

  (define (parse-import [sexp : Sexp]) : Import
    (case sexp
      [(symbol-sexp v) (import v v)]
      [(node (cons (symbol-sexp external-name) (cons (symbol-sexp local-name) (empty))))
       (import external-name local-name)]
      [_ (panic #"Not a valid import clause")]))


  (define (parse-exports/top [sexp : Sexp]) : (List Bytes)
    (case sexp
      [(node (cons (symbol-sexp #"export") exported-symbols))
       (map parse-exported-symbol exported-symbols)]
      [_ (panic #"Bad exports")]))

  (define (parse-exported-symbol [sexp : Sexp]) : Bytes
    (case sexp
      [(symbol-sexp bytes) bytes]
      [_ (panic #"Not a valid export")]))



  (define (parse-types/top [sexp : Sexp]) : (List TypeDefinition)
    (case sexp
      [(node (cons (symbol-sexp #"types") type-definitions))
       (map parse-type-definition type-definitions)]
      [_ (panic #"Bad types")]))

  (define (parse-type-definition [sexp : Sexp]) : TypeDefinition
    (case sexp
      [(node (cons (symbol-sexp #"define-type") (cons type-name/vars variants)))
       (type-definition
         (extract-type-name type-name/vars)
         (extract-type-vars type-name/vars)
         (map parse-variant variants))]
      [_ (panic #"Bad type definition")]))

  (define (parse-variant [sexp : Sexp]) : VariantDefinition
    (case sexp
      [(node (cons (symbol-sexp variant-name) fields))
       (variant-definition variant-name (map parse-variant-field fields))]
      [_ (panic #"Bad variant definition")]))

  (define (parse-variant-field [sexp : Sexp]) : VariantField
    (case sexp
      [(node (cons (symbol-sexp field-name) (cons type (empty))))
       (variant-field field-name (parse-pre-type type))]
      [_ (panic #"Bad variant field")]))

  (define (extract-type-name [sexp : Sexp]) : Bytes
    (case sexp
      [(symbol-sexp type-name)
       type-name]
      [(node (cons (symbol-sexp type-name) _))
       type-name]
      [_ (panic #"Bad type-name/vars")]))

  (define (extract-type-vars [sexp : Sexp]) : (Maybe (List Bytes))
    (case sexp
      [(symbol-sexp _)
       (nothing)]
      [(node (cons _ type-var-sexps))
       (just (map parse-type-var-symbol type-var-sexps))]
      [_ (panic #"Bad type-name/vars")]))

  (define (parse-type-var-symbol [sexp : Sexp]) : Bytes
    (case sexp
      [(symbol-sexp bytes) bytes]
      [_ (panic #"Not a valid type variable")]))


  (define (parse-pre-type [sexp : Sexp]) : PreType
    (case sexp
      [(symbol-sexp v) (var-pre-type v)]
      ;; TODO Make function parsing better
      [(node (cons arg (cons (symbol-sexp #"->") (cons result (empty)))))
       (fun-pre-type (empty) (cons (parse-pre-type arg) (empty)) (parse-pre-type result))]
      [(node (cons (symbol-sexp constructor) args))
       (type-app-pre-type constructor (map parse-pre-type args))]
      [_ (panic #"Not a valid Type")]))

  (define (parse-function-definition [sexp : Sexp]) : FunctionDefinition
    (case sexp
      [(node (cons (symbol-sexp #"define")
                   (cons (node type-vars)
                         (cons (node (cons (symbol-sexp function-name) args))
                               (cons (symbol-sexp #":")
                                     (cons result-type (cons body (empty))))))))
       (function-definition
         function-name
         (fun-pre-type
           (map parse-type-var-symbol type-vars)
           (map parse-arg-type args)
           (parse-pre-type result-type))
         (map parse-arg-name args)
         (parse-expression body))]
      [(node (cons (symbol-sexp #"define")
                   (cons (node (cons (symbol-sexp function-name) args))
                         (cons (symbol-sexp #":")
                               (cons result-type (cons body (empty)))))))
       (function-definition
         function-name
         (fun-pre-type
           (empty)
           (map parse-arg-type args)
           (parse-pre-type result-type))
         (map parse-arg-name args)
         (parse-expression body))]
      [_ (panic #"Not a valid function definition")]))

  (define (parse-arg-type [sexp : Sexp]) : PreType
    (case sexp
      [(node (cons (symbol-sexp _) (cons (symbol-sexp #":") (cons type (empty)))))
       (parse-pre-type type)]
      [_ (panic #"Not a valid argument")]))
  (define (parse-arg-name [sexp : Sexp]) : Bytes
    (case sexp
      [(node (cons (symbol-sexp name) (cons (symbol-sexp #":") (cons _ (empty)))))
       name]
      [_ (panic #"Not a valid argument")]))

  (define (parse-expression [sexp : Sexp]) : Expression
    (case sexp
      [(number-sexp v) (byte-literal v)]
      [(bytes-sexp v) (bytes-literal v)]
      [(boolean-sexp v) (boolean-literal v)]
      [(symbol-sexp v) (var-expr v)]
      [(node (cons (symbol-sexp #"if") (cons cond (cons true (cons false (empty))))))
       (if-expr (parse-expression cond) (parse-expression true) (parse-expression false))]
      [(node (cons (symbol-sexp #"begin") (cons first-expr exprs)))
       (begin-expr (parse-expression first-expr) (map parse-expression exprs))]
      [(node (cons (symbol-sexp #"let")
                   (cons (node (cons (node (cons (symbol-sexp name) (cons expr (empty)))) (empty)))
                         (cons body (empty)))))
       (let-expr name (parse-expression expr) (parse-expression body))]
      [(node (cons (symbol-sexp #"case") (cons expr clauses)))
       (case-expr (parse-expression expr) (map parse-case-clause clauses))]
      [(node (cons op-expr arg-exprs))
       (app-expr (parse-expression op-expr) (map parse-expression arg-exprs))]
      [(node (empty))
       (panic #"Application needs an operator")]
      [(keyword-sexp _)
       (panic #"Keywords are not valid expressions")]))

  (define (parse-case-clause [sexp : Sexp]) : CaseClause
    (case sexp
      [(node (cons pattern (cons expr (empty))))
       (case-clause (parse-pattern pattern) (parse-expression expr))]
      [_ (panic #"Not a valid case clause")]))

  (define (parse-pattern [sexp : Sexp]) : Pattern
    (case sexp
      [(symbol-sexp #"_") (ignore-pattern)]
      [(symbol-sexp sym) (variable-pattern sym)]
      [(bytes-sexp v) (bytes-pattern v)]
      [(node (cons (symbol-sexp name) patterns))
       (abstraction-pattern name (map parse-pattern patterns))]
      [_ (panic #"Not a valid pattern")]))


  (define (parse-module [sexp : Sexp]) : (Either Bytes Module)
    (case sexp
      [(node (cons (symbol-sexp #"module")
                   (cons (symbol-sexp name)
                         (cons imports
                               (cons exports
                                     (cons types
                                           definitions))))))
       (right
         (module name
                 (parse-imports/top imports)
                 (parse-exports/top exports)
                 (parse-types/top types)
                 (map parse-function-definition definitions)))]
      [_ (left #"Bad module form")]))


  (define (a) (extract-either [either : (Either Bytes a)]) : a
    (case either
      [(right v) v]
      [(left b) (panic b)]))

  (define (main [stdin : InputPort] [stdout : OutputPort] [stderr : OutputPort]) : Byte
    (case (parse-module (extract-either (parse-sexp (read-all-bytes stdin))))
      [(right _) 0]
      [(left bytes)
       (begin
         (write-all-bytes bytes stderr)
         1)])))

