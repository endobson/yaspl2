#:module (totality-checker)
#:import {
  (bytes)
  (either)
  (list)
  (maybe)
  (module-signature)
  (prim)
  (resolved-types-language)
  (tuples)
  (variant-info)
}
(export
  (#:types)
  (#:values totality-check-patterns)
  (#:patterns))
(types
  (define-type AbstractValue
    (any-value)
    (full-variants [name : (List Bytes)])
    (variant [name : Bytes] [fields : (List AbstractValue)]))
  (define-type MatchResult
    (match-result
      [unmatched-values : (List AbstractValue)]
      [matched-values : (List AbstractValue)]
      [any-matches : Boolean])))

(define (totality-check-patterns [patterns : (List Pattern)]) : (Maybe Bytes)
  (case (check-patterns* patterns (varargs list (any-value)))
    [(left v) (just v)]
    [(right (empty)) (nothing)]
    [(right values)
     (just (bytes-append
                   (cons #"Patterns are not exhaustive.\n"
                         (interleave (map abstract-value->bytes values) #"\n"))))]))

(define (abstract-value->bytes [v : AbstractValue]) : Bytes
  (case v
    [(any-value) #"*"]
    [(full-variants variant-names)
     (bytes-append
       (append
         (varargs list #"([")
         (append
           (interleave variant-names #", ")
           (varargs list #"] ...)"))))]
    [(variant variant-name fields)
     (bytes-append
       (append
         (varargs list #"(" variant-name)
         (append
           (map (lambda ([v : AbstractValue])
                  (bytes-append (varargs list #" " (abstract-value->bytes v))))
                fields)
           (varargs list #")"))))]))

(define (check-patterns* [patterns : (List Pattern)] [vals : (List AbstractValue)]) :
  (Either Bytes (List AbstractValue))
  (case patterns
    [(empty) (right vals)]
    [(cons pat patterns)
     (case vals
       [(empty)
        (left #"No values left for patterns to match")]
       [_
        (match-define (match-result unmatched-vals _ matched) (abstract-match/values pat vals))
        (if matched
            (check-patterns* patterns unmatched-vals)
            (left #"Pattern didn't match any values"))])]))

(define (abstract-match/values [pat : Pattern] [vals : (List AbstractValue)])
  : MatchResult
  (abstract-match/values* pat vals (empty) (empty) #f))

(define (abstract-match/values*
          [pat : Pattern] [vals : (List AbstractValue)]
          [acc-unmatched-values : (List AbstractValue)]
          [acc-matched-values : (List AbstractValue)]
          [acc-matched : Boolean])
  : MatchResult
  (case vals
    [(empty) (match-result acc-unmatched-values acc-matched-values acc-matched)]
    [(cons val vals)
     (match-define (match-result unmatched-values matched-values matched)
       (abstract-match pat val))
     (abstract-match/values* pat vals
                             (append unmatched-values acc-unmatched-values)
                             (append matched-values acc-matched-values)
                             (or matched acc-matched))]))

(define (find/remove [e : Bytes] [l : (List Bytes)]) : (Maybe (Tuple2 Bytes (List Bytes)))
  (case l
    [(empty) (nothing)]
    [(cons e2 l)
     (if (bytes=? e e2)
         (just (tuple2 e2 l))
         (case (find/remove e l)
           [(nothing) (nothing)]
           [(just (tuple2 e l))
            (just (tuple2 e (cons e2 l)))]))]))



(define (abstract-match/vec [variant-name : Bytes]
                            [pat/vals : (List (Tuple2 Pattern AbstractValue))])
  : MatchResult
  (abstract-match/vec* variant-name (reverse pat/vals) (empty) (varargs list (empty)) #t))


(define (abstract-match/vec* [variant-name : Bytes]
                             [pat/vals : (List (Tuple2 Pattern AbstractValue))]
                             [unmatched-vecs : (List (List AbstractValue))]
                             [matched-vecs : (List (List AbstractValue))]
                             [acc-matched : Boolean])
  : MatchResult
  (case pat/vals
    [(empty)
     (match-result
       (map (lambda ([vs : (List AbstractValue)]) (variant variant-name vs))
            unmatched-vecs)
       (map (lambda ([vs : (List AbstractValue)]) (variant variant-name vs))
            matched-vecs)
       acc-matched)]
    [(cons (tuple2 pat val) pat/vals)
     (match-define (match-result unmatched-vals matched-vals matched)
       (abstract-match pat val))
     (abstract-match/vec*
       variant-name
       pat/vals
       (append
         (append
           (cons* unmatched-vals unmatched-vecs)
           (cons* matched-vals unmatched-vecs))
         (cons* unmatched-vals matched-vecs))
       (cons* matched-vals matched-vecs)
       (and matched acc-matched))]))

(define [A] (cons* [heads : (List A)] [tails : (List (List A))]) : (List (List A))
  (append*
    (reverse-map
      (lambda ([head : A])
        (reverse-map
          (lambda ([tail : (List A)])
            (cons head tail))
          tails))
      heads)))

(define (abstract-match [pattern : Pattern] [val : AbstractValue])
  : MatchResult
  (case pattern
    [(bytes-pattern _ _)
     (match-result (varargs list val) (empty) #t)]
    [(int-pattern _ _)
     (match-result (varargs list val) (empty) #t)]
    [(variable-pattern _ _)
     (match-result (empty) (varargs list val) #t)]
    [(ignore-pattern _)
     (match-result (empty) (varargs list val) #t)]
    [(abstraction-pattern (pattern-signature (variant-info matched-name _) _ _ _ _
                                             (type-signature all-variants))
                          patterns _)
     (case val
       [(any-value)
        (abstract-match pattern (full-variants all-variants))]
       [(full-variants var-names)
        (case (find/remove matched-name var-names)
          [(just (tuple2 var-name var-names))
           (case var-names
             [(empty)
              (abstract-match/vec var-name
                (map (lambda ([p : Pattern]) (tuple2 p (any-value))) patterns))]
             [_
               (case (abstract-match/vec var-name
                       (map (lambda ([p : Pattern]) (tuple2 p (any-value))) patterns))
                 [(match-result unmatched-values matched-values matched)
                  (match-result
                    (cons (full-variants var-names) unmatched-values)
                    matched-values
                    matched)])])]
          [(nothing)
           (match-result (varargs list val) (empty) #f)])]
       [(variant var-name fields)
        (if (bytes=? matched-name var-name)
            (case (zip patterns fields)
              [(nothing) (panic #"Some pattern doesn't match its spec")]
              [(just pat/vals)
               (abstract-match/vec var-name pat/vals) ])
            (match-result (varargs list val) (empty) #f))])]))
