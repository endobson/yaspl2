(module dict
  (import
    (prim
      #:types (Boolean)
      #:values ()
      #:patterns ())
    (maybe
      #:types (Maybe)
      #:values (just nothing)
      #:patterns ())
    (tuples
      #:types (Tuple2)
      #:values (tuple2)
      #:patterns (tuple2))
    (list
      #:types (List)
      #:values (cons empty)
      #:patterns (cons empty)))
  (export make-dict dict-set dict-ref)
  (types
    (define-type (Dict a b)
      (dict [eq (a a -> Boolean)] [vs (List (Tuple2 a b))])))

  (define (a b) (make-dict [eq : (a a -> Boolean)]) : (Dict a b)
    (dict eq (empty)))

  (define (a b) (dict-ref [d : (Dict a b)] [k : a]) : (Maybe b)
    (case d
      [(dict eq nodes) (dict-ref-helper eq nodes k)]))

  (define (a b) (dict-ref-helper [eq : (a a -> Boolean)] [nodes : (List (Tuple2 a b))]  [k : a]) : (Maybe b)
    (case nodes
      [(empty) (nothing)]
      [(cons (tuple2 a b) nodes)
       (if (eq a k) (just b) (dict-ref-helper eq nodes k))]))

  (define (a b) (dict-set [d : (Dict a b)] [k : a] [v : b]) : (Dict a b)
    (case d
      [(dict eq nodes)
       (dict eq (dict-set-helper eq nodes k v))]))

  (define (a b) (dict-set-helper [eq : (a a -> Boolean)] [nodes : (List (Tuple2 a b))]
                                 [k : a] [v : b]) : (List (Tuple2 a b))
    (case nodes
      [(empty) (cons (tuple2 k v) (empty))]
      [(cons (tuple2 a b) nodes)
       (if (eq a k)
           (cons (tuple2 k v) nodes)
           (cons (tuple2 a b) (dict-set-helper eq nodes k v)))])))
