#:module (intermediate-to-module-signature)
#:import {
  (bytes)
  (dict)
  (intermediate-language)
  (list)
  (module-signature)
  (prim)
}
(export
  (#:values module->module-signature))
(types)

(define (module->module-signature [mod : Module]) : ModuleSignature
  (let ([exports (module-exports mod)])
    (module-signature
      (module-name mod)
      (make-exports (exports-values exports) (make-dict bytes-cmp))
      (make-pattern-exports (exports-patterns exports) (make-dict bytes-cmp))
      (make-type-exports (exports-types exports) (make-dict bytes-cmp))
      (make-static-exports (exports-statics exports) (make-dict bytes-cmp))
      (module-static-info mod))))

(define (make-exports
          [exports : (List ValueExport)]
          [acc : (Dict Bytes ValueSignature)])
  : (Dict Bytes ValueSignature)
  (case exports
    [(empty) acc]
    [(cons (value-export exported-name top-level-name type) exports)
     (make-exports
       exports
       (dict-add acc exported-name (value-signature type top-level-name)))]))

(define (make-pattern-exports
          [exports : (List PatternExport)]
          [acc : (Dict Bytes PatternSignature)]) : (Dict Bytes PatternSignature)
  (case exports
    [(empty) acc]
    [(cons (pattern-export name sig) exports)
     (make-pattern-exports exports (dict-add acc name sig))]))


(define (make-type-exports
          [exports : (List TypeExport)]
          [acc : (Dict Bytes TypeSignature)])
  : (Dict Bytes TypeSignature)
  (case exports
    [(empty) acc]
    [(cons (type-export exported-name type) exports)
     (make-type-exports exports (dict-add acc exported-name (type-signature type)))]))

(define (make-static-exports
          [exports : (List StaticExport)]
          [acc : (Dict Bytes StaticSignature)])
  : (Dict Bytes StaticSignature)
  (case exports
    [(empty) acc]
    [(cons (varargs-export exported-name type-vars arg-type return-type cons-name empty-name) exports)
     (make-static-exports
       exports
       (dict-add acc exported-name (varargs-signature type-vars arg-type return-type cons-name
                                                      empty-name)))]))
